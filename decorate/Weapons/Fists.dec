Actor AltPunchCharge : Ammo
{
inventory.maxamount 100
Inventory.Icon "PUNXA0"
}

Actor 64Fist : Fist Replaces Fist
{
Weapon.SlotNumber 1
Weapon.SlotPriority 1
Weapon.SelectionOrder 3700
Obituary "$FO_OB_MPFIST"
Tag "$FO_TAG_FIST"
Inventory.Icon "PUNXA0"
VisibleToPlayerClass "64DoomPlayer"
Inventory.ForbiddenTo "Tactical64DoomPlayer","Tactical64DoomPlayer_P","Modern"
+INVENTORY.RESTRICTABSOLUTELY
+INVENTORY.UNDROPPABLE
+WEAPON.NOALERT
+WEAPON.MELEEWEAPON
	States
	{
	Ready:
		PUN1 A 1 A_WeaponReady
		Loop
	Select:
		PUN1 A 1 A_Raise
		WAIT
	Deselect:
		PUN1 A 1 A_Lower
		Wait
	PullUp:
		TNT1 A 1 Offset(0,99)
		PUN1 A 1 Offset(0,99)
		PUN1 A 1 Offset(0,65)
		PUN1 A 1 Offset(0,53)
		PUN1 A 1 Offset(0,39)
		PUN1 A 1 Offset(0,35)
		TNT1 A 0 A_TakeInventory("64Fist2",1)
		Goto Ready
	Fire:
		TNT1 A 0 A_JumpIfInventory("64Fist2",1,"Punk")
		PUN1 A 1 Offset(0,53)
		TNT1 A 1 Offset(0,32)
		Goto Punk2
	Punk:
		TNT1 A 2
	Punk2:
		TNT1 A 0 A_Jump(256,"Punch1","Punch2")
	Punch1:
		TNT1 A 0 A_JumpIfInventory("powerstrength",1,"BerserkPunch1")
		TNT1 A 0 A_PlaySound("world/swing",CHAN_AUTO)
		PUNG EF 1
		TNT1 A 0 A_FireCustomMissile("PunchAttack",0,0,0,0)
		PUNG GHIJKL 1
		TNT1 A 0 A_GiveInventory("64Fist2",1)
		TNT1 A 1
		TNT1 A 7 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		TNT1 A 0 A_ReFire
		Goto PullUp
	Punch2:
		TNT1 A 0 A_JumpIfInventory("powerstrength",1,"BerserkPunch2")
		TNT1 A 0 A_PlaySound("world/swing",CHAN_AUTO)
		PUNG PQ 1
		TNT1 A 0 A_FireCustomMissile("PunchAttack",0,0,0,0)
		PUNG RSTUVW 1
		TNT1 A 0 A_GiveInventory("64Fist2",1)
		TNT1 A 1
		TNT1 A 7 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		TNT1 A 0 A_ReFire
		Goto PullUp
	BerserkPunch1:
		PUNG EF 1
		TNT1 A 0 A_FireCustomMissile("SuperPunchAttack",0,0,0,0)
		TNT1 A 0 A_PlaySound("world/swingB",CHAN_AUTO)
		PUNG GHIJKL 1
		TNT1 A 0 A_GiveInventory("64Fist2",1)
		TNT1 A 1
		TNT1 A 7 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		TNT1 A 0 A_ReFire
		Goto PullUp
	BerserkPunch2:
		TNT1 A 0 A_PlaySound("world/swingB",CHAN_AUTO)
		PUNG PQ 1
		TNT1 A 0 A_FireCustomMissile("SuperPunchAttack",0,0,0,0)
		PUNG RSTUVW 1
		TNT1 A 0 A_GiveInventory("64Fist2",1)
		TNT1 A 1
		TNT1 A 7 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		TNT1 A 0 A_ReFire
		Goto PullUp
	}
}

actor 64Fist2 : Inventory
{
Inventory.MaxAmount 1
+UNDROPPABLE
}
actor 64Fist3 : Inventory
{
Inventory.MaxAmount 1
+UNDROPPABLE
}
Actor 64FistTactical : Brutal64Weapon
{
Weapon.SlotNumber 1
Weapon.SlotPriority 1
Weapon.SelectionOrder 3700
Obituary "$FO_OB_MPFIST"
Tag "$FO_TAG_FIST"
Inventory.Icon "PUNXA0"
Inventory.forbiddento "64DoomPlayer"
Weapon.AmmoType "AltPunchCharge"
Weapon.AmmoUse 0
Weapon.AmmoGive 0
VisibleToPlayerClass "Tactical64DoomPlayer","Tactical64DoomPlayer_P","Modern"
+INVENTORY.UNDROPPABLE
+WEAPON.AMMO_OPTIONAL
+WEAPON.MELEEWEAPON
-WEAPON.NOAUTOFIRE
	States
	{
	MineThrowFlash:
		PUN6 EFGH 1
		TNT1 A 18
		PUN6 ABCD 1
		Stop
	KickingFlash:
		PUN6 CBA 1
		TNT1 A 10
		PUN6 ABC 1
		Stop
	AirKickingFlash:
		PUN6 CBA 1
		TNT1 A 12
		PUN6 ABC 1
		Stop
	SlideKickingStart:
		PUN6 CBA 1
		TNT1 A 20
	SlideKickingEnd:
		TNT1 A 7
		PUN6 ABC 1
		Stop
	v1Ready:
	Ready:
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		PUN1 A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		Loop
	Tired:
		TNT1 A 0 A_Print("$WM_NOSTAMINA",1)
		TNT1 A 0 A_PlaySound("Tired",CHAN_VOICE)
		Goto PullUp
	AltTired:
		TNT1 A 0 A_Print("$WM_NOSTAMINA",1)
		TNT1 A 0 A_PlaySound("Tired",CHAN_VOICE)
		TNT1 A 0 A_TakeInventory("AltPunchCharge",0)
		Goto PullUp
	Select:
		PUN6 ABCD 1 Offset(0,32)
		TNT1 A 0 A_Raise
		Wait
	Deselect:
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Takeinventory("WeaponSpecialAction",0)
		TNT1 A 0 A_Takeinventory("TossMine",0)
		TNT1 A 0 A_Takeinventory("PlaceMine",0)
		TNT1 A 0 A_Takeinventory("Kicking",0)
		TNT1 A 0 A_TakeInventory("AltPunchCharge",0)
		PUN6 DCBA 1
		TNT1 A 0 A_Lower
		Wait
	PullUp:
		TNT1 A 0 A_TakeInventory("64FistUpperToken",1)
		TNT1 A 1 Offset(0,99)
		PUN1 A 1 Offset(0,99)
		PUN1 A 1 Offset(0,65)
		PUN1 A 1 Offset(0,53)
		PUN1 A 1 Offset(0,39)
		PUN1 A 1 Offset(0,35)
		TNT1 A 0 A_TakeInventory("64Fist2",1)
		TNT1 A 0 A_TakeInventory("64Fist3",1)
		Goto Ready
	PullUp2:
		TNT1 A 0 A_TakeInventory("64FistUpperToken",1)
		TNT1 A 0 Offset(0,99)
		TNT1 A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM|WRF_NOBOB)
		TNT1 A 0 Offset(0,99) 
		PUN1 A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM|WRF_NOBOB)
		TNT1 A 0 Offset(0,65)
		PUN1 A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM|WRF_NOBOB)
		TNT1 A 0 Offset(0,53)
		PUN1 A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM|WRF_NOBOB)
		TNT1 A 0 Offset(0,39)
		PUN1 A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM|WRF_NOBOB)
		TNT1 A 0 Offset(0,35)
		PUN1 A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM|WRF_NOBOB)
		TNT1 A 0 A_TakeInventory("64Fist2",1)
		TNT1 A 0 A_TakeInventory("64Fist3",1)
		Goto Ready
	Fire:
		TNT1 A 0 A_JumpIfInventory("64Fist2",1,"TheLeft")
		TNT1 A 0 A_JumpIfInventory("64Fist3",1,"UpperWait")
		PUN1 A 1 Offset(0,35)
		PUN1 A 1 Offset(0,39)
		PUN1 A 1 Offset(0,53)
		PUN1 A 1 Offset(0,65)
		PUN1 A 1 Offset(0,99)
		TNT1 A 1 Offset(0,32)
		Goto TheLeft2
	TheLeft:
		TNT1 A 5 Offset(-1,32)
	TheLeft2:
		TNT1 A 0 A_JumpIfInventory("istacticalclass",1,2)
		TNT1 A 0 A_JumpIfInventory("isModern",1,3)
		Goto punch1
		TNT1 A 0 A_JumpIfInventory("Stamina",91,"Tired")
		TNT1 A 0 A_GiveInventory("Stamina",9)
	Punch1:
		TNT1 A 0 A_JumpIfInventory("powerstrength",1,"BerserkPunch1")
		TNT1 A 0 A_PlaySound("world/swing",CHAN_AUTO)
		PUNG EF 2
		TNT1 A 0 A_FireCustomMissile("PunchAttack",0,0,0,-7)
		PUNG GHIJ 1
		TNT1 A 0 A_GiveInventory("64Fist2",1)
		PUNG KL 2
		Goto PullUp2
	Altfire:
		TNT1 A 0 A_JumpIfInventory("64Fist3",1,"Altfire2")
		TNT1 A 0 A_JumpIfInventory("64Fist2",1,"The")
		PUN1 A 1 Offset(0,35)
		PUN1 A 1 Offset(0,39)
		PUN1 A 1 Offset(0,53)
		PUN1 A 1 Offset(0,65)
		PUN1 A 1 Offset(0,99)
		TNT1 A 1 Offset(0,32)
	Punch2P:
		TNT1 A 1 A_WeaponReady(WRF_NOFIRE)
		TNT1 A 0 A_Refire("Punch2P")
		Goto The
	Altfire2:
		PUN1 OPQ 1
		TNT1 A 6
	The:
		TNT1 A 0 A_JumpIfInventory("istacticalclass",1,2)
		TNT1 A 0 A_JumpIfInventory("isModern",1,3)
		Goto punch2
		TNT1 A 0 A_JumpIfInventory("Stamina",91,"Tired")
		TNT1 A 0 A_GiveInventory("Stamina",9)
	Punch2:
		TNT1 A 0 A_JumpIfInventory("powerstrength",1,"BerserkPunch2")
		TNT1 A 0 A_PlaySound("world/swing",CHAN_AUTO)
		PUNG PQ 2 A_WeaponReady(WRF_NOFIRE)
		TNT1 A 0 A_FireCustomMissile("PunchAttack",0,0,0,-7)
		PUNG RSTU 1
		TNT1 A 0 A_TakeInventory("64Fist2",1)
		TNT1 A 0 A_GiveInventory("64Fist3",1)
		PUNG WV 2 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		Goto PullUp2
	BerserkPunch1:
		TNT1 A 0 A_PlaySound("world/swingB",CHAN_AUTO)
		PUNG EF 1
		TNT1 A 0 A_FireCustomMissile("SuperPunchAttack",0,0,0,-7)
		PUNG GHIJ 1
		TNT1 A 0 A_GiveInventory("64Fist2",1)
		PUNG K 1
		PUNG L 2
		Goto PullUp2
	BerserkPunch2:
		TNT1 A 0 A_PlaySound("world/swingB",CHAN_AUTO)
		PUNG PQ 1 Offset(-1,32)
		TNT1 A 0 A_FireCustomMissile("SuperPunchAttack",0,0,0,-7)
		PUNG RSTU 1
		TNT1 A 0 A_TakeInventory("64Fist2",1)
		TNT1 A 0 A_GiveInventory("64Fist3",1)
		PUNG W 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		PUNG V 2 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		Goto PullUp2
	UpperWait:
		TNT1 A 6 A_WeaponReady(WRF_NOFIRE)
		TNT1 A 0 A_ReFire("Upper")
		Goto AltEndPunchStamina
	AltEndPunchStamina:
		TNT1 A 0 A_GiveInventory("Stamina",8)
		Goto AltEndPunch
	Upper:
		PUN1 R 1 A_WeaponReady(WRF_NOFIRE)
		PUN1 STUVWXYZ 1
		TNT1 A 0 A_GiveInventory("64FistUpperToken",1)
		PUN0 ABC 1
		TNT1 A 0 A_ReFire("UpperHold")
	UpperHold:
		TNT1 A 0 A_JumpIfInventory("IsModern",1,3)
		TNT1 A 0 A_JumpIfInventory("Stamina",100,"AltTired")
		TNT1 A 0 A_GiveInventory("Stamina",1)
		TNT1 A 0 A_GiveInventory("AltPunchCharge",1)
		PUN0 C 1
		TNT1 A 0 A_Refire("UpperHold")
	AltEndPunch:
		TNT1 A 0 A_JumpIfInventory("AltPunchCharge",100,"AltEndPunch3")
		TNT1 A 0 A_JumpIfInventory("AltPunchCharge",50,"AltEndPunch2")
		TNT1 A 0 A_JumpIfInventory("PowerStrength",1,3)
		TNT1 A 0 A_PlaySound("world/swing",CHAN_AUTO)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_PlaySound("world/swingB",CHAN_AUTO)
		TNT1 A 0 A_TakeInventory("64FistUpperToken",1)
		PUN0 DEF 1
		TNT1 A 0 A_ZoomFactor(0.95)
		TNT1 A 0 A_JumpIfInventory("PowerStrength",1,"BerserkAltEndPunch")
		TNT1 A 0 A_FireCustomMissile("AltPunchAttack",0,0,0,-7)
		PUN0 GH 1
		TNT1 A 0 A_ZoomFactor(1.0)
		PUN0 IJKLM 1
		TNT1 A 0 A_TakeInventory("AltPunchCharge",0)
		Goto PullUp
	BerserkAltEndPunch:
		TNT1 A 0 A_ZoomFactor(0.95)
		TNT1 A 0 A_FireCustomMissile("SuperAltPunchAttack",0,0,0,-7)
		TNT1 A 0 A_RadiusThrust(5000,65,RTF_NOTMISSILE|RTF_NOIMPACTDAMAGE)
		TNT1 A 0 A_TakeInventory("64FistUpperToken",1)
		PUN0 GH 1
		TNT1 A 0 A_ZoomFactor(1.0)
		PUN0 IJKLM 1
		TNT1 A 0 A_TakeInventory("AltPunchCharge",0)
		Goto PullUp
	AltEndPunch2:
		TNT1 A 0 A_PlaySound("world/swing",CHAN_AUTO)
		PUN0 DEF 1
		TNT1 A 0 A_ZoomFactor(0.95)
		TNT1 A 0 A_JumpIfInventory("PowerStrength",1,"BerserkAltEndPunch2")
		TNT1 AA 0 A_FireCustomMissile("AltPunchAttack",0,0,0,-7)
		TNT1 A 0 A_RadiusThrust(750,65,RTF_NOTMISSILE|RTF_NOIMPACTDAMAGE)
		TNT1 A 0 A_TakeInventory("64FistUpperToken",1)
		PUN0 GH 1
		TNT1 A 0 A_ZoomFactor(1.0)
		PUN0 IJKLM 1
		TNT1 A 0 A_TakeInventory("AltPunchCharge",0)
		Goto PullUp
	BerserkAltEndPunch2:
		TNT1 A 0 A_ZoomFactor(0.95)
		TNT1 AA 0 A_FireCustomMissile("SuperAltPunchAttack",0,0,0,-7)
		TNT1 A 0 A_RadiusThrust(6000,65,RTF_NOTMISSILE|RTF_NOIMPACTDAMAGE)
		TNT1 A 0 A_TakeInventory("64FistUpperToken",1)
		PUN0 GH 1
		TNT1 A 0 A_ZoomFactor(1.0)
		PUN0 IJKLM 1
		TNT1 A 0 A_TakeInventory("AltPunchCharge",0)
		Goto PullUp
	AltEndPunch3:
		TNT1 A 0 A_PlaySound("world/swing",CHAN_AUTO)
		PUN0 DEF 1
		TNT1 A 0 A_ZoomFactor(0.95)
		TNT1 A 0 A_JumpIfInventory("PowerStrength",1,"BerserkAltEndPunch3")
		TNT1 AAA 0 A_FireCustomMissile("AltPunchAttack",0,0,0,-7)
		TNT1 A 0 A_RadiusThrust(1500,65,RTF_NOTMISSILE|RTF_NOIMPACTDAMAGE)
		TNT1 A 0 A_TakeInventory("64FistUpperToken",1)
		PUN0 GH 1
		TNT1 A 0 A_ZoomFactor(1.0)
		PUN0 IJKLM 1
		TNT1 A 0 A_TakeInventory("AltPunchCharge",0)
		Goto PullUp
	BerserkAltEndPunch3:
		TNT1 A 0 A_ZoomFactor(0.95)
		TNT1 AAA 0 A_FireCustomMissile("SuperAltPunchAttack",0,0,0,-7)
		TNT1 A 0 A_RadiusThrust(7000,65,RTF_NOTMISSILE|RTF_NOIMPACTDAMAGE)
		TNT1 A 0 A_TakeInventory("64FistUpperToken",1)
		PUN0 GH 1
		TNT1 A 0 A_ZoomFactor(1.0)
		PUN0 IJKLM 1
		TNT1 A 0 A_TakeInventory("AltPunchCharge",0)
		Goto PullUp
	Reload: //Knuckle animation(Tactical and Modern only)
		PUNS DE 2 A_WeaponReady(WRF_ALLOWZOOM)
		PUNS FG 1 A_WeaponReady(WRF_ALLOWZOOM)
		TNT1 A 0 A_PlaySound("Knuckled",CHAN_VOICE)
		PUNS J 4 A_WeaponReady(WRF_ALLOWZOOM)
		PUNS IHG 1 A_WeaponReady(WRF_ALLOWZOOM)
		PUNS KL 2 A_WeaponReady(WRF_ALLOWZOOM)
		PUNS MN 1 A_WeaponReady(WRF_ALLOWZOOM)
		TNT1 A 0 A_PlaySound("Knuckled",CHAN_VOICE)
		PUNS Q 4 A_WeaponReady(WRF_ALLOWZOOM)
		PUNS PON 1 A_WeaponReady(WRF_ALLOWZOOM)
		PUNS LK 2 A_WeaponReady(WRF_ALLOWZOOM)
		Goto Ready
	}
}

Actor PunchAttack : FastProjectile
{
Damage 6
DamageType "Melee"
Speed 31
Height 6
Radius 12
Alpha 0.6
RenderStyle Add
SeeSound "none"
DeathSound "none"
Decal "none"
Obituary "$FO_OB_MPFIST"
+NOEXTREMEDEATH
	States
	{
	Spawn:
		TNT1 A 1
		Stop
	Death:
		TNT1 A 0 A_PlaySound("weapons/melee/heavy",CHAN_ITEM)
		TNT1 AAAAAA 0 A_CustomMissile("MeleeSmoke",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 Radius_Quake(2,6,0,5,0)
		TNT1 A 10
		Stop
	}
}

Actor SuperPunchAttack : PunchAttack
{
Damage 18	//*3 more than regular punch
DamageType "SuperFists"
Obituary "$FO_OB_MPFIST"
Decal "Crack"
+EXTREMEDEATH
	States
	{
	Spawn:
		TNT1 A 1 NoDelay A_PlaySound("weapons/gswing",CHAN_BODY)
		Stop
	Death:
		TNT1 A 0 A_PlaySound("FOOTWALL",CHAN_WEAPON)
		TNT1 AAAAAA 0 A_CustomMissile("MeleeSmoke",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 Radius_Quake(4,6,0,5,0)
		TNT1 A 15
		Stop
	XDeath:
		TNT1 A 0 A_PlaySound("SUPERFIST",CHAN_WEAPON)
		TNT1 AAAAAA 0 A_CustomMissile("MeleeSmoke",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 Radius_Quake(4,6,0,5,0)
		TNT1 A 15
		Stop
	}
}

Actor AltPunchAttack : PunchAttack
{
Damage 7
}

Actor SuperAltPunchAttack : SuperPunchAttack
{
Damage 21	//*3 more than regular alt punch
}