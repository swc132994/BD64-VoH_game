Actor SMGSpawner : CustomInventory 19121
{
//$category "Game/BD64_Weapons"
Scale 0.82
Tag "Sub-Machine Gun"
Inventory.PickupMessage "$BD64_GOTSMG"
Inventory.PickupSound "WPICK"
	States
	{
	Spawn:
		SMGZ A 2
		TNT1 A 0 ACS_NamedExecuteAlways("DM_SpawnACS")
		SMGZ A 2
		TNT1 A 0 A_JumpIfInventory("ISDM",1,"DMSpawn")
		SMGZ A -1
		Stop
	Pickup:
		TNT1 A 0 A_JumpIfInventory("IsTacticalClass",1,"PickupTactical")
		TNT1 A 0 A_JumpIfInventory("IsModern",1,"PickupTactical")
		TNT1 A 0 A_GiveInventory("64SMG",1)
		Stop
	PickupTactical:
		TNT1 A 0 A_GiveInventory("64SMGTactical",1)
		Stop		
	DMSpawn:
		TNT1 A 0 A_ChangeFlag("THRUACTORS",TRUE)
		TNT1 A 0 A_SpawnItemEx("TeleportationFog")
		TNT1 A 0 A_SpawnItemEx("64SMG")
		TNT1 A 0 A_SpawnItemEx("64SMGTactical")
		TNT1 A 1050
		Loop
	}
}

//SMG(based on Rifle code) by IDDQD_1337
Actor 64SMG : DoomWeapon
{
Scale 0.82
Weapon.SlotNumber 2
Weapon.SlotPriority 2
Weapon.SelectionOrder 800
Weapon.AmmoType "Clip"
Weapon.AmmoUse 1
Weapon.AmmoGive 20
Inventory.PickupMessage "You got the SMG!"
Obituary "%o was picked off by %k."
Tag "Sub-Machine Gun"
VisibleToPlayerClass "64DoomPlayer"
Inventory.ForbiddenTo "Tactical64DoomPlayer","Tactical64DoomPlayer_p","Modern"
+INVENTORY.RESTRICTABSOLUTELY
	States
	{
	Spawn:
		SMGZ A -1
		Stop
	Ready:
		SMGG A 1 A_WeaponReady
		Wait
	Select:
		SMGG A 1 A_Raise
		Wait
	Deselect:
		SMGG A 1 A_Lower
		Wait
	Fire:
		TNT1 A 0 A_Jump(128,3)
		SMGF A 1 Bright A_FireBullets(random(1,7),random(1,7),-1,6,"HitPuff")
		TNT1 A 0 A_Jump(256,2)
		SMGF B 1 Bright A_FireBullets(random(0,7),random(0,7),-1,6,"HitPuff")
		TNT1 A 0 A_FireCustomMissile("GunFireSmoke",0,0)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,22)
		TNT1 A 0 A_Firecustommissile("BulletCaseSpawn",0,0,0,-8)
		TNT1 A 0 A_FireCustomMissile("Tracer",random(-3,3),0,-1,-12,0,random(-2,2))
		TNT1 A 0 A_PlaySound("SMG",CHAN_WEAPON)
		TNT1 A 0 A_GiveInventory("BurstFiredSMG",1)
		SMGF D 1
		TNT1 A 0 A_JumpIfInventory("BurstFiredSMG",3,1)
		Goto Fire
		SMGF CD 1
		TNT1 A 0 A_TakeInventory("BurstFiredSMG",0)
		SMGG A 6
		TNT1 A 0 A_Refire
		Goto Ready
	}
}

Actor SMGAmmo : Ammo
{
Inventory.Amount 0
Inventory.MaxAmount 46
Ammo.BackpackAmount 0
Ammo.BackpackMaxAmount 46
Inventory.Icon "SMGZA0"
+IGNORESKILL
}

Actor 64SMGTactical : Brutal64Weapon
{
Scale 0.82
Weapon.SlotNumber 2
Weapon.SlotPriority 2
Weapon.SelectionOrder 800
Weapon.AmmoType1 "Clip"
Weapon.AmmoUse1 1
Weapon.AmmoGive1 20
Weapon.AmmoType2 "SMGAmmo"
Weapon.AmmoUse2 1
Weapon.AmmoGive2 0
Inventory.PickupMessage "You got the SMG!"
Obituary "%o was picked off by %k SMG."
Tag "Sub-machine gun"
	States
	{
	MineThrowFlash:
		SMGS DCBA 1
		TNT1 A 18
		SMGS ABCD 1
		Stop
	KickingFlash:
		SMGG BCDE 1
		SMGG E 8
		SMGG EDCB 1
		Stop
	SlideKickingStart:
		SMGG BCDE 1
		SMGG E 35
	SlideKickingEnd:
		SMGG E 2
		SMGG EDCB 1
		Stop
	Spawn:
		SMGZ A -1
		Stop
	v1Ready:
	Ready:
		SMGG A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		TNT1 A 0 A_JumpIfInventory("WeaponSpecialAction",1,"BurstFireMode")
		Loop
	Ready2:
		SMGA A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		TNT1 A 0 A_JumpIfInventory("WeaponSpecialAction",1,"BurstFireMode")
		Loop
	NoAmmo:
		SMGG A 1
		SMGG A 10 A_PlaySound("DRYFIRE",CHAN_WEAPON)
		Goto Ready
	WarmUp:
		TNT1 A 0 A_TakeInventory("NeverSelectedSMG",1)
		SMGR P 1 Offset(-113,65)
		SMGR P 1 Offset(-89,45)
		SMGR P 1 Offset(-61,39)
		SMGR P 1 Offset(-33,35)
		SMGR P 1 Offset(-13,33)
		SMGR P 1 Offset(-1,32)
		SMGR PQRSTUVWXYZ 1
		SMGR "[\]" 1
		2MGR ABC 1
		TNT1 A 0 A_PlaySound("SMGBACK",CHAN_WEAPON)
		2MGR DEFFGH 1
		2MGR H 2
		2MGR IJ 1
		TNT1 A 0 A_PlaySound("SMGFORW",CHAN_WEAPON)
		2MGR KLLMNOP 1
		Goto Ready
	Select:
		TNT1 A 0 A_JumpIfInventory("NeverSelectedSMG",1,"WarmUp")
		TNT1 A 0 A_PlaySound("RIFBOLT",CHAN_AUTO)
		TNT1 A 0 A_Print("Weapon Special Available",1)
		SMGS ABCD 1 offset(0,32)
		TNT1 A 0 A_Raise
		Wait
	Fidget:
		TNT1 A 0 A_Jump(69,"Fidget2")
		SMGG A 1 A_WeaponReady
		SMGI ABCDEF 1 A_WeaponReady
		SMGI GG 2 A_WeaponReady
		SMGI HI 1 A_WeaponReady
		TNT1 A 0 A_PlaySound("SMGBACK",CHAN_WEAPON)
		SMGI J 5 A_WeaponReady
		TNT1 A 0 A_PlaySound("SMGFORW",CHAN_WEAPON)
		SMGI IH 1 A_WeaponReady
		SMGI GG 2 A_WeaponReady
		SMGI EFDCBA 1 A_WeaponReady
		Goto Ready
	Fidget2:
		2MGR PONM 1 A_WeaponReady
		2MGR L 2 A_WeaponReady
		2MGR KJ 1 A_WeaponReady
		TNT1 A 0 A_PlaySound("SMGBACK",CHAN_WEAPON)
		2MGR DEFFGHHH 1 A_WeaponReady
		2MGR H 3 A_WeaponReady
		2MGR IJJJ 1 A_WeaponReady
		TNT1 A 0 A_PlaySound("SMGFORW",CHAN_WEAPON)
		2MGR K 1 A_WeaponReady
		2MGR L 2 A_WeaponReady
		2MGR MNOP 1 A_WeaponReady
		Goto Ready
	ADSFidget:
		SMGA RSTU 1 A_WeaponReady
		SMGA V 2 A_WeaponReady
		TNT1 A 0 A_PlaySound("SMGBACK",CHAN_WEAPON)
		SMGA WX 1 A_WeaponReady
		SMGA X 2 Offset(0,33) A_WeaponReady
		SMGA X 1 Offset(0,35) A_WeaponReady
		SMGA X 1 Offset(0,35) A_PlaySound("SMGFORW",CHAN_WEAPON)
		SMGA X 2 Offset(0,33) A_WeaponReady
		SMGA XW 1 A_WeaponReady
		SMGA V 2 A_WeaponReady
		SMGA UTSR 1 A_WeaponReady
		Goto Ready2
	Deselect:
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Takeinventory("Zoomed",0)
		TNT1 A 0 A_Takeinventory("WeaponSpecialAction",0)
		TNT1 A 0 A_Takeinventory("SMGBurstMode",0)
		TNT1 A 0 A_Takeinventory("TossMine",0)
		TNT1 A 0 A_Takeinventory("PlaceMine",0)
		TNT1 A 0 A_Takeinventory("Kicking",0)
		SMGS EFGH 1
		TNT1 A 0 A_Lower
		Wait
	Fire:
		TNT1 A 0 A_JumpIfInventory("NoReloading",1,"NoReload")
		TNT1 A 0 A_JumpIfInventory("SMGAmmo",1,1)
		Goto Reload
		TNT1 A 0 A_Takeinventory("SMGAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_JumpIfInventory("SMGAmmo",9,"ResumeFire")
		TNT1 A 0 ACS_NamedExecute("LowAmmoSFX")
		Goto ResumeFire
	NoReload:
		TNT1 A 0 A_JumpIfInventory("Clip",1,1)
		Goto NoAmmo
		TNT1 A 0 A_Takeinventory("Clip",1,TIF_NOTAKEINFINITE)
	ResumeFire:
		TNT1 A 0 A_JumpIfInventory("SMGBurstMode",1,"BurstFire")
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"FireZoomed")
		TNT1 A 0 A_SetPitch(pitch-1.2)
		TNT1 A 0 A_FireCustomMissile("GunFireSmoke",0,0)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,22)
		TNT1 A 0 A_FireCustomMissile("Tracer",random(-3,3),0,-1,-12,0,random(-2,2))
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("SMG",CHAN_WEAPON)
		TNT1 A 0 A_Jump(128,3)
		SMGF A 1 Bright A_FireBullets(random(1,7),random(1,7),1,6,"HitPuff",FBF_NORANDOM)
		TNT1 A 0 A_Jump(256,2)
		SMGF B 1 Bright A_FireBullets(random(0,7),random(0,7),1,6,"HitPuff",FBF_NORANDOM)
		SMGF D 1 A_SetPitch(pitch+0.4,SPF_INTERPOLATE)
		TNT1 A 0 A_Firecustommissile("BulletCaseSpawn",0,0,0,-8)
		SMGG A 1 A_SetAngle(angle + random(-1,1))
		TNT1 A 0 A_Refire
		Goto Ready
	FireZoomed:
		TNT1 A 0 A_FireCustomMissile("Tracer",0,0,0,-6,0,0)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,22)
		TNT1 A 0 A_Firecustommissile("BulletCaseSpawn",0,0,0,-8)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("SMG",CHAN_WEAPON)
		SMGA B 1 Bright A_FireBullets(random(1,7),random(1,7),1,6,"HitPuff",FBF_NORANDOM)
		TNT1 A 0 A_SetPitch(-1.0 + pitch)
		SMGA D 1 A_SetPitch(0.5 + pitch)
		SMGA A 1 A_SetPitch(0.5 + pitch)
		TNT1 A 0 A_Refire
		SMGA A 5 A_WeaponReady
		Goto Ready2
	AltFire:
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"NoAim")
		SMGG A 0 A_Giveinventory("Zoomed",1)
		SMGG A 0 A_ZoomFactor(1.3)
		SMGP ABC 1
		Goto Ready2
	NoAim:
		SMGG A 0 A_Takeinventory("Zoomed",0)
		SMGG A 0 A_ZoomFactor(1.0)
		SMGP CBA 1
		Goto Ready
	BurstFire:
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"BurstFireZoomed")
		TNT1 A 0 A_Jump(128,3)
		SMGF A 1 Bright A_FireBullets(2.0,2.0,1,6,"HitPuff",FBF_NORANDOM)
		TNT1 A 0 A_Jump(256,2)
		SMGF B 1 Bright A_FireBullets(1.5,1.5,1,6,"HitPuff",FBF_NORANDOM)
		SMGF C 1 A_SetPitch(pitch-0.4)
		TNT1 A 0 A_Firecustommissile("BulletCaseSpawn",0,0,0,-8)
		TNT1 A 0 A_FireCustomMissile("GunFireSmoke",0,0)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,22)
		TNT1 A 0 A_FireCustomMissile("Tracer",random(-3,3),0,-1,-12,0,random(-2,2))
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("SMG",CHAN_WEAPON)
		TNT1 A 0 A_GiveInventory("BurstFiredSMG",1)
		TNT1 A 0 A_JumpIfInventory("BurstFiredSMG",3,1)
		Goto Fire
		SMGF D 1 A_SetAngle(angle + random(-1,1))
		TNT1 A 0 A_TakeInventory("BurstFiredSMG",0)
		Goto Ready
	FireZoomed:
		TNT1 A 0 A_FireCustomMissile("Tracer",0,0,0,-6,0,0)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,22)
		TNT1 A 0 A_Firecustommissile("BulletCaseSpawn",0,0,0,-8)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("SMG",CHAN_WEAPON)
		SMGA B 1 Bright A_FireBullets(0.0,0.0,1,6,"HitPuff",FBF_NORANDOM)
		SMGA C 1 A_SetPitch(-1.0 + pitch)
		SMGA D 1 A_SetPitch(0.5 + pitch)
		TNT1 A 0 A_SetPitch(0.5 + pitch)
		TNT1 A 0 A_Refire
		SMGA A 5 A_WeaponReady
		Goto Ready2
	BurstFireZoomed:
		SMGA B 1 Bright A_FireBullets(2.0,2.0,1,6,"HitPuff",FBF_NORANDOM)
		SMGA C 1 A_SetPitch(pitch-0.4)
		TNT1 A 0 A_Firecustommissile("BulletCaseSpawn",0,0,0,-8)
		TNT1 A 0 A_FireCustomMissile("GunFireSmoke",0,0)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,22)
		TNT1 A 0 A_FireCustomMissile("Tracer",0,0,0,-6,0,0)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("SMG",CHAN_WEAPON)
		TNT1 A 0 A_GiveInventory("BurstFiredSMG",1)
		TNT1 A 0 A_JumpIfInventory("BurstFiredSMG",3,1)
		Goto Fire
		SMGA D 1 A_SetAngle(angle + random(-1,1))
		TNT1 A 0 A_TakeInventory("BurstFiredSMG",0)
		Goto Ready2
	BurstFireMode:
		TNT1 A 0 A_JumpIFInventory("SMGBurstMode",1,"AutomaticMode")
		TNT1 A 0 A_TakeInventory("WeaponSpecialAction",0)
		TNT1 A 0 A_GiveInventory("SMGBurstMode",1)
		TNT1 A 0 A_Print("Burst fire mode")
		TNT1 A 0 A_PlaySound("SMGBURST",CHAN_BODY)
		SMGA A 0 A_JumpIfInventory("Zoomed",1,2)
		SMGG A 0
		"####" A 1 Offset(0,33)
		"####" A 1 Offset(0,34)
		"####" A 1 Offset(0,33)
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"Ready2")
		Goto Ready
	AutomaticMode:
		TNT1 A 0 A_TakeInventory("WeaponSpecialAction",0)
		TNT1 A 0 A_TakeInventory("SMGBurstMode",1)
		TNT1 A 0 A_Print("Automatic Mode")
		TNT1 A 0 A_PlaySound("SMGAUTO",CHAN_ITEM)
		SMGA A 0 A_JumpIfInventory("Zoomed",1,2)
		SMGG A 0
		"####" A 1 Offset(0,33)
		"####" A 1 Offset(0,34)
		"####" A 1 Offset(0,33)
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"Ready2")
		Goto Ready
	Reload:
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"ADSReload")
		SMGG A 1 A_WeaponReady
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_JumpIfInventory("SMGAmmo",0,"Fidget")
		TNT1 A 0 A_JumpIfInventory("Clip",1,1)
		Goto NoAmmo
		TNT1 A 0 A_PlaySound("RELOAU",CHAN_AUTO)
		SMGR ABCD 1 A_JumpIfInventory("Kicking",1,"DoKick")
		SMGR EFGHIJKMNNN 1 A_JumpIfInventory("Kicking",1,"DoKick")
		SMGR N 1 A_FireCustomMissile("EmptyClipSpawn",-5,0,8,-18)
		SMGR N 2 A_JumpIfInventory("Kicking",1,"DoKick")
		SMGR OPQRSTUVWXYZ 1
		SMGR "[" 1
		TNT1 A 0 A_JumpIfInventory("SMGAmmo",1,"InsertBullets2")//45+1 effect
		Goto InsertBullets
	FinishReload:
		SMGR "\]" 1
		2MGR AB 1 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("SMGBolt",1,"ReloadEmpty")
		2MGR L 2
		2MGR MNOP 1
		Goto Ready
	ReloadEmpty:
		TNT1 A 0 A_TakeInventory("SMGBolt",1)
		2MGR C 1
		TNT1 A 0 A_PlaySound("SMGBACK",CHAN_WEAPON)
		2MGR DEFFGHHH 1
		2MGR H 3
		2MGR IJJJ 1
		TNT1 A 0 A_PlaySound("SMGFORW",CHAN_WEAPON)
		2MGR K 1
		2MGR L 2
		2MGR MNOP 1
		Goto Ready
	InsertBullets:
		TNT1 A 0 A_TakeInventory("BurstFiredSMG",0)
		TNT1 A 0 A_JumpIfInventory("SMGAmmo",45,"FinishReloadChecker")
		TNT1 A 0 A_JumpIfInventory("Clip",1,1)
		Goto FinishReloadChecker
		TNT1 A 0 A_Giveinventory("SMGAmmo",1)
		TNT1 A 0 A_GiveInventory("SMGBolt",1)		
		TNT1 A 0 A_Takeinventory("Clip",1,TIF_NOTAKEINFINITE)
		Goto InsertBullets
		TNT1 A 0 A_Refire
		Goto FinishReloadChecker
	InsertBullets2:
		TNT1 A 0 A_JumpIfInventory("SMGAmmo",0,"FinishReloadChecker")
		TNT1 A 0 A_JumpIfInventory("Clip",1,1)
		Goto FinishReloadChecker
		TNT1 A 0 A_Giveinventory("SMGAmmo",1)
		TNT1 A 0 A_Takeinventory("Clip",1,TIF_NOTAKEINFINITE)
		Goto InsertBullets2
		TNT1 A 0 A_Refire
		Goto FinishReloadChecker
	FinishReloadChecker:
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"FinishADSReload")
		Goto FinishReload
	ADSReload:
		SMGA A 1 A_WeaponReady
		TNT1 A 0 A_JumpIfInventory("SMGAmmo",0,"ADSFidget")
		TNT1 A 0 A_JumpIfInventory("Clip",1,1)
		Goto ADSFidget
		TNT1 A 0 A_PlaySound("RELOAU",CHAN_AUTO)
		SMGA EFGHIJKLMNO 1 A_JumpIfInventory("Kicking",1,"DoKick")
		SMGA P 1 A_FireCustomMissile("EmptyClipSpawn",-5,0,8,-18)
		SMGA P 11 A_JumpIfInventory("Kicking",1,"DoKick")
		SMGA ONMLK 1
		SMGA JIHG 1
		TNT1 A 0 A_JumpIfInventory("SMGAmmo",1,"InsertBullets2")//45+1 effect
		Goto InsertBullets
	FinishADSReload:
		SMGA FE 1 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("SMGBolt",1,"ADSReloadEmpty")
		Goto Ready2
	ADSReloadEmpty:
		TNT1 A 0 A_TakeInventory("SMGBolt",1)
		SMGA RSTU 1
		SMGA V 2
		SMGA W 1 A_PlaySound("SMGBACK",CHAN_WEAPON)
		SMGA X 1
		SMGA X 2 Offset(0,33)
		SMGA X 1 Offset(0,34)
		SMGA X 1 Offset(0,34) A_PlaySound("SMGFORW",CHAN_WEAPON)
		SMGA X 2 Offset(0,33)
		SMGA XW 1 A_WeaponReady(WRF_NOFIRE)
		SMGA V 2
		SMGA UTSR 1
		Goto Ready2
	}
}