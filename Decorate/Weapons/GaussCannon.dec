Actor GaussCannonSpawner : CustomInventory	19134	//spawned from randomspawners
{
//$category "Game/BD64_Weapons
Scale 0.9
Tag "Gauss Cannon"
Inventory.PickupMessage "You picked up the Gauss Cannon"
Inventory.PickupSound "WPICK"
	States
	{
	Spawn:
		GCPC A 2
		TNT1 A 0 ACS_NamedExecuteAlways("DM_SpawnACS")
		GCPC A 2
		TNT1 A 0 A_JumpIfInventory("ISDM",1,"DMSpawn")
		GCPC A -1
		Stop
	Pickup:
		TNT1 A 0 A_JumpIfInventory("IsTacticalClass",1,"Pickup2")
		TNT1 A 0 A_JumpIfInventory("IsModern",1,"Pickup2")
		TNT1 A 0 A_GiveInventory("64GaussCannon",1)
		Stop
	Pickup2:
		TNT1 A 0 A_GiveInventory("64GaussCannonTACTICAL",1)
		Stop
	DMSpawn:
		TNT1 A 0 A_ChangeFlag("THRUACTORS",TRUE)
		TNT1 A 0 A_SpawnItemEx("TeleportationFog")
		TNT1 A 0 A_SpawnItemEx("64GaussCannon")
		TNT1 A 0 A_SpawnItemEx("64GaussCannonTACTICAL")
		TNT1 A 1050
		Loop
	}
}

ACTOR GaussScopeLightAmp : PowerLightAmp
{
Powerup.Duration 2147483647
}

ACTOR 64GaussCannon : DoomWeapon
{
Scale 0.9
Weapon.SlotNumber 6
Weapon.SlotPriority 1
Weapon.Selectionorder 2000
Weapon.AmmoType "Cell"
Weapon.AmmoUse 10
Weapon.AmmoGive 40
Inventory.Pickupmessage "You picked up the Gauss Cannon"
Obituary "%o was pierced by %k's Gauss Cannon"
Tag "Gauss Cannon"
VisibleToPlayerClass "64DoomPlayer"
Inventory.ForbiddenTo "Tactical64DoomPlayer","Tactical64DoomPlayer_P","Modern"
Decal "BigScorch"
+INVENTORY.RESTRICTABSOLUTELY
+WEAPON.NOAUTOFIRE
	States
	{
	Ready:
		GCID A 1 A_WeaponReady
		Loop
	Deselect:
		TNT1 A 0 A_ZoomFactor(1.0)
		GCID A 1 A_Lower
		Wait
	Select:
		GCID A 1 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_PlaySound("GaussCannon/Fire",CHAN_WEAPON)
		TNT1 A 0 A_ZoomFactor(0.93)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle3",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle3",20,5,22)
		TNT1 A 0 A_RailAttack(random(250,400),0,1,None,None,RGF_FULLBRIGHT|RGF_SILENT,0,"GaussCannonProjectilePuff",0,0,0,0,5,0,"GaussCannonProjectileTrail",-5)
		TNT1 A 0 A_ChangeVelocity(-cos(-pitch)*2,0,sin(pitch)*2,CVF_RELATIVE)
		TNT1 A 0 A_CheckFloor(2)
		TNT1 A 0 A_ChangeVelocity(-cos(-pitch)*12,0,sin(pitch)*12,CVF_RELATIVE)
		GCFI A 1 Bright A_ZoomFactor(0.94)
		GCFI A 1 Bright A_ZoomFactor(0.98)
		GCFI C 1 Bright A_ZoomFactor(0.99)
		GCFI D 1 Bright A_ZoomFactor(1)
		GCFI EFG 1
		GCFI H 4
		GCFI I 3
		GCFI J 2
		GCFI KLMNOP 2
		GCID A 4
		goto Ready
	Spawn:
		GCPC A -1
		stop
	}
}

Actor NeverSelectedGauss : Inventory
{
Inventory.MaxAmount 1
}

Actor GaussAmmo : Ammo
{
Inventory.Amount 0
Inventory.MaxAmount 10
Ammo.BackpackAmount 0
Ammo.BackpackMaxAmount 10
Inventory.Icon "GCPCA0"
+IGNORESKILL
}

ACTOR 64GaussCannonTACTICAL : Brutal64Weapon
{
Scale 0.9
Weapon.SlotNumber 6
Weapon.SlotPriority 1
Weapon.Selectionorder 2000
Weapon.AmmoType1 "Cell"
Weapon.AmmoUse1 1
Weapon.AmmoGive1 40
Weapon.AmmoType2 "GaussAmmo"
Weapon.AmmoUse2 1
Weapon.AmmoGive2 0
Inventory.Pickupmessage "You picked up the Gauss Cannon"
Obituary "%o was pierced by %k's Gauss Cannon"
Tag "Gauss Cannon"
VisibleToPlayerClass "Tactical64DoomPlayer","Tactical64DoomPlayer_P","Modern"
Inventory.ForbiddenTo "64DoomPlayer"
Decal "BigScorch"
+INVENTORY.RESTRICTABSOLUTELY
+WEAPON.AMMO_CHECKBOTH
+WEAPON.NOALERT
+WEAPON.NOAUTOFIRE
	States
	{
	MineThrowFlash:
		TNT1 A 0 A_TakeInventory("GaussScopeLightAmp",1)
		TGCS DCBA 1
		TNT1 A 18
		TGCS ABCD 1
		Stop
	KickingFlash:
		TNT1 A 0 A_TakeInventory("GaussScopeLightAmp",1)
		GCIK ABCD 1
		GCIK D 8
		GCIK DCBA 1
		Stop
	AirKickingFlash:
		TNT1 A 0 A_TakeInventory("GaussScopeLightAmp",1)
		GCIK ABCD 1
		GCIK D 10
		GCIK DCBA 1
		Stop
	SlideKickingStart:
		TNT1 A 0 A_TakeInventory("GaussScopeLightAmp",1)
		GCIK ABCD 1
		GCIK D 18
	SlideKickingEnd:
		TNT1 A 0 A_TakeInventory("GaussScopeLightAmp",1)
		GCIK D 6
		GCIK DCBA 1
		Stop
	v1Ready:
	Ready:
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_TakeInventory("GaussScopeLightAmp",1)
		TNT1 A 0 A_TakeInventory("Zoomed",0)
		TGCI A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Deselect:
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Takeinventory("WeaponSpecialAction",0)
		TNT1 A 0 A_Takeinventory("TossMine",0)
		TNT1 A 0 A_Takeinventory("PlaceMine",0)
		TNT1 A 0 A_Takeinventory("Kicking",0)
		TNT1 A 0 A_TakeInventory("Zoomed",0)
		TNT1 A 0 A_TakeInventory("GaussScopeLightAmp",0)
		TNT1 A 0 A_TakeInventory("HasPlasmaWeapon",0)
		TGCS EFGH 1
		TNT1 A 0 A_Lower
		Wait
	WarmUp:
		TNT1 A 0 A_TakeInventory("NeverSelectedGauss",1)
		GCRL E 1 Offset(164, 122)
		GCRL E 1 Offset(134, 97)
		GCRL E 1 Offset(91, 73)
		GCRL E 1 Offset(51, 55)
		GCRL E 1 Offset(15, 37)
		GCRL E 1 Offset(4, 33)
		GCRL E 10
		GCBL AABBCCDEFGHIJK 1
		TNT1 A 0 A_PlaySound("RAILMAG2",CHAN_WEAPON)
		GCBL LLMNOPQRS 1
		Goto Ready
	Select:
		TNT1 A 0 A_JumpIfInventory("NeverSelectedGauss",1,"WarmUp")
		TNT1 A 0 A_PlaySound("GAUDR1",CHAN_BODY)
		TGCS ABCD 1 Offset(0,32)
		TNT1 A 0 A_Raise
		Wait
	Fire:
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"FireZoomed")
		Goto AmmoCheck
	ResumeFire:
		TNT1 A 0 A_PlaySound("GaussCannon/Fire",CHAN_WEAPON)
		TNT1 A 0 A_ZoomFactor(0.93)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle3",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle3",20,5,22)
		TNT1 A 0 A_RailAttack(random(250,400),0,0,None,None,RGF_FULLBRIGHT|RGF_SILENT,0,"GaussCannonProjectilePuff",0,0,0,35,5,0,"GaussCannonProjectileTrail",-5)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_ChangeVelocity(-cos(-pitch)*2,0,sin(pitch)*2,CVF_RELATIVE)
		TNT1 A 0 A_CheckFloor(2)
		TNT1 A 0 A_ChangeVelocity(-cos(-pitch)*12,0,sin(pitch)*12,CVF_RELATIVE)
		TGCF A 1 Bright A_ZoomFactor(0.94)
		TGCF A 1 Bright A_ZoomFactor(0.98)
		TGCF C 1 Bright A_ZoomFactor(0.99)
		TGCF D 1 Bright A_ZoomFactor(1)
		TGCF EFG 1
		TGCF H 4
		TGCF I 3
		TGCF J 2
		TGCF KLMNOP 2
		TGCI A 4
		goto Ready
	Reload:
		TNT1 A 0 A_JumpIfInventory("Cell",1,1)
		Goto Ready
		TNT1 A 0 A_TakeInventory("Zoomed",0)
		TNT1 A 0 A_TakeInventory("GaussScopeLightAmp",1)
		TNT1 A 0 A_TakeInventory("HasPlasmaWeapon",1)
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_JumpIfInventory("GaussAmmo",10,"Ready")
		GCRL ABC 1
		GCRL C 2 Offset(0,32)
		GCRL D 5 Offset(0,35) A_PlaySound("Railgun/Eject",CHAN_VOICE)
		GCRL D 2 Offset(0,32)
		GCRL D 6
		GCFL ABC 1
		TNT1 A 0 A_JumpIfInventory("GaussAmmo",1,"FullReload")
		TNT1 A 0 A_PlaySound("RAILMAG1",CHAN_ITEM)
		GCEL ABCDDEFGHIJKLMNOP 1
		TNT1 A 0 A_FireCustomMissile("EmptyCellPack",-30,0,-11,-24)
		GCEL QRR 1
		GCRL E 10
		Goto FinishReload
	FullReload:
		TNT1 A 0 A_PlaySound("RAILMAG1",CHAN_WEAPON)
		GCFL DDEFGHIJKLMN 1
		GCRL E 10
	FinishReload:
		GCBL AABBCCDEFGHIJK 1
		TNT1 A 0 A_PlaySound("RAILMAG2",CHAN_WEAPON)
		GCBL LLMNOPQRS 1
	InsertBullets:
		TNT1 A 0 A_JumpIfInventory("GaussAmmo",10,"Ready")
		TNT1 A 0 A_JumpIfInventory("Cell",1,1)
		Goto Ready
		TNT1 A 0 A_Giveinventory("GaussAmmo",1)
		TNT1 A 0 A_Takeinventory("Cell",10,TIF_NOTAKEINFINITE)
		Goto InsertBullets
		TNT1 A 0 A_Refire
		Goto Ready
	Spawn:
		GCPC A -1
		stop
	AltFire:
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"NoAim")
		TNT1 A 0 A_GiveInventory("Zoomed",1)
		GCSZ ABCD 1
		GCSC ABCDEFGHI 1
		TNT1 A 0 A_ZoomFactor(10.0)
		TNT1 A 0 A_GiveInventory("GaussScopeLightAmp",1)
		Goto Ready2
	NoAim:
		TNT1 A 0 A_Takeinventory("Zoomed",0)
		TNT1 A 0 A_TakeInventory("GaussScopeLightAmp",1)
		TNT1 A 0 A_TakeInventory("HasPlasmaWeapon",1)
		TNT1 A 0 A_ZoomFactor(1.0)
		GCSC IHGFEDCBA 1
		GCSZ DCBA 1
		Goto Ready
	Ready2:
		TNT1 A 0 A_GiveInventory("HasPlasmaWeapon",1)
		GCSC J 1 A_WeaponReady(WRF_ALLOWRELOAD)
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		Loop
	FireZoomed:
		Goto AmmoCheck
	ResumeFireZoomed:
		TNT1 A 0 A_PlaySound("GaussCannon/Fire",CHAN_BODY)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle3",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle3",20,5,22)
		TNT1 A 0 A_RailAttack(random(125,175),0,0,None,None,RGF_FULLBRIGHT|RGF_SILENT,0,"GaussCannonProjectilePuff",0,0,0,35,5,0,"GaussCannonProjectileTrail")
		TNT1 A 0 A_AlertMonsters
		GCSC J 3 Bright A_SetPitch(-1.0 + pitch)
		GCSC J 7 A_SetPitch(0.5 + pitch)
		GCSC J 20
		Goto Ready2
	AmmoCheck:
		TNT1 A 0 A_JumpIfInventory("NoReloading",1,"NoReload")
		TNT1 A 0 A_JumpIfInventory("GaussAmmo",1,1)
		Goto Reload
		TNT1 A 0 A_TakeInventory("GaussAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"ResumeFireZoomed")
		Goto ResumeFire
	NoReload:
		TNT1 A 0 A_JumpIfInventory("Cell",1,1)
		Goto Ready
		TNT1 A 0 A_TakeInventory("Cell",10,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"ResumeFireZoomed")
		Goto ResumeFire
	}
}

Actor GaussCannonProjectilePuff
{
+ALWAYSPUFF
-NOCLIP
+NOGRAVITY
+PIERCEARMOR
+EXTREMEDEATH
+FORCEXYBILLBOARD
Alpha 1
Translation "0:255=%[0,0,0]:[0.4,1.0,2.0]"
RenderStyle Add
DamageType "Plasma"
Scale 1.5
+SLIDESONWALLS
	States
	{
	Spawn:
		GEXP A 1 light("PLASMABALL")
		Goto Death
	Death:
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("BluePlasmaParticle",0,0,random(0,360),2,random(0,360))
		TNT1 AAAA 0 A_SpawnItemEx("LensFlareTorchBlue")
		TNT1 A 0 A_SpawnItemEx("DetectCeilCrater")
		TNT1 A 0 ThrustThingZ(0,1,1,0)
		TNT1 A 1 A_Explode(30,40)
		GEXP ABCDEFGHIJKL 2 Bright light("PLASMABALL")
		TNT1 A 0 A_SpawnItemEx("DetectFloorCrater")
		GEXP MNOPQRS 2 Bright light("PLASMA_X3") A_CustomMissile("PlasmaSmoke",1,0,random(0,360),2,random(0,160))
		GEXP TUVWXYZ 2 Bright light("PLASMA_X4")
		stop
	}
}

Actor GaussCannonProjectileTrail
{
+NOBLOCKMAP
+NOGRAVITY
+PIERCEARMOR
+NOINTERACTION
+FORCEXYBILLBOARD
Alpha 0.6
RenderStyle Add
Scale 0.7
	States
	{
	Spawn:
	GCPR A 1 NoDelay Bright A_SetScale(0.6)
	GCPR A 1 Bright A_SetScale(0.55)
	GCPR A 1 Bright A_SetScale(0.5)
	GCPR A 1 Bright A_SetScale(0.45)
	GCPR A 1 Bright A_SetScale(0.4)
	GCPR A 1 Bright A_SetScale(0.35)
	GCPR A 1 Bright A_SetScale(0.3)
	GCPR A 1 Bright A_SetScale(0.25)
	GCPR A 1 Bright A_SetScale(0.2)
	GCPR A 1 Bright A_SetScale(0.15)
	GCPR A 1 Bright A_SetScale(0.1)
	GCPR A 1 Bright A_SetScale(0.05)
	GCPR A 1 Bright A_SetScale(0.0)
	Stop
	}
}

ACTOR EmptyCellPack
{
Scale 0.9
Height 2
Radius 2
Speed 4
Mass 6
SeeSound "weapons/largemagdrop"
-NOGRAVITY
-DROPOFF
+WINDTHRUST
+CLIENTSIDEONLY
+MOVEWITHSECTOR
+MISSILE
+NOBLOCKMAP
+NOTELEPORT
+FORCEXYBILLBOARD
+NOTDMATCH
+GHOST
+ISMONSTER
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_Jump(128,2)
		TNT1 A 0 A_SetScale(0-ScaleX,ScaleY)
		ECPC A 4 A_CheckFloor("Death")
		Wait
	Death:
		TNT1 A 0 ACS_NamedExecuteAlways("LowBloodCheckerACS")
		ECPC A 600
		TNT1 A 0 A_JumpIfInventory("LowGraphicsMode",1,"Stoping")
		Loop
	Stoping:
		TNT1 A 1
		Stop
	}
}
