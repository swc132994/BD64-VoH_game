Actor ShotgunSpawner : CustomInventory	replaces Shotgun	//spawned from randomspawners
{
Scale 0.87
Tag "$FO_TAG_SHOTGUN"
Inventory.PickupMessage "$GOTSHOTGUN"
Inventory.PickupSound "WPICK"
	States
	{
	Spawn:
		SHOT A 2
		TNT1 A 0 ACS_NamedExecuteAlways("DM_SpawnACS")
		SHOT A 2
		TNT1 A 0 A_JumpIfInventory("ISDM",1,"DMSpawn")
		SHOT A -1
		Stop
	Pickup:
		TNT1 A 0 ACS_NamedExecuteWithResult("WeaponPickupChecker",4)
		Stop
	DMSpawn:
		TNT1 A 0 A_ChangeFlag("THRUACTORS",TRUE)
		TNT1 A 0 A_SpawnItemEx("TeleportationFog")
		TNT1 A 0 A_SpawnItemEx("64Shotgun")
		TNT1 A 0 A_SpawnItemEx("64ShotgunTACTICAL")
		TNT1 A 1050
		Loop
	}
}

Actor 64Shotgun : DoomWeapon
{
Scale 0.87
Weapon.SlotNumber 3
Weapon.SlotPriority 1
Weapon.SelectionOrder 1300
Weapon.AmmoType "Shell"
Weapon.AmmoUse 1
Weapon.AmmoGive 8
Inventory.PickupMessage "$GOTSHOTGUN"
Obituary "$FO_OB_MPSHOTGUN"
Tag "$FO_TAG_SHOTGUN"
VisibleToPlayerClass "64DoomPlayer"
Inventory.ForbiddenTo "Tactical64DoomPlayer","Tactical64DoomPlayer_p","Modern"
+INVENTORY.RESTRICTABSOLUTELY
	States
	{
	Ready:
		CHTG A 0 A_JumpIfInventory("VWeaponMode",1,2)
		SHTG A 0
		"####" A 1 A_WeaponReady
		Wait
	Select:
		CHTG A 0 A_JumpIfInventory("VWeaponMode",1,2)
		SHTG A 0
		"####" A 1 A_Raise
		Wait
	Deselect:
		TNT1 A 0 A_ZoomFactor(1.0)
		CHTG A 0 A_JumpIfInventory("VWeaponMode",1,2)
		SHTG A 0
		"####" A 1 A_Lower
		Wait
	Fire:
		CHTG A 0 A_JumpIfInventory("VWeaponMode",1,2)
		SHTG A 0
		"####" A 1
		TNT1 AA 0 A_FireCustomMissile("GunFireSmoke",0,0)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,22)
		TNT1 A 0 A_JumpIfInventory("VWeaponMode",1,"VanillaFire")
		TNT1 AAAAA 0 A_FireCustomMissile("Tracer",random(-3,3),0,-1,-12,0,random(-5,5))
		TNT1 A 0 A_FireBullets(4.0,4.0,10,5,"ShotgunPuff")
		TNT1 A 0 A_PlaySound("SGFIRE",CHAN_WEAPON)
		TNT1 A 0 A_ZoomFactor(0.95)
		SHTF AB 2 Bright
		TNT1 A 0 A_ZoomFactor(1.0)
		SHTF CDEFG 1
		SHTG ABCDEFG 1
		SHTG H 1 A_PlaySound("Shotgun/PumpIn",CHAN_7)
		SHTG IJ 1
		SHTG K 1 A_FireCustomMissile("ShotCaseSpawn",0,0,1,-9)
		SHTG LNOP 1
		SHTG Q 1 A_PlaySound("Shotgun/PumpOut",CHAN_7)
		SHTG RS 1
		SHTG FEDCBA 1
		TNT1 A 0 A_Takeinventory("HasUsedShell",1)
		TNT1 A 0 A_Refire
		Goto Ready
	VanillaFire:
		TNT1 A 0 A_GunFlash("VanillaFireFlash")
		TNT1 AAAAA 0 A_FireCustomMissile("Tracer",random(-3,3),0,-1,-12,0,random(-5,5))
		TNT1 A 0 A_FireBullets(4.0,4.0,10,5,"ShotgunPuff")
		TNT1 A 0 A_PlaySound("SGFIRE",CHAN_WEAPON,1)
		CHTG A 5
		CHTG B 15
		TNT1 A 0 A_FireCustomMissile("ShotCaseSpawn",0,0,1,-9)
		TNT1 A 0 A_PlaySound("Shotgun/PumpIn",CHAN_7)
		CHTG C 7
		TNT1 A 0 A_PlaySound("Shotgun/PumpOut",CHAN_7)
		CHTG A 8
		TNT1 A 0 A_Refire
		Goto Ready
	VanillaFireFlash:
		CHTF A 1 Bright A_Light1
		CHTF A 0 A_Light0
		Goto LightDone
	Spawn:
		SHOT A -1
		Stop
	}
}

Actor ShotgunAmmo : Ammo
{
Inventory.Amount 0
Inventory.MaxAmount 9
Ammo.BackpackAmount 0
Ammo.BackpackMaxAmount 9
Inventory.Icon "shota0"
+IGNORESKILL
}

Actor 64ShotgunTACTICAL : Brutal64Weapon
{
Scale 0.87
Weapon.SlotNumber 3
Weapon.SlotPriority 1
Weapon.SelectionOrder 1300
Weapon.AmmoType1 "Shell"
Weapon.AmmoUse1 1
Weapon.AmmoGive1 8
Weapon.AmmoType2 "ShotgunAmmo"
Weapon.AmmoUse2 1
Weapon.AmmoGive2 0
Inventory.PickupMessage "$GOTSHOTGUN"
Obituary "$FO_OB_MPSHOTGUN"
Tag "$FO_TAG_SHOTGUN"
	States
	{
	MineThrowFlash:
		SHTF TUVW 1
		TNT1 A 18
		SHTF PQRS 1
		Stop
	KickingFlash:
		SHTF XYZ 1
		SHTF "[" 1
		SHTF "]" 8
		SHTF "[" 1
		SHTF ZYX 1
		Stop
	ReloadingKick:
		SHSR C 16
		Stop
	SlideKickingStart:
		SHTF XYZ 1
		SHTF "[" 1
		SHTF "]" 35
	SlideKickingEnd:
		SHTF "]" 2
		SHTF "[" 1
		SHTF ZYX 1
		Stop
	Spawn:
		SHOT A -1
		Stop
	WarmUp:
		TNT1 A 0 A_TakeInventory("NeverSelectedShotgun")
		SHTF QRS 1 A_WeaponReady(WRF_NOFIRE)
		SHTG BCDEFG 1
		SHTG H 1 A_PlaySound("Shotgun/PumpIn",CHAN_7)
		SHTG IJJKKLLLLLMMNO 1
		SHTG P 1 A_PlaySound("Shotgun/PumpOut",CHAN_7)
		SHTG QRSGFEDCB 1 A_WeaponReady
		Goto Ready
	v1Ready:
	Ready:
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
	OkToFire:
		SHTG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Goto Ready
	NoAmmo:
		SHTG A 10
		TNT1 A 0 A_PlaySound("DRYFIRE",CHAN_WEAPON)
		TNT1 A 0 A_TakeInventory("HasUsedShell",0)
		TNT1 A 0 A_TakeInventory("ShotgunPumping",0)
		Goto Ready
	Select:
		TNT1 A 0 A_JumpIfInventory("NeverSelectedShotgun",1,"WarmUp")
		TNT1 A 0 A_JumpIfInventory("HasUsedShell",1,2)
		TNT1 A 0 A_PlaySound("Shotgun/Pump",CHAN_7)
		SHTF PQRS 1 A_WeaponReady(WRF_NOFIRE)
		SHTG A 0 A_JumpIfInventory("HasUsedShell",1,"Pump")
		TNT1 A 0 A_Raise
		Wait
	Deselect:
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Takeinventory("WeaponSpecialAction",0)
		TNT1 A 0 A_TakeInventory("HoldFidget",0)
		TNT1 A 0 A_TakeInventory("ShotgunPumping",0)
		TNT1 A 0 A_Takeinventory("TossMine",0)
		TNT1 A 0 A_Takeinventory("PlaceMine",0)
		TNT1 A 0 A_Takeinventory("Kicking",0)
		SHTF TUVW 1
		TNT1 A 0 A_Lower
		Wait
	Fidget:
		SHTG BCDEFG 1 A_WeaponReady(WRF_NOFIRE)
		SHTG S 1 A_PlaySound("Shotgun/PumpIn",CHAN_7)
		SHTG R 1
		1HTG CDEFFFFF 1 A_WeaponReady(WRF_NOFIRE)
		TNT1 A 0 A_GiveInventory("HoldFidget",1)
	HoldingFidget:
		1HTG F 2 A_WEAPONREADY(WRF_ALLOWRELOAD|WRF_NOFIRE|WRF_ALLOWZOOM)
		TNT1 A 0 A_TakeInventory("HoldFidget",1)
		1HTG GH 1 A_WEAPONREADY(WRF_NOFIRE|WRF_ALLOWZOOM)
		SHTG S 1 A_PlaySound("Shotgun/PumpOut",CHAN_7)
		SHTG GFEDCB 1 A_WEAPONREADY(WRF_NOFIRE|WRF_ALLOWZOOM)
		Goto Ready
	Fire:
		TNT1 A 0 A_JumpIfInventory("ShotgunPumping",1,"FinishInsertingShells")
		TNT1 A 0 A_JumpIfInventory("NoReloading",1,"NoReload")
		TNT1 A 0 A_JumpIfInventory("ShotgunAmmo",1,1)
		Goto Reload
		TNT1 A 0 A_TakeInventory("ShotgunAmmo",1,TIF_NOTAKEINFINITE)
		Goto ResumeFire
	NoReload:
		TNT1 A 0 A_JumpIfInventory("Shell",1,1)
		Goto NoAmmo
		TNT1 A 0 A_TakeInventory("Shell",1,TIF_NOTAKEINFINITE)
	ResumeFire:
		TNT1 AA 0 A_FireCustomMissile("GunFireSmoke",0,0)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,22)
		TNT1 AAAAA 0 A_FireCustomMissile("Tracer",random(-3,3),0,-1,-12,0,random(-5,5))
		TNT1 A 0 A_FireBullets(4.0,4.0,10,5,"ShotgunPuff",0)
		TNT1 A 0 A_GiveInventory("HasUsedShell",1)
		TNT1 A 0 A_PlaySound("SGFIRE",CHAN_WEAPON,1)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_ZoomFactor(0.95)
		SHTF AB 2 Bright
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_JumpIfInventory("PowerStrength",1,"SlamPump")
		SHTF C 1 A_SetPitch(pitch-2,SPF_INTERPOLATE)
		SHTF D 1 A_SetPitch(pitch+0.8,SPF_INTERPOLATE)
		SHTF E 1 A_SetPitch(pitch+0.6,SPF_INTERPOLATE)
		SHTF F 1 A_SetPitch(pitch+0.4,SPF_INTERPOLATE)
		SHTF G 1 A_SetPitch(pitch+0.2,SPF_INTERPOLATE)
		SHTG A 1 A_WeaponReady(WRF_NOPRIMARY)
	Pump:
		TNT1 A 0 A_JumpIfInventory("ShotgunAmmo",1,2)
		TNT1 A 0 A_Giveinventory("HasUsedShell",1)
		Goto ReloadNormally
		SHTG BCDEFG 1 A_WeaponReady(WRF_NOPRIMARY)
		SHTG H 1 A_PlaySound("Shotgun/PumpIn",CHAN_7)
		SHTG IJ 1
		SHTG K 1 A_FireCustomMissile("ShotCaseSpawn",0,0,-4,-4)
		SHTG LMNOP 1
		SHTG Q 1 A_PlaySound("Shotgun/PumpOut",CHAN_7)
		SHTG RS 1
		SHTG G 1 A_Takeinventory("HasUsedShell",1)
		SHTG FEDCB 1 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("ShotgunAmmo",1,1)
	ReadyToFireAgain:
		SHTG A 4
		TNT1 A 0 A_Refire
		Goto Ready
	SlamPump:
		SHTF H 1 A_WeaponReady(WRF_NOFIRE)
		TNT1 A 0 A_TakeInventory("ShotgunPumping",0)
		SHTF I 1 A_WeaponReady(WRF_NOFIRE)
		SHTF J 0 A_PlaySound("Shotgun/PumpIn",CHAN_7)
		SHTF K 1
		SHTF L 1 A_FireCustomMissile("ShotCaseSpawn",0,0,1,-9)
		SHTF MNOOONM 1
		SHTF L 1 A_PlaySound("Shotgun/PumpOut",CHAN_7)
		SHTF KJI 1
		SHTF FG 1
		SHTG A 2
		TNT1 A 0 A_Takeinventory("HasUsedShell",1)
		TNT1 A 0 A_Refire
		Goto Ready
	Reload:
		TNT1 A 0 A_JumpIfInventory("Shell",1,1)
		Goto NoAmmo
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_JumpIfInventory("HoldFidget",1,"HoldingFidget")
		TNT1 A 0 A_JumpIfInventory("ShotgunAmmo",0,"Fidget")
		TNT1 A 0 A_JumpIfInventory("ShotgunAmmo",1,"ReloadNormally")
		TNT1 A 0 A_GiveInventory("HasUsedShell",1)
	ReloadNormally:
		TNT1 A 0 A_JumpIfInventory("Shell",1,1)
		Goto Ready
		SHTG BCDEFG 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		SHTG TU 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
	InsertingShells:
		TNT1 A 0 A_JumpIfInventory("Shell",1,1)
		Goto PumpNewShell
		TNT1 A 0 A_JumpIfInventory("ShotgunAmmo",8,"CheckIfFinishReload")	//reloads 8 if shotgun completly empty
		TNT1 A 0 A_GiveInventory("ShotgunAmmo",1)
		TNT1 A 0 A_TakeInventory("Shell",1,TIF_NOTAKEINFINITE)
	InsertShellAnimation:
		SHTG VVVVVVWX 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		SHTG Y 1 A_PlaySound("inshell",CHAN_AUTO)
		TNT1 A 0 A_GiveInventory("ShotgunPumping",0)
		SHTG Z 1 A_WEAPONREADY(WRF_NOBOB|WRF_ALLOWZOOM)
		SHTG "[\]" 1 A_WEAPONREADY(WRF_NOBOB|WRF_ALLOWZOOM)
		1HTG AB 1 A_WEAPONREADY(WRF_NOBOB|WRF_ALLOWZOOM)
		TNT1 A 0 A_JumpIfInventory("ShotgunAmmo",0,"FinishInsertingShells")
		Goto InsertingShells
	CheckIfFinishReload:
		TNT1 A 0 A_JumpIfInventory("HasUsedShell",1,"FinishInsertingShells")
		TNT1 A 0 A_JumpIfInventory("Shell",1,1)
		Goto PumpNewShell
		TNT1 A 0 A_GiveInventory("ShotgunAmmo",1)
		TNT1 A 0 A_TakeInventory("Shell",1,TIF_NOTAKEINFINITE)
		Goto InsertShellAnimation
	FinishInsertingShells:
		SHTG UT 1
		SHTG G 1 A_TakeInventory("ShotgunPumping",0)
		TNT1 A 0 A_JumpIfInventory("HasUsedShell",1,"PumpNewShell")
		SHTG FEDCB 1
		Goto Ready
	PumpNewShell:
		TNT1 A 0 A_ZoomFactor(1.0)
		SHTG H 1 A_PlaySound("Shotgun/PumpIn",CHAN_BODY)
		SHTG IJ 1
		SHTG K 1 A_FireCustomMissile("ShotCaseSpawn",0,0,-4,-4)
		SHTG LMNOP 1
		SHTG Q 1 A_PlaySound("Shotgun/PumpOut",CHAN_BODY)
		SHTG RS 1
		SHTG G 1 A_TakeInventory("HasUsedShell",1)
		SHTG FEDCB 1
		Goto ReadyToFireAgain
	Altfire:
		TNT1 A 0 A_JumpIfInventory("PowerStrength",1,"AltFire2")
		TNT1 A 0 A_JumpIfInventory("IsModern",1,3)
		TNT1 A 0 A_JumpIfInventory("Stamina",85,"Tired")
		TNT1 A 0 A_GiveInventory("Stamina",15)	
		SHTA ABCDEF 1
		SHTA G 1 A_Playsound("SGSWING",CHAN_AUTO)
		SHTA HI 1
		SHTA J 1 A_FireCustomMissile("KickAttack",0,0,0,-7)
		SHTA J 1 A_RadiusThrust(4500,65,RTF_NOTMISSILE|RTF_NOIMPACTDAMAGE)
		SHTA KLNOP 1
		TNT1 A 0 A_Refire("AltHold")
	FinishAlt:	
		SHTA VWXY 1
		SHTA GFEDCBA 1
		Goto Ready
	AltHold:
		TNT1 A 0 A_JumpIfInventory("PowerStrength",1,"AltHold2")
		TNT1 A 0 A_JumpIfInventory("IsModern",1,3)
		TNT1 A 0 A_JumpIfInventory("Stamina",80,"Tired")
		TNT1 A 0 A_GiveInventory("Stamina",5)
		SHTA Q 1 A_PlaySound("fstatk",CHAN_WEAPON)
		SHTA RST 2
		SHTA U 3
		SHTA U 1 A_FireCustomMissile("KickAttack",0,0,0,-7)
		SHTA U 1 A_RadiusThrust(4500,65,RTF_NOTMISSILE|RTF_NOIMPACTDAMAGE)
		SHTA TSRQ 1
		TNT1 A 0 A_Refire
		Goto FinishAlt	
	AltFire2:
		TNT1 A 0 A_JumpIfInventory("IsModern",1,3)
		TNT1 A 0 A_JumpIfInventory("Stamina",100,"Tired")
		TNT1 A 0 A_GiveInventory("Stamina",10)	
		SHTA ABCDEF 1
		SHTA G 1 A_Playsound("SGSWING",CHAN_AUTO)
		SHTA HI 1
		SHTA I 1 A_FireCustomMissile("SuperKickAttack",0,0,0,-7)
		SHTA J 1 A_RadiusThrust(6750,65,RTF_NOTMISSILE|RTF_NOIMPACTDAMAGE)
		SHTA KLNOP 1
		TNT1 A 0 A_Refire("AltHold2")
		Goto FinishAlt	
	AltHold2:
		TNT1 A 0 A_JumpIfInventory("IsModern",1,3)
		TNT1 A 0 A_JumpIfInventory("Stamina",100,"Tired")
		TNT1 A 0 A_GiveInventory("Stamina",2)
		SHTA Q 1 A_PlaySound("fstatk",CHAN_WEAPON)
		SHTA RSTU 1
		SHTA U 1 A_FireCustomMissile("SuperKickAttack",0,0,0,-7)
		SHTA U 1 A_RadiusThrust(6750,65,RTF_NOTMISSILE|RTF_NOIMPACTDAMAGE)
		SHTA TSRQ 1		
		TNT1 A 0 A_Refire
		Goto FinishAlt
	}
}