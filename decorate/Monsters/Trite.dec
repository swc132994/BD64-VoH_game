Actor 64Trite 19139
{
//$category "Game/BD64_Monsters"
Scale 0.5
Health 30
Radius 16
Height 20
Mass 100
Speed 10
PoisonDamage 10
MeleeThreshold 64
MaxTargetRange 150
MinMissileChance 10
MaxStepHeight 28
MaxDropOffHeight 32
PainSound "Trite/Pain"
SeeSound "Trite/Sight"
DeathSound "Trite/Death"
ActiveSound "Trite/Active"
BloodType "Brutal_BloodGreen"
PainChance 200
PainChance "Flames",64
PainChance "Bullet",26
PainChance "SuperFists",255
PainChance "Kick",255
PainChance "SuperKick",255
DamageFactor "Acid",0
DamageFactor "Poison",0
DamageFactor "CauseWaterSplash",0.0
DamageFactor "CauseObjectsToSplash",0.0
DamageFactor "Jump",50
DamageFactor "Kick",1.5
DamageFactor "SuperKick",1.5
DamageFactor "Flames",1.5
DamageFactor "Plasma",1.5
DamageFactor "BFG",1.5
DropItem "DemonEssenceSpawner"
Tag "$FO_TAG_TRITE"
HitObituary "$FO_OB_TRITE"
Species "Trite"
MONSTER
-COUNTKILL
-SOLID
+FLOORCLIP
+DONTHARMSPECIES
+NOBLOODDECALS
+NOTARGET
+NEVERFAST
+THRUSPECIES
	States
	{
	Spawn:
		TSPI A 1 A_LookEx
		Loop
	See:
		TSPI A 2 A_Chase("","",0)
		TSPI A 2 A_Chase
		TNT1 A 0 A_PlaySound("Trite/Step",CHAN_7)
		TSPI BB 2 A_Chase("Melee","Missile",2)
		TNT1 A 0 A_PlaySound("Trite/Step",CHAN_7)
		TSPI CC 2 A_Chase
		TNT1 A 0 A_PlaySound("Trite/Step",CHAN_7)
		TSPI DD 2 A_Chase("Melee","Missile",2)
		TNT1 A 0 A_PlaySound("Trite/Step",CHAN_7)
		Loop
	Melee:
		TSPI E 6 A_FaceTarget
		TNT1 A 0 A_Jump(192,2)
		TNT1 A 0 A_PlaySound("Trite/Attack",CHAN_BODY)
		TSPI F 6 A_CustomMeleeAttack(random(1,3)*2,"Trite/MeleeHit","")
		Goto See
	Missile:
		TNT1 A 0 A_Jump(92,"See")
		TSPI GHH 3 A_FaceTarget
		TNT1 A 0 A_PlaySound("Trite/Attack",CHAN_BODY)
		TNT1 A 0 A_SkullAttack
		TSPI I 10 ThrustThingZ(0,10,0,1)
		TNT1 A 0 A_CustomMeleeAttack(random(1,3)*2,"Trite/MeleeHit","")
		TSPI J 8 A_Stop
		Goto See
/*	Web:	//unused
		TNT1 A 0 A_ChangeFlag("LowGravity",1)
		TNT1 A 0 A_ChangeFlag("NoDamage",1)
		TNT1 A 0 A_ChangeFlag("NoPain",1)
		TNT1 A 0 A_PlaySound("Trite/WebStart",CHAN_BODY)
		TSPI F 1 A_CheckFloor(1)
		Goto Web+5
		TNT1 A 0 A_ChangeFlag("NoDamage",0)
		TNT1 A 0 A_ChangeFlag("NoPain",0)
		TNT1 A 0 A_ChangeFlag("LowGravity",0)
		TSPI A 12 A_PlaySound("Trite/WebEnd",CHAN_BODY)
		Goto Spawn*/
	Pain:
		TNT1 AA 0 A_SpawnItemEx("64AcidProjectileD",0,0,10,random(-6,6),random(-6,6),random(4,6),random(0,359),0)
		TSPI K 6 A_Pain
		Goto See
	Death:
		TSPI K 5 A_Scream
		TNT1 A 0 A_PlaySound("misc/xdeath2",CHAN_BODY)
		TNT1 A 0 A_PlaySound("Trite/Splat",CHAN_7)
		TSPI L 5 A_NoBlocking
		TNT1 AAA 0 A_CustomMissile("BloodmistGreen",1,0,random(0,360),2,random(0,90))
		TNT1 AAAAAA 0 A_SpawnItemEx("64AcidProjectileD",0,0,1,random(-6,6),random(-6,6),random(4,6),random(0,359),0)
		TNT1 A 0 A_SpawnItemEx("BigBloodSpotGreen")
		TSPI MNOPQ 5
		Stop
	Ice:
		TSPI "#" 2 ACS_NamedExecuteAlways("IceDeathTranslate")
		"####" "#" 5 A_FreezeDeath
		"####" "#" 1 A_FreezeDeathChunks
		wait
	}
}