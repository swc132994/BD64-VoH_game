Actor ChainsawSpawner : CustomInventory Replaces Chainsaw
{
//$category "Game/BD64_Weapons
Scale 0.82
Tag "Chainsaw"
Inventory.PickupMessage "$GOTCHAINSAW"
Inventory.PickupSound "weapons/chainsaw/draw"
	States
	{
	Spawn:
		CSAW A -1
		Stop
	Pickup:
		TNT1 A 0 A_JumpIfInventory("IsTacticalClass",1,"Pickup2")
		TNT1 A 0 A_JumpIfInventory("IsModern",1,"Pickup2")
		TNT1 A 0 A_GiveInventory("64Chainsaw")
		Stop
	Pickup2:
		TNT1 A 0 A_GiveInventory("64ChainsawTactical",1)
		stop
	}
}

Actor 64Chainsaw : Weapon
{
Scale 0.82
Weapon.SlotNumber 1
Weapon.SlotPriority 2
Weapon.SelectionOrder 2200
Weapon.Kickback 0
Weapon.ReadySound "weapons/sawidle"
Inventory.PickupMessage "$GOTCHAINSAW"
+WEAPON.AXEBLOOD
Tag "$TAG_CHAINSAW"
VisibleToPlayerClass "64DoomPlayer"
Inventory.ForbiddenTo "Tactical64DoomPlayer","Tactical64DoomPlayer_p","Modern"
+INVENTORY.RESTRICTABSOLUTELY
	States
	{
	Spawn:
		CWAS A -1
		Stop
	Select:
		TNT1 A 0 A_PlaySound("weapons/sawup",CHAN_AUTO)
		WASG C 1 A_Raise
		Wait
	Ready:
		WASG CD 5 A_WeaponReady
		Loop
	Fire:
		WASG HI 1
	Hold:
		TNT1 A 0 A_StopSound(CHAN_5)
		TNT1 A 0 A_PlaySound("weapons/chainsaw/loop",CHAN_WEAPON)
		WASG A 2 A_FireCustomMissile("SawDamage",15,0)
		WASG B 2 A_FireCustomMissile("SawDamage",-15,0)
		WASG E 2 A_FireCustomMissile("SawDamage",15,0)
		WASG F 2 A_FireCustomMissile("SawDamage",-15,0)
		TNT1 A 0 A_ReFire
		TNT1 A 0 A_PlaySound("weapons/chainsaw/Stop",CHAN_ITEM)
		TNT1 A 0 A_PlaySound("weapons/chainsaw/Stop",CHAN_WEAPON)
		WASG IH 1
		Goto Ready
	Deselect:
		WASG C 1 A_Lower
		Wait
	}
}

Actor SawDamage : FastProjectile
{
Damage(Random(5,6))
DamageType "Chainsaw"
Speed 25
Radius 2
Height 2
Alpha 0.7
RenderStyle Add
Decal "SawVertical"
SeeSound "none"
DeathSound "none"
Obituary "$OB_MPCHAINSAW"
+FORCEXYBILLBOARD
+EXTREMEDEATH
+BLOODSPLATTER
	States
	{
	Spawn:
		TNT1 A 2
		Stop
	Death:
		TNT1 A 0 A_SpawnItemEx("SSawPuff")
		TNT1 A 0 A_PlaySound("DSSAWAL",CHAN_WEAPON)
		TNT1 A 0 Radius_Quake(2,3,0,4,0)
		TNT1 A 10
		Stop
	XDeath:
		TNT1 A 0 A_PlaySound("CSAWFLESH",CHAN_WEAPON)
		TNT1 A 0 Radius_Quake(2,3,0,4,0)
		TNT1 A 10
		Stop
	}
}

Actor ClassicChainsawToken : Inventory
{
	Inventory.MaxAmount 1
}

Actor 64ChainsawTACTICAL : Brutal64Weapon	//CHAINSAW FOR TACTICAL CLASS
{
Scale 0.82
Weapon.SlotNumber 1
Weapon.SlotPriority 2
Weapon.SelectionOrder 2200
Weapon.Kickback 0
Weapon.AmmoType "SawGas"
Weapon.AmmoUse 1
Weapon.AmmoGive 100
Inventory.PickupMessage "$GOTCHAINSAW"
Obituary "$OB_MPCHAINSAW"
Tag "$TAG_CHAINSAW"
+WEAPON.MELEEWEAPON
+WEAPON.AXEBLOOD
+WEAPON.ALT_USES_BOTH
+INVENTORY.RESTRICTABSOLUTELY
Inventory.forbiddento "64DoomPlayer"
VisibleToPlayerClass "Tactical64DoomPlayer","Tactical64DoomPlayer_P","Modern"
Weapon.SisterWeapon "64ChainsawTactical_Classic"
+NOAUTOFIRE
+NOALERT
	States
	{
	MineThrowFlash:
		SAWS ONDCBA 1
		TNT1 A 11
		SAWS ABCDNOPQR 1
		Stop
	KickingFlash:
		TNT1 A 0 A_PlaySound("weapons/sawidle",CHAN_5)
		SAWS RQPON 1
		SAWS D 6
		SAWS NOPQR 1
		Stop
	AirKickingFlash:
		TNT1 A 0 A_PlaySound("weapons/sawidle",CHAN_5)
		SAWS RQPON 1
		SAWS D 8
		SAWS NOPQR 1
		Stop
	SSGFlash:
		TNT1 A 0 A_PlaySound("weapons/sawidle",CHAN_5)
		SAWS RQPON 1
		SAWS D 20
		SAWS NOPQR 1
		Stop
	SlideKickingStart:
		TNT1 A 0 A_PlaySound("weapons/sawidle",CHAN_5)
		SAWS RQPON 1
		SAWS D 17
	SlideKickingEnd:
		SAWS D 5
		SAWS NOPQR 1
		Stop
	ClassicSawSelected:	//Deselect animation
		SAWS RQPONDCBA 1 Offset(0,32)
		TNT1 A 3
	ClassicSawSelected2:	//Classic select animation
		TNT1 A 0 A_PlaySound("weapons/sawup",CHAN_7)
		WASS ABCD 1 Offset(0,32)
		TNT1 A 0 A_GiveInventory("SisterWeaponSelector",1)
	Spawn:
		CSAW A -1
		Stop
	Deselect:
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Takeinventory("WeaponSpecialAction",0)
		TNT1 A 0 A_Takeinventory("TossMine",0)
		TNT1 A 0 A_Takeinventory("PlaceMine",0)
		TNT1 A 0 A_Takeinventory("Kicking",0)
		TNT1 A 0 A_SetCrosshair(0)
		TNT1 A 0 A_StopSound(CHAN_WEAPON)
		TNT1 A 0 A_StopSound(CHAN_ITEM)
		TNT1 A 0 A_StopSound(CHAN_5)
		SAWS RQPONDCBA 1
		TNT1 A 0 A_Takeinventory("SisterWeaponSelector",0)
		TNT1 A 0 A_Lower
		Wait
	Select:
		TNT1 A 0 A_TakeInventory("SisterWeaponSelector",0)
		TNT1 A 0 A_JumpIfInventory("ClassicChainsawToken",1,"ClassicSawSelected2")
		TNT1 A 0 A_JumpIfInventory("SawGas",1,"Select2")
		SAWS ABCDNOPQR 1 Offset(0,32)
		TNT1 A 0 A_Raise
		Wait
	Select2:
		SAWS ABCDDDDD 1 Offset(0,32)
		TNT1 A 0 A_PlaySound("DSSAWZIP",CHAN_WEAPON)
		SAWS EFGHIJKLM 1 Offset(0,32)
		TNT1 A 0 A_PlaySound("weapons/chainsaw/draw",CHAN_ITEM)
		SAWT AB 1 Offset(0,32)
		SAWS NOPQR 1 Offset(0,32)
		TNT1 A 0 A_Raise
		Wait
	v1Ready:
	Ready:
		TNT1 A 0 A_JumpIfInventory("ClassicChainsawToken",1,"ClassicSawSelected")
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		TNT1 A 0 A_JumpIfInventory("SawGas",1,1)
		Goto NotReady
		TNT1 A 0 A_PlaySound("weapons/sawidle",CHAN_5)
		SAWG ABCD 2 A_WeaponReady(WRF_ALLOWRELOAD)
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		TNT1 A 0 A_PlaySound("weapons/sawidle",CHAN_5)
		SAWG ABCD 2 A_WeaponReady(WRF_ALLOWRELOAD)
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		TNT1 A 0 A_PlaySound("weapons/sawidle",CHAN_5)
		SAWG ABCD 2 A_WeaponReady(WRF_ALLOWRELOAD)
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		TNT1 A 0 A_PlaySound("weapons/sawidle",CHAN_5)
		SAWG ABCD 2 A_WeaponReady(WRF_ALLOWRELOAD)
		TNT1 A 0 A_TakeInventory("SawGas",1)
		Loop
	NotReady:
		TNT1 A 0 A_JumpIfInventory("ClassicChainsawToken",1,"ClassicSawSelected")
		SAWG A 5 A_WeaponReady
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		TNT1 A 0 A_JumpIfInventory("SawGas",1,"Select")
		Loop
	Fire:
		TNT1 A 0 A_JumpIfInventory("SawGas",1,1)
		Goto FinishFire
		SAWF AB 1
	Hold:
		TNT1 A 0 A_StopSound(CHAN_5)
		TNT1 A 0 A_PlaySound("weapons/chainsaw/loop",CHAN_WEAPON)
		TNT1 A 0 A_AlertMonsters
		SAWF C 1 Offset(0,36) A_FireCustomMissile("SawDamage",15)
		SAWF C 1 Offset(-1,34) A_FireCustomMissile("SawDamage",-15,0)
		SAWF C 1 Offset(0,35) A_FireCustomMissile("SawDamage",15,0)
		SAWF D 1 Offset(1,33) A_FireCustomMissile("SawDamage",-15,0)
		SAWF C 1 Offset(0,35) A_FireCustomMissile("SawDamage",15,0)
		SAWF C 1 Offset(-1,34) A_FireCustomMissile("SawDamage",-15,0)
		SAWF D 1 Offset(0,37) A_FireCustomMissile("SawDamage",15,0)
		SAWF D 1 Offset(1,34) A_FireCustomMissile("SawDamage",-15,0)
		TNT1 A 0 A_ReFire
		TNT1 A 0 A_PlaySound("weapons/chainsaw/Stop",CHAN_ITEM)
		TNT1 A 0 A_PlaySound("weapons/chainsaw/Stop",CHAN_WEAPON)
	FinishFire:
		SAWF BA 1
		Goto Ready
	AltFire:
		TNT1 A 0 A_JumpIfInventory("64SuperShotgunTactical",1,"AmmoCheck")
		TNT1 A 0 A_Print("You Need a Super Shotgun",1)
		Goto Ready
	NeedReloading:
		TNT1 A 0 A_PlaySound("DRYFIRE")
		TNT1 A 0 A_Print("Super Shotgun Needs Reloading",1)
		Goto Ready
	SSGFire:
		TNT1 A 0 A_JumpIfInventory("SSGAlt",1,"AltFire2")
		TNT1 A 10 A_GunFlash("SSGFlash")
		OSSS ABCD 1 Offset(0,50)
		TNT1 A 0 A_SetAngle(angle + random(-10,10))
		TNT1 AAAA 0 A_FireCustomMissile("GunFireSmoke",0,0)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,22)
		TNT1 AAAAAAAAAA 0 A_FireCustomMissile("Tracer",random(-6,6),0,-1,-12,0,random(-5,5))
		TNT1 A 0 A_FireBullets(8.5,6.5,20,6,"ShotPuff",0)
		Goto SSGAmmoCheck
	ResumeFire:
		TNT1 A 0 A_PlaySound("SSGFIRE",CHAN_WEAPON)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_ZoomFactor(0.75)
		OSSF AB 1 Bright
		TNT1 A 0 A_ZoomFactor(1.0)
		OSSF C 3 A_SetPitch(pitch-4,SPF_INTERPOLATE)
		OSSF DE 1 A_SetPitch(pitch+2.6,SPF_INTERPOLATE)
		OSSF F 1 A_SetPitch(pitch+1.2,SPF_INTERPOLATE)
		OSSG A 1 A_SetPitch(pitch+0.8,SPF_INTERPOLATE)
		OSSG A 1 A_SetPitch(pitch+0.4,SPF_INTERPOLATE)
		OSSS DCBA 1 Offset(0,50)
		Goto Ready
	AltFire2:
		TNT1 A 10 A_GunFlash("SSGFlash")
		OSSS ABCDEF 1 Offset(0,50)
		TNT1 A 0 A_SetAngle(angle + random(-6,6))
		TNT1 AA 0 A_FireCustomMissile("GunFireSmoke",0,0)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,22)
		TNT1 AAAA 0 A_FireCustomMissile("Tracer",random(-6,6),0,-1,-12,0,random(-5,5))
		TNT1 A 0 A_FireBullets(7.5,5.5,10,6,"ShotgunPuff",0)
		Goto AltSSGAmmoCheck
	ResumeAltFire:
		TNT1 A 0 A_PlaySound("SGFIRE",CHAN_WEAPON)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_ZoomFactor(0.95)
		OSSU AB 1 Bright
		TNT1 A 0 A_ZoomFactor(1.0)
		OSSF E 3 A_SetPitch(pitch-2,SPF_INTERPOLATE)
		TNT1 A 0 Offset(-4,32)
		OSSF E 1 A_SetPitch(pitch+1.3,SPF_INTERPOLATE)
		TNT1 A 0 Offset(-3,32)
		OSSF F 1 A_SetPitch(pitch+0.6,SPF_INTERPOLATE)
		TNT1 A 0 Offset(-2,32)
		OSSG A 1 A_SetPitch(pitch+0.4,SPF_INTERPOLATE)
		TNT1 A 0 Offset(-1,32)
		OSSG A 1 A_SetPitch(pitch+0.2,SPF_INTERPOLATE)
		TNT1 A 0 Offset(0,32)
		OSSS FEDCBA 1 Offset(0,32)
		Goto Ready
	Reload:
		SAWS RQPON 1
		SAWS D 1
		SAWG A 0 A_PlaySound("DSSAWZIP",CHAN_WEAPON)
		SAWS EFGHHGFE 1
		TNT1 A 0 A_PlaySound("weapons/chainsaw/draw",CHAN_ITEM)
		SAWT AB 2 
		SAWS NOPQR 1
		Goto Ready
	AmmoCheck:
		TNT1 A 0 A_JumpIfInventory("SSGAmmo",1,"SSGFire")
		Goto NeedReloading
	SSGAmmoCheck:
		TNT1 A 0 A_TakeInventory("SSGAmmo",2)
		TNT1 A 0 A_JumpIfInventory("NoReloading",1,"SSGNoReload")
		Goto ResumeFire
	SSGNoReload:
		TNT1 A 0 A_TakeInventory("Shell",2,TIF_NOTAKEINFINITE)
		Goto ResumeFire
	AltSSGAmmoCheck:
		TNT1 A 0 A_TakeInventory("SSGAmmo",1)
		TNT1 A 0 A_JumpIfInventory("NoReloading",1,"AltNoReload")
		TNT1 A 0 A_TakeInventory("SSGAlt",1)
		Goto ResumeAltFire
	AltNoReload:
		TNT1 A 0 A_TakeInventory("Shell",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_TakeInventory("SSGAlt",1)
		Goto ResumeAltFire
	}
}

Actor SawGas : Ammo
{
Scale 0.9
SpawnID 11
Inventory.PickupMessage "You found some fuel!"
Inventory.Amount 50
Inventory.MaxAmount 600
Ammo.BackpackAmount 100
Ammo.BackpackMaxAmount 600
Inventory.Icon "GSLNA0"
+IGNORESKILL
VisibleToPlayerClass "Tactical64DoomPlayer","Tactical64DoomPlayer_p","Modern"
Inventory.ForbiddenTo "64DoomPlayer"
+INVENTORY.RESTRICTABSOLUTELY
	States
	{
	Spawn:
		GSLN A -1
		Stop
	}
}

//CLASSICAL LOOK
Actor 64ChainsawTactical_Classic : Brutal64Weapon
{
Scale 0.82
Weapon.SlotNumber 1
Weapon.SlotPriority 2
Weapon.SelectionOrder 2200
Weapon.Kickback 0
Weapon.AmmoType "SawGas"
Weapon.AmmoUse 1
Weapon.AmmoGive 100
Inventory.PickupMessage "$GOTCHAINSAW"
Obituary "$OB_MPCHAINSAW"
Tag "$TAG_CHAINSAW"
+WEAPON.MELEEWEAPON
+WEAPON.AXEBLOOD
+POWERED_UP
+INVENTORY.RESTRICTABSOLUTELY
Inventory.forbiddento "64DoomPlayer"
VisibleToPlayerClass "Tactical64DoomPlayer","Tactical64DoomPlayer_P","Modern"
Weapon.SisterWeapon "64ChainsawTactical"
	States
	{
	MineThrowFlash:
		WASS EFGH 1
		TNT1 A 18
		WASS ABCD 1
		Stop
	KickingFlash:
		TNT1 A 0 A_PlaySound("weapons/sawidle",CHAN_5)
		WASS EFGH 1
		WASS H 8
		WASS HGFE 1
		Stop
	AirKickingFlash:
		TNT1 A 0 A_PlaySound("weapons/sawidle",CHAN_5)
		WASS EFGH 1
		WASS H 10
		WASS HGFE 1
		Stop
	SlideKickingStart:
		TNT1 A 0 A_PlaySound("weapons/sawidle",CHAN_5)
		WASS EFGH 1
		WASS H 18
	SlideKickingEnd:
		WASS H 6
		WASS HGFE 1
		Stop
	Spawn:
		CSAW A -1
		Stop
	Deselect:
		TNT1 A 0 A_Takeinventory("WeaponSpecialAction",0)
		TNT1 A 0 A_Takeinventory("TossMine",0)
		TNT1 A 0 A_Takeinventory("PlaceMine",0)
		TNT1 A 0 A_Takeinventory("Kicking",0)
		WASS EFGH 1
		TNT1 A 0 A_Takeinventory("SisterWeaponSelector",0)
		TNT1 A 0 A_Lower
		Wait
	Select:
		TNT1 A 0 A_JumpIfInventory("ClassicChainsawToken",1,2)
		TNT1 A 0 A_TakeInventory("SisterWeaponSelector",0)
		TNT1 A 0 A_JumpIfInventory("SawGas",1,1)
		Goto SelectNoFuel
		TNT1 A 0 A_PlaySound("weapons/sawup",CHAN_7)
	SelectNoFuel:
		WASS ABCD 1 Offset(0,32)
		TNT1 A 0 A_Raise
		Wait
	v1Ready:
	Ready:
		TNT1 A 0 A_JumpIfInventory("ClassicChainsawToken",1,2)
		TNT1 A 0 A_TakeInventory("SisterWeaponSelector",0)
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		TNT1 A 0 A_JumpIfInventory("SawGas",1,1)
		Goto NotReady
		TNT1 A 0 A_PlaySound("weapons/sawidle",CHAN_5)
		WASG CD 4 A_WeaponReady
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		TNT1 A 0 A_PlaySound("weapons/sawidle",CHAN_5)
		WASG CD 4 A_WeaponReady
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		TNT1 A 0 A_PlaySound("weapons/sawidle",CHAN_5)
		WASG CD 4 A_WeaponReady
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		TNT1 A 0 A_PlaySound("weapons/sawidle",CHAN_5)
		WASG CD 4 A_WeaponReady
		TNT1 A 0 A_TakeInventory("SawGas",1)
		Loop
	NotReady:
		WASG C 5 A_WeaponReady
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("SawGas",1,"Select")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		Loop
	Fire:
		WASG HI 1
	Hold:
		TNT1 A 0 A_StopSound(CHAN_5)
		TNT1 A 0 A_PlaySound("weapons/chainsaw/loop",CHAN_WEAPON)
		WASG A 1 A_FireCustomMissile("SawDamage",15)
		WASG BBB 1 A_FireCustomMissile("SawDamage",-15,0)
		WASG EEE 1 A_FireCustomMissile("SawDamage",15,0)
		WASG FFF 1 A_FireCustomMissile("SawDamage",-15,0)
		TNT1 A 0 A_ReFire
		TNT1 A 0 A_PlaySound("weapons/chainsaw/Stop",CHAN_ITEM)
		TNT1 A 0 A_PlaySound("weapons/chainsaw/Stop",CHAN_WEAPON)
		WASG IH 1
		Goto Ready
	}
}