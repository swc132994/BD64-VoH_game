Actor RocketLauncherSpawner : CustomInventory Replaces RocketLauncher
{
//$category "Game/BD64_Weapons
Scale 0.82
Tag "$FO_TAG_ROCKETLAUNCHER"
Inventory.PickupMessage "$FO_GOTLAUNCHER"
Inventory.PickupSound "WPICK"
	States
	{
	Spawn:
		LAUN A -1
		Stop
	Pickup:
		TNT1 A 0 A_Jumpifinventory("IsTacticalClass",1,"Pickup2")
		TNT1 A 0 A_Jumpifinventory("IsModern",1,"Pickup2")
		TNT1 A 0 A_GiveInventory("64RocketLauncher")
		Stop
	Pickup2:
		TNT1 A 0 A_GiveInventory("64RocketLauncherTactical",1)
		Stop
	}
}

Actor 64RocketLauncher : DoomWeapon
{
Scale 0.82
Weapon.SlotNumber 5
Weapon.SlotPriority 2
Weapon.SelectionOrder 2500
Weapon.AmmoType "RocketAmmo"
Weapon.AmmoUse 1
Weapon.AmmoGive 2
Inventory.PickupMessage "$FO_GOTLAUNCHER"
Tag "$FO_TAG_ROCKETLAUNCHER"
VisibleToPlayerClass "64DoomPlayer"
Inventory.ForbiddenTo "Tactical64DoomPlayer","Tactical64DoomPlayer_p","Modern"
+INVENTORY.RESTRICTABSOLUTELY
	States
	{
	Ready:
		MISG A 1 A_WeaponReady
		Wait
	Deselect:
		MISG A 1 A_Lower
		Wait
	Select:
		MISG A 1 A_Raise
		Wait
	Fire:
		MISG B 2 Bright Offset(0,36)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,22)
		MISG C 2 Bright Offset(0,37) A_GunFlash
		TNT1 A 0 A_FireMissile
		TNT1 A 0 A_Recoil(2)
		MISG DEFFF 1
		MISG FGHI 2 Bright
		MISG A 4
		TNT1 A 0 A_ReFire
		Goto Ready
	Flash:
		TNT1 A 3 Bright A_Light1
		TNT1 A 2 Bright
		TNT1 A 2 Bright A_Light2
		TNT1 A 6 Bright
		Goto LightDone
	Spawn:
		LAUN A -1
		Stop
	}
}

Actor AltMode : Inventory
{
Inventory.MaxAmount 1
}

Actor ClassicRLToken : Inventory
{
Inventory.MaxAmount 1
}

Actor NeverSelectedRL : Inventory
{
Inventory.MaxAmount 1
}

Actor SisterWeaponSelector : PowerWeaponLevel2
{
+UNDROPPABLE
+UNTOSSABLE
-COUNTITEM
-INVBAR
+PERSISTENTPOWER
powerup.duration 2147483647
}

Actor RLAmmo : Ammo
{
Inventory.Amount 0
Inventory.MaxAmount 6
Ammo.BackpackAmount 0
Ammo.BackpackMaxAmount 6
Inventory.Icon "LAUNA0"
+IGNORESKILL
}

Actor 64RocketLauncherTactical : Brutal64Weapon
{
Scale 0.82
Weapon.SlotNumber 5
Weapon.SlotPriority 2
Weapon.SelectionOrder 2500
Weapon.AmmoType1 "RocketAmmo"
Weapon.AmmoUse1 1
Weapon.AmmoGive1 2
Weapon.AmmoType2 "RLAmmo"
Weapon.AmmoUse2 1
Weapon.AmmoGive2 0
Inventory.PickupMessage "$FO_GOTLAUNCHER"
Tag "$FO_TAG_ROCKETLAUNCHER"
+INVENTORY.RESTRICTABSOLUTELY
+WEAPON.NOAUTOFIRE
+WEAPON.NOALERT
+WEAPON.AMMO_CHECKBOTH
+WEAPON.PRIMARY_USES_BOTH
Weapon.SisterWeapon "64ClassicRocketLauncherTactical"
	States
	{
	MineThrowFlash:
		RLTS EFGH 1
		TNT1 A 18
		RLTS ABCD 1
		Stop
	KickingFlash:
		RLHK A 0 A_JumpIfInventory("AltMode",1,"HeavyKickingFlash")
		RLLG A 0 A_JumpIfInventory("RocketAmmo",3,"KickingFlash2")
		RLLG G 0 A_JumpIfInventory("RocketAmmo",2,"KickingFlash3")		
		RLLG A 0 A_JumpIfInventory("RLAmmo",3,"KickingFlash2")
		RLLG G 0 A_JumpIfInventory("RLAmmo",2,"KickingFlash3")
		RLLK KLMN 1
		RLLK O 8
		RLLK NMLK 1
		Stop
	KickingFlash2:
		RLLK ABCD 1
		RLLK E 8
		RLLK DCBA 1
		Stop
	KickingFlash3:
		RLLK FGHI 1
		RLLK J 8
		RLLK IHGF 1
		Stop
	SlideKickingStart:
		RLHK A 0 A_JumpIfInventory("AltMode",1,"HeavySlideKickingStart")
		RLLG A 0 A_JumpIfInventory("RocketAmmo",3,"SlideKickingStart2")
		RLLG G 0 A_JumpIfInventory("RocketAmmo",2,"SlideKickingStart3")		
		RLLG A 0 A_JumpIfInventory("RLAmmo",3,"SlideKickingStart2")
		RLLG G 0 A_JumpIfInventory("RLAmmo",2,"SlideKickingStart3")
		RLLK KLMN 1
		RLLK O 35
	SlideKickingEnd:
		RLHK A 0 A_JumpIfInventory("AltMode",1,"HeavySlideKickingEnd")
		RLLG A 0 A_JumpIfInventory("RocketAmmo",3,"SlideKickingEnd2")
		RLLG G 0 A_JumpIfInventory("RocketAmmo",2,"SlideKickingEnd3")		
		RLLG A 0 A_JumpIfInventory("RLAmmo",3,"SlideKickingEnd2")
		RLLG G 0 A_JumpIfInventory("RLAmmo",2,"SlideKickingEnd3")
		RLLK O 2
		RLLK NMLK 1
		Stop
	SlideKickingStart2:
		RLLK ABCD 1
		RLLK E 35
	SlideKickingEnd2:
		RLLK E 2
		RLLK DCBA 1
		Stop
	SlideKickingStart3:
		RLLK FGHI 1
		RLLK J 35
	SlideKickingEnd3:
		RLLK J 2
		RLLK IHGF 1
		Stop		
	HeavyKickingFlash:
		RLHG A 0 A_JumpIfInventory("RocketAmmo",3,"HeavyKickingFlash2")
		RLHG G 0 A_JumpIfInventory("RocketAmmo",2,"HeavyKickingFlash3")
		RLHG A 0 A_JumpIfInventory("RLAmmo",3,"HeavyKickingFlash2")
		RLHG G 0 A_JumpIfInventory("RLAmmo",2,"HeavyKickingFlash3")
		RLHK KLMN 1
		RLHK O 8
		RLHK NMLK 1
		Stop
	HeavySlideKickingStart:
		RLHG A 0 A_JumpIfInventory("RocketAmmo",3,"HeavySlideKickingStart2")
		RLHG G 0 A_JumpIfInventory("RocketAmmo",2,"HeavySlideKickingStart3")
		RLHG A 0 A_JumpIfInventory("RLAmmo",3,"HeavySlideKickingStart2")
		RLHG G 0 A_JumpIfInventory("RLAmmo",2,"HeavySlideKickingStart3")
		RLHK KLMN 1
		RLHK O 35
	HeavySlideKickingEnd:
		RLHG A 0 A_JumpIfInventory("RocketAmmo",3,"HeavySlideKickingEnd2")
		RLHG G 0 A_JumpIfInventory("RocketAmmo",2,"HeavySlideKickingEnd3")	
		RLHG A 0 A_JumpIfInventory("RLAmmo",3,"HeavySlideKickingEnd2")
		RLHG G 0 A_JumpIfInventory("RLAmmo",2,"HeavySlideKickingEnd3")
		RLHK O 2
		RLHK NMLK 1
		Stop
	HeavyKickingFlash2:
		RLHK ABCD 1
		RLHK E 8
		RLHK DCBA 1
		Stop
	HeavySlideKickingStart2:
		RLHK ABCD 1
		RLHK E 35
	HeavySlideKickingEnd2:
		RLHK E 2
		RLHK DCBA 1
		Stop
	HeavyKickingFlash3:
		RLHK FGHI 1
		RLHK J 8
		RLHK IHGF 1
		Stop
	HeavySlideKickingStart3:
		RLHK FGHI 1
		RLHK J 35
	HeavySlideKickingEnd3:
		RLHK J 2
		RLHK IHGF 1
		Stop		
	Spawn:
		LAUN A -1
		Stop
	WarmUp:
		TNT1 A 0 A_TakeInventory("NeverSelectedRL",1)
		RLNR G 1 Offset(48, 55)
		RLNR G 1 Offset(20, 43)
		RLNR G 1 Offset(4, 35)
		RLNR G 1 Offset(2, 33)
		RLNR G 1 Offset(0, 32)
		RLNR KL 1
		RLNR L 4
		TNT1 A 0 A_PlaySound("ROCKLOAD",CHAN_BODY)
		RLNR MNOPQQQRSTUVWXYZ 1 A_WeaponReady
		RLNR "[" 1 A_WeaponReady
		Goto Ready
		Goto Ready
	ClassicRLSelected:	//Deselect animation
		RLTS DCBA 1 Offset(0,32)
		TNT1 A 3
	ClassicRLSelected2:	//Classic select animation
		TNT1 A 0 A_PlaySound("ROCKPKUP",CHAN_BODY)
		MISS ABCD 1 Offset(0,32)
		TNT1 A 0 A_GiveInventory("SisterWeaponSelector",1)
	v1Ready:
	Ready:
		TNT1 A 0 A_JumpIfInventory("ClassicRLToken",1,"ClassicRLSelected")
		RLLG A 0 A_JumpIfInventory("AltMode",1,"HeavyReady")
		RLHG A 0 A_JumpIfInventory("NoReloading",1,"ReadyNoReload")
		RLLG A 0 A_JumpIfInventory("RLAmmo",3,3)
		RLLG G 0 A_JumpIfInventory("RLAmmo",2,2)
		RLLG K 0
		"####" "#" 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		Loop
	ReadyNoReload:
		RLLG A 0 A_JumpIfInventory("RocketAmmo",3,3)
		RLLG G 0 A_JumpIfInventory("RocketAmmo",2,2)
		RLLG K 0
		"####" "#" 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		Loop		
	HeavyReady:
		RLHG A 0 A_JumpIfInventory("NoReloading",1,"HeavyReadyNoReload")
		RLHG A 0 A_JumpIfInventory("RLAmmo",3,3)
		RLHG G 0 A_JumpIfInventory("RLAmmo",2,2)
		RLHG K 0
		"####" "#" 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("ClassicRLToken",1,"ClassicRLSelected")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		Loop
	HeavyReadyNoReload:
		RLHG A 0 A_JumpIfInventory("RocketAmmo",3,3)
		RLHG G 0 A_JumpIfInventory("RocketAmmo",2,2)
		RLHG K 0
		"####" "#" 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		Loop
	ReadyNoAmmo:
		RLNG A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		Loop		
	Select:
		TNT1 A 0 A_TakeInventory("SisterWeaponSelector",0)
		TNT1 A 0 A_JumpIfInventory("ClassicRLToken",1,"ClassicRLSelected2")
		TNT1 A 0 A_JumpIfInventory("NeverSelectedRL",1,"WarmUp")
		TNT1 A 0 A_PlaySound("ROCKPKUP",CHAN_BODY)
		TNT1 A 0 A_JumpIfInventory("RLAmmo",3,"FullSelect")
		TNT1 A 0 A_JumpIfInventory("RLAmmo",2,"HalfSelect")
		RLNS AOPQ 1 A_WeaponReady(WRF_NOFIRE)
		Goto Ready
	FullSelect:
		RLNS ABCD 1 A_WeaponReady(WRF_NOFIRE)
		Goto Ready
	HalfSelect:
		RLNS AIJK 1 A_WeaponReady(WRF_NOFIRE)
		Goto Ready		
	Deselect:	
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Takeinventory("WeaponSpecialAction",0)
		TNT1 A 0 A_Takeinventory("TossMine",0)
		TNT1 A 0 A_Takeinventory("PlaceMine",0)
		TNT1 A 0 A_Takeinventory("Kicking",0)
		TNT1 A 0 A_Takeinventory("SisterWeaponSelector",0)
		TNT1 A 0 A_JumpIfInventory("RLAmmo",3,"FullDeSelect")
		TNT1 A 0 A_JumpIfInventory("RLAmmo",2,"HalfDeSelect")		
		RLNS RSTH 1
		TNT1 A 0 A_Lower
		Wait
	FullDeSelect:
		RLNS EFGH 1
		TNT1 A 0 A_Lower
		Wait
	HalfDeSelect:
		RLNS LMNH 1
		TNT1 A 0 A_Lower
		Wait
	Fire:
		TNT1 A 0 A_JumpIfInventory("NoReloading",1,"NoReload")
		TNT1 A 0 A_JumpIfInventory("RLAmmo",1,1)
		Goto Reload
		TNT1 A 0 A_TakeInventory("RLAmmo",1,TIF_NOTAKEINFINITE)
		Goto ResumeFire
	NoReload:
		TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,1)
		Goto Ready
		TNT1 A 0 A_TakeInventory("RocketAmmo",1,TIF_NOTAKEINFINITE)	
	ResumeFire:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_ZoomFactor(0.97)
		TNT1 A 0 A_JumpIfInventory("AltMode",1,"HeavyFire")
		TNT1 A 0 A_Recoil(2)
		TNT1 A 0 A_FireCustomMissile("64rocket",0,0)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,22)
		RLHG A 0 A_JumpIfInventory("NoReloading",1,"RecoilFireNoReload")
		RLHG A 0 A_JumpIfInventory("RLAmmo",2,"FireFull")
		RLHG G 0 A_JumpIfInventory("RLAmmo",1,"FireHalf")
		Goto FireLast
	RecoilFireNoReload:
		RLHG A 0 A_JumpIfInventory("RocketAmmo",2,"FireFull")
		RLHG G 0 A_JumpIfInventory("RocketAmmo",1,"FireHalf")
		Goto FireLast		
	FireFull:	
		RLLF ABC 1 Bright offset(0,33)
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_SetPitch(pitch-2,SPF_INTERPOLATE)
		RLLF DEFGHI 2
		RLLF JKLMN 1
		RLLG A 5
	LoadAnimation:
		RLLG BCD 2
		RLLG E 0 A_JumpIfInventory("NoReloading",1,"LoadAnimationNoReload")
		RLLG E 0 A_JumpIfInventory("RLAmmo",3,2)
		RLLG F 0
		RLLG "#" 2
		TNT1 A 0 A_ReFire
		Goto Ready
	LoadAnimationNoReload:
		RLLG E 0 A_JumpIfInventory("RocketAmmo",3,2)
		RLLG F 0
		RLLG "#" 2
		TNT1 A 0 A_ReFire
		Goto Ready	
	FireHalf:
		RLLF OPQ 1 Bright
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_SetPitch(pitch+0.8,SPF_INTERPOLATE)
		RLLF DEFGHI 2
		RLLF RSTUV 1
		RLLG G 5
	LoadAnimation2:
		RLLG HIJK 2
		TNT1 A 0 A_ReFire
		Goto Ready
	FireLast:
		RLLF WXY 1 Bright
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_SetPitch(pitch+0.8,SPF_INTERPOLATE)
		RLLF DEFGHI 2
		RLLF Z 1
		RLLF "[\]" 1
		2LLF A 1
		RLLG K 5
		Goto Ready
	HeavyFire:
		TNT1 A 0 A_Recoil(3)
		TNT1 A 0 A_FireCustomMissile("TacticalRocket",0,0)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,22)
		RLHG A 0 A_JumpIfInventory("NoReloading",1,"RecoilHeavyFireNoReload")
		RLHG A 0 A_JumpIfInventory("RLAmmo",2,"HeavyFireFull")
		RLHG G 0 A_JumpIfInventory("RLAmmo",1,"HeavyFireHalf")
		Goto HeavyFireLast
	RecoilHeavyFireNoReload:	
		RLHG A 0 A_JumpIfInventory("RocketAmmo",2,"HeavyFireFull")
		RLHG G 0 A_JumpIfInventory("RocketAmmo",1,"HeavyFireHalf")
		Goto HeavyFireLast		
	HeavyFireFull:
		RLHF ABC 1 Bright
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_SetPitch(pitch+0.8,SPF_INTERPOLATE)
		RLHF DDEEFFGGHI 2
		RLHF JKLMN 1		
		RLHG A 10		
	HeavyLoadAnimation:
		RLHG BCD 2
		RLLG E 0 A_JumpIfInventory("NoReloading",1,"HeavyLoadAnimationNoReload")		
		RLHG E 0 A_JumpIfInventory("RLAmmo",3,2)
		RLHG F 0
		RLHG "#" 2
		TNT1 A 0 A_ReFire
		Goto Ready
	HeavyLoadAnimationNoReload:	
		RLHG E 0 A_JumpIfInventory("RocketAmmo",3,2)
		RLHG F 0
		RLHG "#" 2
		TNT1 A 0 A_ReFire
		Goto Ready
	HeavyFireHalf:
		RLHF OPQ 1 Bright
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_SetPitch(pitch+0.8,SPF_INTERPOLATE)
		RLHF DDEEFFGGHI 2
		RLHF RSTUV 1		
		RLHG G 5		
	HeavyLoadAnimation2:
		RLHG HIJK 2
		TNT1 A 0 A_ReFire
		Goto Ready	
	HeavyFireLast:
		RLHF WXY 1 Bright
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_SetPitch(pitch+0.8,SPF_INTERPOLATE)
		RLHF DDEEFFGGHI 2
		RLHF Z 1
		RLHF "[\]" 1
		2LHF A 1		
		RLHG K 5
		Goto Ready
	Reload:
		RLTG A 0 A_JumpIfInventory("AltMode",1,3)
		RLTL A 0 A_JumpIfInventory("RLAmmo",1,2)
		RLTN A 0 A_Jump(256,2)
		"####" A 0 A_JumpIfInventory("RLAmmo",1,2)
		RLTN A 0
		"####" A 0 A_WeaponReady(WRF_NOFIRE)
		RLTG A 0 A_JumpIfInventory("RLAmmo",0,"Ready")
		RLTG A 0 A_JumpIfInventory("RocketAmmo",1,1)
		Goto Ready
		RLTG A 0 A_PlaySound("ROCKREL",CHAN_BODY)
		RLNR ABCDE 1
		TNT1 A 0 A_FireCustomMissile("RocketCaseSpawn",-20,0,10,-20)
		RLNR EF 2
		RLNR GHIJKLLLLLLL 1
		TNT1 A 0 A_PlaySound("ROCKLOAD",CHAN_BODY)
		RLNR MNOPQQQQ 1
		RLNR RST 1
	InsertRounds:
		RLTG A 0 A_JumpIfInventory("RLAmmo",0,"FinishReload")
		RLTG A 0 A_JumpIfInventory("RocketAmmo",1,1)
		Goto Ready
		TNT1 A 0 A_Giveinventory("RLAmmo",1)
		TNT1 A 0 A_Takeinventory("RocketAmmo",1,TIF_NOTAKEINFINITE)
		Goto InsertRounds	
	FinishReload:
		RLNR UVVWWXXYZ 1 A_WeaponReady(WRF_ALLOWZOOM)
		RLNR "[" 1 A_WeaponReady(WRF_ALLOWZOOM)
		RLNG BCDE 2 A_WeaponReady(WRF_ALLOWZOOM)
		Goto Ready
	Altfire:
		TNT1 A 0 A_JumpIfInventory("NoReloading",1,"AltFireNoReload")
		TNT1 A 0 A_JumpIfInventory("AltMode",1,"ReturnToLaser")
		TNT1 A 0 A_JumpIfInventory("RLAmmo",3,"FullAltfire")
		TNT1 A 0 A_JumpIfInventory("RLAmmo",2,"HalfAltfire")
	AltFireAnimation:	
		2LLG ABCDE 1
		2LLG FGH 2
		2LLG I 1 A_PlaySound("BEP",CHAN_ITEM)
		TNT1 A 0 A_Print("Heavy Payload Rockets Enabled",1)
		TNT1 A 0 A_GiveInventory("AltMode",1)
		TNT1 A 0 A_PlaySound("LASEROFF",CHAN_WEAPON)
		2LHG ABBCCDD 1
		2LHG EFGHI 1
		Goto Ready		
	FullAltFire:	
		RLLG LMNOP 1
		RLLG QRS 2
		RLLG T 1 A_PlaySound("BEP",CHAN_ITEM)
		TNT1 A 0 A_Print("Heavy Payload Rockets Enabled",1)
		TNT1 A 0 A_GiveInventory("AltMode",1)
		TNT1 A 0 A_PlaySound("LASEROFF",CHAN_WEAPON)
		RLHG LMMNNOO 1
		RLHG PQRST 1
		Goto Ready
	HalfAltFire:
		RLLG UVWXY 1
		RLLG Z 2
		RLLG "[\]" 2 A_PlaySound("BEP",CHAN_ITEM)
		TNT1 A 0 A_Print("Heavy Payload Rockets Enabled",1)
		TNT1 A 0 A_GiveInventory("AltMode",1)
		TNT1 A 0 A_PlaySound("LASEROFF",CHAN_WEAPON)
		RLHG UVVWWXX 1
		RLHG YZ 1
		RLHG "[\]" 1
		Goto Ready
	AltFireNoReload:
		TNT1 A 0 A_JumpIfInventory("RocketAmmo",3,"FullAltfire")
		TNT1 A 0 A_JumpIfInventory("RocketAmmo",2,"HalfAltfire")
		Goto AltFireAnimation
	ReturnToLaser:
		TNT1 A 0 A_JumpIfInventory("NoReloading",1,"ReturnToLaserNoReload")
		TNT1 A 0 A_JumpIfInventory("RLAmmo",3,"FullReturnToLaser")
		TNT1 A 0 A_JumpIfInventory("RLAmmo",2,"HalfReturnToLaser")
	ReturnToLaserAnimation:	
		2LHG IHGFE 1
		2LHG DCB 2
		2LHG A 1 A_PlaySound("BEP",CHAN_ITEM)
		TNT1 A 0 A_Print("Regular Rockets Enabled",1)
		TNT1 A 0 A_TakeInventory("AltMode",1)
		TNT1 A 0 A_PlaySound("LASERON",CHAN_WEAPON)
		2LLG IHHGGFF 1
		2LLG EDCBA 1
		Goto Ready
	FullReturnToLaser:	
		RLHG TSRQP 1
		RLHG ONM 2
		RLHG L 1 A_PlaySound("BEP",CHAN_ITEM)
		TNT1 A 0 A_Print("Regular Rockets Enabled",1)
		TNT1 A 0 A_TakeInventory("AltMode",1)
		TNT1 A 0 A_PlaySound("LASERON",CHAN_WEAPON)
		RLLG TSSRRQQ 1
		RLLG PONML 1
		Goto Ready
	HalfReturnToLaser:
		RLHG "]\[" 1
		RLHG ZY 1
		RLHG XWV 2
		RLHG U 1 A_PlaySound("BEP",CHAN_ITEM)
		TNT1 A 0 A_Print("Regular Rockets Enabled",1)
		TNT1 A 0 A_TakeInventory("AltMode",1)
		TNT1 A 0 A_PlaySound("LASERON",CHAN_WEAPON)
		RLLG "]\\[[" 1
		RLLG Z 2
		RLLG YXWVU 1
		Goto Ready
	ReturnToLaserNoReload:
		TNT1 A 0 A_JumpIfInventory("RocketAmmo",3,"FullReturnToLaser")
		TNT1 A 0 A_JumpIfInventory("RocketAmmo",2,"HalfReturnToLaser")
		Goto ReturnToLaserAnimation			
	}
}

Actor 64ClassicRocketLauncherTactical : Brutal64Weapon
{
Scale 0.82
Weapon.SlotNumber 5
Weapon.SlotPriority 2
Weapon.SelectionOrder 2500
Weapon.AmmoType "RocketAmmo"
Weapon.AmmoUse 1
Weapon.AmmoGive 0
Inventory.PickupMessage "$FO_GOTLAUNCHER"
Tag "$FO_TAG_ROCKETLAUNCHER"
+INVENTORY.RESTRICTABSOLUTELY
+WEAPON.NOAUTOFIRE
+WEAPON.NOALERT
+POWERED_UP
Weapon.SisterWeapon "64RocketLauncherTactical"
	States
	{
	MineThrowFlash:
		MISS DCBA 1
		TNT1 A 18
		MISS ABCD 1
		Stop
	KickingFlash:
		MISS DCBA 1
		MISS A 8
		MISS ABCD 1
		Stop
	SlideKickingStart: //18
		MISS DCBA 1
		MISS A 35
	SlideKickingEnd:
		MISS A 2
		MISS ABCD 1
		Stop
	Spawn:
		LAUN A -1
		Stop
	v1Ready:
	Ready:
		TNT1 A 0 A_JumpIfInventory("ClassicRLToken",1,2)
		TNT1 A 0 A_TakeInventory("SisterWeaponSelector",0)
		MISG A 1 A_WeaponReady
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		Goto Ready
	Select:
		TNT1 A 0 A_PlaySound("ROCKPKUP",CHAN_BODY)
		TNT1 A 0 A_JumpIfInventory("ClassicRLToken",1,2)
		TNT1 A 0 A_TakeInventory("SisterWeaponSelector",0)		
		MISS ABCD 1 Offset(0,32)
		TNT1 A 0 A_Raise
		Wait
	Deselect:
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Takeinventory("WeaponSpecialAction",0)
		TNT1 A 0 A_Takeinventory("TossMine",0)
		TNT1 A 0 A_Takeinventory("PlaceMine",0)
		TNT1 A 0 A_Takeinventory("Kicking",0)
		MISS DCBA 1
		TNT1 A 0 A_Takeinventory("SisterWeaponSelector",0)
		TNT1 A 0 A_Lower
		Wait
	Fire:
		MISG B 2 Bright Offset(0,36)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,22)
		MISG C 2 Bright Offset(0,37) A_GunFlash
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_ZoomFactor(0.97)
		TNT1 A 0 A_JumpIfInventory("AltMode",1,"HeavyFire")	//added "Altmode" alternates rocket types
		TNT1 A 0 A_FireCustomMissile("64rocket")
		TNT1 A 0 A_Recoil(2)
		MISG D 1 Bright A_SetPitch(pitch-2)
		TNT1 A 0 A_ZoomFactor(1.0)
		MISG E 1 Bright A_SetPitch(pitch+0.8)
		MISG F 1 A_SetPitch(pitch+0.6,SPF_INTERPOLATE)
		MISG F 1 A_SetPitch(pitch+0.4,SPF_INTERPOLATE)
		MISG F 1 A_SetPitch(pitch+0.2,SPF_INTERPOLATE)
		MISG FGHI 2
		MISG A 4
		TNT1 A 0 A_ReFire
		Goto Ready
	HeavyFire:
		TNT1 A 0 A_Recoil(3)
		TNT1 A 0 A_FireCustomMissile("TacticalRocket")
		MISG D 1 Bright A_SetPitch(pitch-2,SPF_INTERPOLATE)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,22)
		TNT1 A 0 A_ZoomFactor(1.0)
		MISG E 1 Bright A_SetPitch(pitch+0.8)
		MISG F 1 A_SetPitch(pitch+0.6,SPF_INTERPOLATE)
		MISG F 1 A_SetPitch(pitch+0.4,SPF_INTERPOLATE)
		MISG F 1 A_SetPitch(pitch+0.2,SPF_INTERPOLATE)
		MISG FGHI 2
		MISG A 4
		MISR ABCCCCC 2
		TNT1 A 0 A_PlaySound("ROCKLOAD",CHAN_WEAPON)
		MISR DEFGFED 1
		MISR CCCCBA 2
		TNT1 A 0 A_ReFire
		Goto Ready
	Flash:
		TNT1 A 3 Bright A_Light1
		TNT1 A 2 Bright
		TNT1 A 2 Bright A_Light2
		TNT1 A 6 Bright
		Goto LightDone
	Altfire:
		MISR ABCCCCC 2
		TNT1 A 0 A_JumpIfInventory("AltMode",1,"ReturnToLaser")
		TNT1 A 0 A_PlaySound("ROCKLOAD",CHAN_WEAPON)
		TNT1 A 0 A_Print("Heavy Payload Rockets Enabled",1)
		TNT1 A 0 A_GiveInventory("AltMode",1)
		MISR DEFGFED 1
		MISR CCCCBA 2
		Goto Ready
	ReturnToLaser:
		TNT1 A 0 A_PlaySound("ROCKLOAD",CHAN_WEAPON)
		TNT1 A 0 A_Print("Regular Rockets Enabled",1)
		TNT1 A 0 A_TakeInventory("AltMode",1)
		MISR DEFGFED 1
		MISR CCCCBA 2
		Goto Ready
	}
}

Actor 64Rocket : Rocket Replaces Rocket
{
scale 0.75
Speed 30
Decal "BigScorch"
Obituary "$FO_OB_MPROCKET"
-DEHEXPLOSION
-ROCKETTRAIL
+EXTREMEDEATH
+NODAMAGETHRUST
	States
	{
	Spawn:
		TNT1 A 3
	Fly:
		MISL A 1 Bright Light("ROCKET")
		TNT1 A 0 A_PlaySound("DSRLAUN",CHAN_BODY)
		TNT1 A 0 A_SpawnItemEx("RocketFlare",-20)
		TNT1 A 0 A_SpawnItemEx("OldschoolRocketSmokeTrail2")
		TNT1 A 0 A_CustomMissile("OldschoolRocketSmokeTrail2",2,0,random(160,210),2,random(-30,30))
		Loop
	Death:
		TNT1 A 0 A_SpawnItemEx("ExplosionFlareSpawner")
		TNT1 AAAAAAAAA 0 A_CustomMissile("ExplosionParticleHeavy",0,0,random(0,360),2,random(0,180))
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleHeavy",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAAAAAA 0 A_CustomMissile("ExplosionParticleVeryFast",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAAAAAAA 0 A_CustomMissile("64ExplosionFireBase",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAA 0 A_CustomMissile("ExplosionSmokeFast22",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_SpawnItemEx("DetectFloorCrater")
		TNT1 A 0 A_SpawnItemEx("DetectCeilCrater")
		TNT1 A 0 A_SpawnItemEx("ExplosionAirDisplacementEffect")
		TNT1 A 0 A_PlaySound("EXPLO3",CHAN_VOICE)
		TNT1 A 0 A_PlaySound("EXPLOSION",CHAN_WEAPON)
		TNT1 A 0 Radius_Quake(2,8,0,12,0)
		TNT1 A 2
		TNT1 A 0 A_Explode
		TNT1 A 0 A_RadiusThrust(128,-1,RTF_AFFECTSOURCE|RTF_NOIMPACTDAMAGE)
		TNT1 A 0 A_SpawnItemEx("LiquidExplosionEffectSpawner")
		MISL BCDEF 1 Bright
		stop
	}
}

Actor TacticalRocket : 64Rocket
{
Scale 1.2
Gravity 0.2
-NOGRAVITY
	States
	{
	Death:
		TNT1 A 0 A_SpawnItemEx("ExplosionFlareSpawner")
		TNT1 AAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleHeavy",0,0,random(0,360),2,random(0,180))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleHeavy",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleVeryFast",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAAAAAAAAAAAA 0 A_CustomMissile("64ExplosionFireBaseTactical",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionSmokeFast22",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_SpawnItemEx("DetectFloorCrater")
		TNT1 A 0 A_SpawnItemEx("DetectCeilCrater")
		TNT1 A 0 A_SpawnItemEx("ExplosionAirDisplacementEffect")
		TNT1 A 0 A_PlaySound("EXPLO3",CHAN_VOICE)
		TNT1 A 0 A_PlaySound("EXPLOSION",CHAN_WEAPON)
		TNT1 A 0 Radius_Quake(7,13,0,17,0)	//+5ea
		TNT1 A 2
		TNT1 AAA 0 A_Explode
		TNT1 A 0 A_RadiusThrust(128,-1,RTF_AFFECTSOURCE|RTF_NOIMPACTDAMAGE)
		TNT1 A 0 A_SpawnItemEx("LiquidExplosionEffectSpawner")
		MISL BCDEF 1 Bright
		stop
	}
}

Actor CyberRocket : 64Rocket
{
Scale 1.3
	States
	{
	Death:
		TNT1 A 0 A_SpawnItemEx("ExplosionFlareSpawner")
		TNT1 AAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleHeavy",0,0,random(0,360),2,random(0,180))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleHeavy",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleVeryFast",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAAAAAAAAAAAA 0 A_CustomMissile("64ExplosionFireBaseTactical",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionSmokeFast22",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_SpawnItemEx("DetectFloorCrater")
		TNT1 A 0 A_SpawnItemEx("DetectCeilCrater")
		TNT1 A 0 A_SpawnItemEx("ExplosionAirDisplacementEffect")
		TNT1 A 0 A_PlaySound("EXPLO3",CHAN_VOICE)
		TNT1 A 0 A_PlaySound("EXPLOSION",CHAN_WEAPON)
		TNT1 A 0 Radius_Quake(8,15,0,20,0)
		TNT1 A 2
		TNT1 AA 0 A_Explode
		TNT1 A 0 A_RadiusThrust(128,-1,RTF_AFFECTSOURCE|RTF_NOIMPACTDAMAGE)
		TNT1 A 0 A_SpawnItemEx("LiquidExplosionEffectSpawner")
		MISL BCDEF 1 Bright
		stop
	}
}

Actor 64ExplosionFireBaseTactical : 64ExplosionFireBase	//larger explosion for tactical rocket
{
scale 1.35
}