ACTOR 64GoreNest
{
Health 400
Radius 31
Height 56
Scale 0.8
Mass 0x7FFFFFFF
Speed 0
Tag "$FO_TAG_GORENEST"
DamageFactor "CauseWaterSplash",0.0
DamageFactor "CauseObjectsToSplash",0.0
DropItem "DemonEssenceSpawner"
DropItem "DemonEssenceSpawner"
DropItem "DemonEssenceSpawner"
DropItem "DemonEssenceSpawner"
DropItem "64HellPotion"
DropItem "64HellPotion"
DropItem "64HellPotion"
DropItem "64HellPotion"
SeeSound "64Thing/sight"
DeathSound "nest/death"
ActiveSound "64Thing/sight"
MONSTER
-CANPUSHWALLS
-COUNTKILL
+NOBLOODDECALS
+NOPAIN
+MISSILEMORE
+MISSILEEVENMORE
+FLOORCLIP
+LOOKALLAROUND
+NOICEDEATH
+STANDSTILL
+NOTARGET
+NOINFIGHTING
	States
	{
	Spawn:
		NEST ABCD 4 Light("BIGREDTORCH") A_LookEx
		Loop
	Forget:
		TNT1 A 0 A_ClearTarget
		Goto Spawn
	See:
		NEST A 4 Light("BIGREDTORCH") A_Chase
		NEST BCD 4 Light("BIGREDTORCH")
		TNT1 A 0 A_CheckSight("Forget")
		Loop
	Missile:
		NEST ABCD 4 Light("BIGREDTORCH") A_Facetarget
		TNT1 A 0 A_SpawnItemEx("64GoreNestSpawnerCubeWeak",0,0,64,10,0,0,0,SXF_NOCHECKPOSITION)
		NEST ABCD 4 Light("BIGREDTORCH")
		Goto See
	Death:
		NEST E 8 A_Scream		
		TNT1 A 0 A_SpawnItemEx("64GoreNestSpawnerCubeMedium",0,0,64,8,0,0,0,SXF_NOCHECKPOSITION)
		TNT1 A 0 A_SpawnItemEx("64GoreNestSpawnerCubeWeak",0,0,64,8,0,0,-120,SXF_NOCHECKPOSITION)
		TNT1 A 0 A_SpawnItemEx("64GoreNestSpawnerCubeWeak",0,0,64,8,0,0,120,SXF_NOCHECKPOSITION)
		NEST F 6 Light("SMALLREDTORCH") A_NoBlocking
		TNT1 AAA 0 A_CustomMissile("ExplosionParticleHeavy",64,0,random(0,360),2,random(0,180))
		TNT1 AAAAAA 0 A_CustomMissile("ExplosionParticleHeavy",64,0,random(0,360),2,random(0,360))
		TNT1 AAAAAA 0 A_CustomMissile("CeilBloodLauncher",0,0,random(0,360),2,random(50,130))
		TNT1 AA 0 A_CustomMissile("XDeath1",32,0,random(0,360),2,random(0,90))
		TNT1 AA 0 A_CustomMissile("XDeath2",32,0,random(0,360),2,random(0,90))
		TNT1 AA 0 A_CustomMissile("XDeath3",32,0,random(0,360),2,random(0,90))
		TNT1 A 0 A_CustomMissile("XDeath5",32,0,random(0,360),2,random(0,90))
		TNT1 AAAAAAAA 0 A_CustomMissile("SmallBrainPiece",32,0,random(0,360),2,random(0,90))
		TNT1 A 0 A_CustomMissile("XDeathOrgan1",40,0,random(0,360),2,random(-5,5))
		TNT1 A 0 A_CustomMissile("XDeathOrgan2",40,0,random(0,360),2,random(-5,5))
		TNT1 AAAA 0 A_CustomMissile("Intestine",32,0,random(0,360),2,random(0,90))
		TNT1 AAAA 0 A_CustomMissile("Bloodmist",35,0,random(0,360),2,random(0,90))
		TNT1 AAAAAA 0 A_CustomMissile("BloodmistExtraBig",45,0,random(0,360),2,random(10,90))
		NEST G 6 Light("SMALLREDTORCH")
		NEST HIJK 6
		NEST L -1 A_SpawnItemEx("GrowingBloodPool")
		Stop
	}
}

ACTOR 64GoreNestSpawnerCubeWeak
{
Radius 32
Height 32
Scale 0.3
Speed 8
Damage 5
BounceType "Doom"
PROJECTILE
+THRUACTORS
+RANDOMIZE
+BOUNCEONWALLS
+BOUNCEONFLOORS
+BOUNCEONCEILINGS
	States
	{
	Spawn:
		6BSF A 3
		TNT1 A 0 A_PlaySound("nest/cube",CHAN_BODY)
		6BSF BCD 3
	Death:
		6BSF ABCD 3
		TNT1 A 0 A_SpawnItemEx("64ArchvileFire")
		TNT1 A 0 A_Jump(128,"Demon","Spectre")
	Imp:
		TNT1 A 1 A_SpawnItemEx("DoomImp",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		Stop
	Demon:
		TNT1 A 1 A_SpawnItemEx("Demon",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		Stop
	Spectre:
		TNT1 A 1 A_SpawnItemEx("Spectre",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		Stop
	}
}

ACTOR 64GoreNestSpawnerCubeMedium : 64GoreNestSpawnerCubeWeak
{
	States
	{
	Spawn:
		6BSF A 3
		TNT1 A 0 A_PlaySound("nest/cube",CHAN_BODY)
		6BSF BCD 3
	Death:
		6BSF ABCD 3
		TNT1 A 0 A_SpawnItemEx("64ArchvileFire")
		TNT1 A 0 A_Jump(128,"Hellknight","Revenant")
	Cacodemon:
		TNT1 A 1 A_SpawnItemEx("Cacodemon",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		Stop
	Hellknight:
		TNT1 A 1 A_SpawnItemEx("Hellknight",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		Stop
	Revenant:
		TNT1 A 1 A_SpawnItemEx("Revenant",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		Stop
	}
}