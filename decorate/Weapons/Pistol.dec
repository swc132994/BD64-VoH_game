ACTOR PistolSpawner : CustomInventory Replaces Pistol
{
//$category "Game/BD64_Weapons
Scale 0.82
Tag "$FO_TAG_PISTOL"
Inventory.PickupMessage "$FO_PICKUP_PISTOL_DROPPED"
Inventory.PickupSound "WPICK"
	States
	{
	Spawn:
		PIST A -1
		Stop
	Pickup:
		TNT1 A 0 A_JumpIfInventory("IsTacticalClass",1,"Pickup2")
		TNT1 A 0 A_JumpIfInventory("IsModern",1,"Pickup2")
		TNT1 A 0 A_GiveInventory("64Pistol",1)
		Stop
	Pickup2:
		TNT1 A 0 A_GiveInventory("64PistolTactical",1)
		Stop
	}
}

Actor 64Pistol : DoomWeapon
{
Scale 0.82
Weapon.SlotNumber 2
Weapon.SlotPriority 1
Weapon.SelectionOrder 1900
Weapon.AmmoType "Clip"
Weapon.AmmoUse 1
Weapon.AmmoGive 10
+WEAPON.WIMPY_WEAPON
+WEAPON.NOAUTOFIRE
Obituary "$FO_OB_MPPISTOL"
Inventory.Pickupmessage "$FO_PICKUP_PISTOL_DROPPED"
Tag "$FO_TAG_PISTOL"
VisibleToPlayerClass "64DoomPlayer"
Inventory.ForbiddenTo "Tactical64DoomPlayer","Tactical64DoomPlayer_P","Modern"
+INVENTORY.RESTRICTABSOLUTELY
	states
	{
	Ready:
		PISG A 1 A_WeaponReady
		Wait
	Select:
		PISG A 1 A_Raise
		wait
	Deselect:
		TNT1 A 0 A_ZoomFactor(1.0)
		PISG A 1 A_Lower
		Wait
	Fire:
		TNT1 A 0 A_FireCustomMissile("GunFireSmoke",0,0)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,22)
		TNT1 A 0 A_FireCustomMissile("Tracer",random(-6,6),0,-1,0,0,random(-5,5))
		TNT1 A 0 A_FireBullets(4.0,4.0,1,10,"HitPuff")
		TNT1 A 0 A_PlaySound("PISTOLF",CHAN_WEAPON)
		TNT1 A 0 A_ZoomFactor(0.95)
		PISF A 1 Bright
		TNT1 A 0 A_ZoomFactor(1.0)
		PISF B 2 Bright
		TNT1 A 0 A_Firecustommissile("BulletCaseSpawn",0,0,0,-10)
		PISF CDEF 1
		PISG A 6 A_WeaponReady(1)
		TNT1 A 0 A_Refire
		Goto Ready
	Spawn:
		PIST A -1
		Stop
	}
}

ACTOR PistolAmmo : Ammo
{
Inventory.icon "PISTA0"
Ammo.BackPackAmount 0
Ammo.BackpackMaxAmount 11
Inventory.Amount 0
Inventory.MaxAmount 11
+IGNORESKILL
}

Actor 64PistolTACTICAL : Brutal64Weapon
{
Scale 0.82
Weapon.SlotNumber 2
Weapon.SlotPriority 1
Weapon.SelectionOrder 1900
Weapon.AmmoType1 "Clip"
Weapon.AmmoUse1 1
Weapon.AmmoGive1 10
Weapon.AmmoType2 "PistolAmmo"
Weapon.AmmoUse2 1
Weapon.AmmoGive2 0
Obituary "$FO_OB_MPPISTOL"
Inventory.Pickupmessage "$FO_PICKUP_PISTOL_DROPPED"
Tag "$FO_TAG_PISTOL"
+INVENTORY.RESTRICTABSOLUTELY
+WEAPON.NOALERT
+WEAPON.NOAUTOFIRE
	states
	{
	Spawn:
		PIST A -1
		Stop
	LetsGetStarted:
		TNT1 A 0 A_TakeInventory("StartWeapon")
		//PUN1 A 10 A_WeaponReady(WRF_ALLOWZOOM)
		//PUNS ABC 1 A_WeaponReady(WRF_ALLOWZOOM)
		TNT1 A 9 A_WeaponReady(WRF_ALLOWZOOM)
		PUN6 ABCD 1	A_WeaponReady(WRF_ALLOWZOOM)
		PUNS DE 2 A_WeaponReady(WRF_ALLOWZOOM)
		PUNS FG 1 A_WeaponReady(WRF_ALLOWZOOM)
		TNT1 A 0 A_PlaySound("Knuckled",CHAN_VOICE)
		PUNS J 4 A_WeaponReady(WRF_ALLOWZOOM)
		PUNS IHG 1 A_WeaponReady(WRF_ALLOWZOOM)
		PUNS KL 2 A_WeaponReady(WRF_ALLOWZOOM)
		PUNS MN 1 A_WeaponReady(WRF_ALLOWZOOM)
		TNT1 A 0 A_PlaySound("Knuckled",CHAN_VOICE)
		PUNS Q 4 A_WeaponReady(WRF_ALLOWZOOM)
		PUNS PON 1 A_WeaponReady(WRF_ALLOWZOOM)
		PUN6 DCBA 1	A_WeaponReady(WRF_ALLOWZOOM)
	WarmUp:
		TNT1 A 3 A_TakeInventory("NeverSelectedPistol")
		PISS ABCD 1 A_WeaponReady(WRF_ALLOWZOOM)
		PISR ABCDEFGGHIJKKLLLMNO 1 A_WeaponReady(WRF_ALLOWZOOM)
		TNT1 A 0 A_PlaySound("pistol/magin",CHAN_5)
		PISR PQRSTTT 1 A_WeaponReady(WRF_ALLOWZOOM)
		PISI OPQRSS 1 A_WeaponReady
		PISI E 2 A_WeaponReady
		TNT1 A 0 A_PlaySound("pistol/backslide",CHAN_WEAPON)
		PISI FG 1 A_WeaponReady(WRF_ALLOWZOOM)
		PISI H 4 A_WeaponReady(WRF_ALLOWZOOM)
		TNT1 A 0 A_PlaySound("pistol/forwardslide",CHAN_WEAPON)
		PISI IJKLLLMMN 1 A_WeaponReady
		PISR EDCBA 1 A_WeaponReady
		Goto Ready
	Select:
		TNT1 A 0 A_JumpIfInventory("StartWeapon",1,"LetsGetStarted")
		TNT1 A 0 A_JumpIfInventory("NeverSelectedPistol",1,"WarmUp")
		TNT1 A 0 A_PlaySound("PISSEL",CHAN_WEAPON)
		TNT1 A 0 A_JumpIfInventory("PistolAmmo",1,1)
		Goto SelectEmpty
		P1SS A 0 A_JumpIfInventory("IsSilencer",1,2)
		PISS A 0
		"####" ABCD 1 A_WeaponReady(WRF_NOFIRE)
		TNT1 A 0 A_Raise
		Wait
	SelectEmpty:
		P1SS I 0 A_JumpIfInventory("IsSilencer",1,2)
		PISS I 0
		"####" IJKL 1 A_WeaponReady(WRF_NOFIRE)
		TNT1 A 0 A_Raise
		Wait
	Deselect:
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Takeinventory("WeaponSpecialAction",0)
		TNT1 A 0 A_Takeinventory("TossMine",0)
		TNT1 A 0 A_Takeinventory("PlaceMine",0)
		TNT1 A 0 A_Takeinventory("Kicking",0)
		TNT1 A 0 A_JumpIfInventory("PistolAmmo",1,1)
		Goto DeselectEmpty
		P1SS E 0 A_JumpIfInventory("IsSilencer",1,2)
		PISS E 0
		"####" EFGH 1
		TNT1 A 0 A_Lower
		Wait
	DeselectEmpty:
		P1SS M 0 A_JumpIfInventory("IsSilencer",1,2)
		PISS M 0
		"####" MNOP 1
		TNT1 A 0 A_Lower
		Wait
	NoAmmo:
		P1SG B 0 A_JumpIfInventory("IsSilencer",1,2)
		PISG B 0
		"####" B 10 A_PlaySound("DRYFIRE",CHAN_WEAPON)
		Goto Ready
	Fidget:
		P1SR A 0 A_JumpIfInventory("IsSilencer",1,2)
		PISR A 0
		"####" AB 1 A_WeaponReady(WRF_ALLOWZOOM)
		P1SI A 0 A_JumpIfInventory("IsSilencer",1,2)
		PISI A 0
		"####" ABCDEEEEE 1 A_WeaponReady(WRF_ALLOWZOOM)
		"####" A 0 A_PlaySound("pistol/backslide",CHAN_WEAPON)
		"####" FG 1 A_WeaponReady(WRF_ALLOWZOOM)
		"####" A 0 A_GiveInventory("HoldFidget",1)
	HoldingFidget:
		"####" H 5 A_WeaponReady(WRF_ALLOWRELOAD|WRF_NOFIRE|WRF_ALLOWZOOM)
		"####" A 0 A_TakeInventory("HoldFidget",1)
		"####" A 0 A_PlaySound("pistol/forwardslide",CHAN_WEAPON)
		"####" IJKLLLMMN 1 A_WeaponReady(WRF_NOFIRE|WRF_ALLOWZOOM)
		P1SR A 0 A_JumpIfInventory("IsSilencer",1,2)
		PISR A 0
		"####" EDCBA 1 A_WeaponReady
		Goto Ready
	MineThrowFlash:
		P1SS A 0 A_JumpIfInventory("IsSilencer",1,2)
		PISS A 0
		"####" EFGH 1
		TNT1 A 18
		P1SS D 0 A_JumpIfInventory("IsSilencer",1,2)
		PISS D 0
		"####" ABCD 1
		Stop
	KickingFlash:
		P1SR A 0 A_JumpIfInventory("IsSilencer",1,2)
		PISR A 0
		"####" ABCD 1
		"####" Y 8
		"####" DCBA 1
		Stop
	SlideKickingStart:
		P1SR A 0 A_JumpIfInventory("IsSilencer",1,2)
		PISR A 0
		"####" ABCD 1
		"####" Y 35
	SlideKickingEnd:
		"####" Y 2
		"####" DCBA 1
		Stop
	v1Ready:
	Ready:
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		TNT1 A 0 A_JumpIfInventory("PistolAmmo",1,1)
		Goto ReadyEmpty
		P1SG A 0 A_JumpIfInventory("IsSilencer",1,2)
		PISG A 0
		"####" A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		Loop
	ReadyEmpty:
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		P1SG B 0 A_JumpIfInventory("IsSilencer",1,2)
		PISG B 0
		"####" B 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		Loop
	Fire:
		TNT1 A 0 A_JumpIfInventory("NoReloading",1,"NoReload")
		TNT1 A 0 A_JumpIfInventory("PistolAmmo",1,1)
		Goto Reload
		TNT1 A 0 A_TakeInventory("PistolAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_JumpIfInventory("PistolAmmo",4,"ResumeFire")
		TNT1 A 0 ACS_NamedExecute("LowAmmoSFX")
		Goto ResumeFire
	NoReload:
		TNT1 A 0 A_JumpIfInventory("Clip",1,1)
		Goto NoAmmo
		TNT1 A 0 A_TakeInventory("Clip",1,TIF_NOTAKEINFINITE)
	ResumeFire:
		TNT1 A 0 A_FireCustomMissile("GunFireSmoke",0,0)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,22)
		TNT1 A 0 A_FireCustomMissile("Tracer",random(-6,6),0,-1,0,0,random(-5,5))
		TNT1 A 0 A_JumpIfInventory("IsSilencer",1,"SilenceFire")
		TNT1 A 0 A_FireBullets(4.0,4.0,1,10,"HitPuff",0)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("PISTOLF",CHAN_WEAPON)
		TNT1 A 0 A_ZoomFactor(0.95)
		PISF A 1 Bright
		TNT1 A 0 A_SetPitch(pitch-1.5,SPF_INTERPOLATE)
		TNT1 A 0 A_ZoomFactor(1.0)
		PISF B 2 Bright
		TNT1 A 0 A_Firecustommissile("BulletCaseSpawn",0,0,0,-10)
		PISF CD 1 A_SetPitch(pitch+0.5,SPF_INTERPOLATE)
		PISF E 1 A_SetPitch(pitch+0.5,SPF_INTERPOLATE)
		PISF F 1
		PISG A 6 A_WeaponReady(1|WRF_ALLOWRELOAD)
		TNT1 A 0 A_Refire
		Goto Ready
	SilenceFire:
		TNT1 A 0 A_FireBullets(4.0,4.0,1,10,"HitPuffSilence",0)
		TNT1 A 0 A_PlaySound("PISTOLF3",CHAN_WEAPON)
		TNT1 A 0 A_ZoomFactor(0.95)
		P1SF A 1 Bright
		TNT1 A 0 A_SetPitch(pitch-1.5,SPF_INTERPOLATE)
		TNT1 A 0 A_ZoomFactor(1.0)
		P1SF B 2 Bright
		TNT1 A 0 A_Firecustommissile("BulletCaseSpawn",0,0,0,-10)
		P1SF CD 1 A_SetPitch(pitch+0.5,SPF_INTERPOLATE)
		P1SF E 1 A_SetPitch(pitch+0.5,SPF_INTERPOLATE)
		P1SF F 1
		P1SG A 6 A_WeaponReady(1|WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		TNT1 A 0 A_Refire
		Goto Ready
	AltFire:
		TNT1 A 0 A_JumpIfInventory("PistolSilencerPickup",1,2)
		TNT1 A 0 A_Print("You need a pistol silencer! This can be found in backpacks")
		Goto Ready
		TNT1 A 0 A_JumpIfInventory("IsSilencer",1,"StopAltFire")
		PISR ABCDE 1
		PISA ABCDEFG 1
		TNT1 A 0 A_PlaySound("SILEIN",CHAN_BODY)
		PISA HI 1
		TNT1 A 0 A_GiveInventory("IsSilencer",1)
		PISA J 3
		PISA KLMN 1
		P1SR DCBA 1
		Goto Ready
	StopAltFire:
		P1SR ABCDEFGGG 1
		PISA ONMLKJI 1
		TNT1 A 0 A_PlaySound("SILEOUT",CHAN_BODY)
		PISA HG 1
		TNT1 A 0 A_TakeInventory("IsSilencer",1)
		PISA F 3
		PISA EDCBA 1
		PISR EDCBA 1
		Goto Ready
	Reload:
		"####" A 0 A_JumpIfInventory("Clip",1,1)
		Goto NoAmmo
		"####" A 0 A_JumpIfInventory("HoldFidget",1,"HoldingFidget")
		"####" A 0 A_JumpIfInventory("PistolAmmo",0,"Fidget")
		"####" A 0 A_Takeinventory("Zoomed",1)
		"####" A 0 A_ZoomFactor(1.0)
		"####" A 0 A_FireCustomMissile("EmptyPistolClipSpawn",-5,0,-10,-20)
		"####" A 0 A_PlaySound("pistol/magout",CHAN_5)
		"####" A 0 A_JumpIfInventory("PistolAmmo",1,"Reload2")//10+1 effect
		P12R A 0 A_JumpIfInventory("IsSilencer",1,2)
		PI2R A 0
		"####" ABCDEFGGHIJKKLLLMNO 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		"####" A 0 A_PlaySound("pistol/magin",CHAN_5)
	InsertBullets:
		TNT1 A 0 A_JumpIfInventory("PistolAmmo",10,4)
		TNT1 A 0 A_JumpIfInventory("Clip",1,1)
		Goto FinishReload
		TNT1 A 0 A_Giveinventory("PistolAmmo",1)
		TNT1 A 0 A_Takeinventory("Clip",1,TIF_NOTAKEINFINITE)
		Goto InsertBullets
		TNT1 A 0 A_Refire
	FinishReload:
		P12R A 0 A_JumpIfInventory("IsSilencer",1,2)
		PI2R A 0
		"####" PQRSS 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		"####" T 1 A_PlaySound("pistol/forwardslide",CHAN_5)
		"####" UVX 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		P1SR A 0 A_JumpIfInventory("IsSilencer",1,2)
		PISR A 0
		"####" TTUVWXDCBA 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		Goto Ready
	Reload2: //empty
		P1SR A 0 A_JumpIfInventory("IsSilencer",1,2)
		PISR A 0
		"####" ABCDEFGGHIJKKLLLMNO 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		"####" A 0 A_PlaySound("pistol/magin",CHAN_5)
	InsertBullets2:
		TNT1 A 0 A_JumpIfInventory("PistolAmmo",0,4)
		TNT1 A 0 A_JumpIfInventory("Clip",1,1)
		Goto FinishReload2
		TNT1 A 0 A_Giveinventory("PistolAmmo",1)
		TNT1 A 0 A_Takeinventory("Clip",1,TIF_NOTAKEINFINITE)
		Goto InsertBullets2
		TNT1 A 0 A_Refire
	FinishReload2:
		P1SR A 0 A_JumpIfInventory("IsSilencer",1,2)
		PISR A 0
		"####" PQRSTTUVWXDCBA 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		Goto Ready
	}
}
