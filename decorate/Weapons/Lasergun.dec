Actor LasergunSpawner : CustomInventory 9351
{
//$category "Game/BD64_Weapons
SpawnID 180
Scale 0.9
Tag "Laser Rifle"
Inventory.PickupMessage "You got the Laser Rifle"
Inventory.PickupSound "WPICK"
	States
	{
	Spawn:
		LGUN C 0 NoDelay A_JumpIfInventory("AltLaserSkin",1,2)
		LGUN B 0
		"####" "#" 1
		"####" "#" 2
		"####" "#" 0 ACS_NamedExecuteAlways("DM_SpawnACS")
		"####" "#" 2
		"####" "#" 0 A_JumpIfInventory("ISDM",1,"DMSpawn")
		"####" "#" 2
		Loop
	Pickup:
		TNT1 A 0 A_JumpIfInventory("IsTacticalClass",1,"Pickup2")
		TNT1 A 0 A_JumpIfInventory("IsModern",1,"Pickup2")
		TNT1 A 0 A_GiveInventory("Lasergun")
		Stop
	Pickup2:
		TNT1 A 0 A_GiveInventory("LasergunTactical")
		Stop
	DMSpawn:
		TNT1 A 0 A_ChangeFlag("THRUACTORS",TRUE)
		TNT1 A 0 A_SpawnItemEx("TeleportationFog")
		TNT1 A 0 A_SpawnItemEx("Lasergun")
		TNT1 A 0 A_SpawnItemEx("LasergunTACTICAL")
		TNT1 A 1050
		Loop
	}
}

Actor Lasergun : DoomWeapon
{
Scale 0.9
Weapon.SlotNumber 9
Weapon.SelectionOrder 200
Inventory.PickupMessage "You got the Laser Rifle"
AttackSound "weapons/AlphaLZRGker"
Weapon.AmmoType "Cell"
Weapon.AmmoUse 1
Weapon.AmmoGive 40
Inventory.Icon LGUNB0
Tag "Laser Rifle"
VisibleToPlayerClass "64DoomPlayer"
Inventory.ForbiddenTo "Tactical64DoomPlayer","Tactical64DoomPlayer_p","Modern"
+INVENTORY.RESTRICTABSOLUTELY
	States
	{
	Spawn:
		LGUN B -1
		Stop
	Ready:
		TNT1 A 0 A_StopSound(CHAN_WEAPON)
		HDLG A 0 A_JumpIfInventory("AltLaserSkin",1,2)
		LZRG A 0
		"####" A 1 A_WeaponReady
		Loop
	Select:
		TNT1 A 0 A_StopSound(CHAN_WEAPON)
		HDLG A 0 A_JumpIfInventory("AltLaserSkin",1,2)
		LZRG A 0
		"####" A 1 A_Raise
		Wait
	Deselect:
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_StopSound(CHAN_WEAPON)
		HDLG A 0 A_JumpIfInventory("AltLaserSkin",1,2)
		LZRG A 0
		"####" A 1 A_Lower
		Wait
	Fire:
		TNT1 A 0 A_JumpIf(waterlevel > 1,"Ready")
		TNT1 A 0 A_GiveInventory("HasPlasmaWeapon",1)
		TNT1 A 0 A_ZoomFactor(0.99)
		TNT1 A 0 A_PlaySound("LSSTART",CHAN_VOICE)
		HDLF A 0 A_JumpIfInventory("AltLaserSkin",1,2)
		LZRF A 0
		"####" ABC 1 Bright
	Hold:
		"####" A 0 A_JumpIf(waterlevel > 1,"Ready")
		"####" A 0 A_JumpIfInventory("IsCrouching",1,3)
		"####" A 0 A_SpawnItemEx("PlayerMuzzle4",20,5,40)
		"####" A 0 A_Jump(256,2)
		"####" A 0 A_SpawnItemEx("PlayerMuzzle4",20,5,22)
		"####" A 0 A_JumpIfInventory("WeaponPower",3,"Hold4")
		"####" A 0 A_JumpIfInventory("WeaponPower",2,"Hold3")
		"####" A 0 A_JumpIfInventory("WeaponPower",1,"Hold2")
	Hold1:
		"####" A 0 A_PlaySound("LSLoop",CHAN_WEAPON,0.25,TRUE)
		"####" D 1 Bright Offset(0,33) A_FireCustomMissile("AlphaLZRGkerLaser",0,1)
		"####" D 1 Bright Offset(1,34)
		"####" D 1 Bright Offset(-1,35) A_FireCustomMissile("AlphaLZRGkerLaser",0,1)
		Goto HoldFinish
	Hold2:
		"####" A 0 A_PlaySound("LSLoop",CHAN_WEAPON,0.5,TRUE)
		"####" D 1 Bright Offset(0,33) A_FireCustomMissile("AlphaLZRGkerLaser2",0,1)
		"####" D 1 Bright Offset(1,34)
		"####" D 1 Bright Offset(-1,35) A_FireCustomMissile("AlphaLZRGkerLaser2",0,1)
		Goto HoldFinish
	Hold3:
		"####" A 0 A_PlaySound("LSLoop",CHAN_WEAPON,0.75,TRUE)
		"####" D 1 Bright Offset(0,33) A_FireCustomMissile("AlphaLZRGkerLaser3",0,1)
		"####" D 1 Bright Offset(1,34)
		"####" D 1 Bright Offset(-1,35) A_FireCustomMissile("AlphaLZRGkerLaser3",0,1)
		Goto HoldFinish
	Hold4:
		"####" A 0 A_PlaySound("LSLoop",CHAN_WEAPON,1,TRUE)
		"####" D 1 Bright Offset(0,33) A_FireCustomMissile("AlphaLZRGkerLaser4",0,1)
		"####" D 1 Bright Offset(1,34)
		"####" D 1 Bright Offset(-1,35) A_FireCustomMissile("AlphaLZRGkerLaser4",0,1)
	HoldFinish:
		"####" A 0 A_Refire
		"####" A 0 A_StopSound(CHAN_WEAPON)
		"####" A 0 A_PlaySound("LSStop",CHAN_VOICE)
		"####" A 0 A_ZoomFactor(1.0)
		"####" EFGHIJK 1 Offset(0,32) A_FireCustomMissile("GunFireSmoke",0,0)
		HDLG A 0 A_JumpIfInventory("AltLaserSkin",1,2)
		LZRG A 0
		"####" A 2
		Goto Ready
	}
}

Actor LasergunTactical : Brutal64Weapon
{
Scale 0.9
Weapon.SlotNumber 9
Weapon.SelectionOrder 200
Inventory.PickupMessage "You got the Laser Rifle"
AttackSound "weapons/AlphaLZRGker"
Weapon.AmmoType "Cell"
Weapon.AmmoUse 1
Weapon.AmmoGive 40
Inventory.Icon LGUNB0
Tag "Laser Rifle"
+INVENTORY.RESTRICTABSOLUTELY
+WEAPON.NOALERT
+WEAPON.ALT_USES_BOTH
-WEAPON.AMMO_CHECKBOTH
	States
	{
	MineThrowFlash:
		HDLS A 0 A_JumpIfInventory("AltLaserSkin",1,2)
		LZRS A 0
		"####" EFGH 1
		TNT1 A 18
		HDLS A 0 A_JumpIfInventory("AltLaserSkin",1,2)
		LZRS A 0
		"####" ABCD 1
		Stop
	KickingFlash:
		HDLG A 0 A_JumpIfInventory("AltLaserSkin",1,2)
		LZRG A 0
		"####" BCDE 1
		"####" F 8
		"####" EDCB 1
		Stop
	SlideKickingStart:
		HDLG A 0 A_JumpIfInventory("AltLaserSkin",1,2)
		LZRG A 0
		"####" BCDE 1
		"####" F 35
	SlideKickingEnd:
		"####" F 2
		"####" EDCB 1
		Stop
	Spawn:
		LGUN B -1
		Stop
	v1Ready:
	Ready:
		HDLG A 0 A_JumpIfInventory("AltLaserSkin",1,2)
		LZRG A 0
		"####" A 1 A_WeaponReady(WRF_ALLOWZOOM)
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		Loop
	Select:
		TNT1 A 0 A_StopSound(CHAN_WEAPON)
		TNT1 A 0 A_PlaySound("LSGREADY",CHAN_BODY)
		HDLS A 0 A_JumpIfInventory("AltLaserSkin",1,2)
		LZRS A 0
		"####" ABCD 1 Offset(0,32)
		TNT1 A 0 A_Raise
		Wait
	Deselect:
		TNT1 A 0 A_StopSound(CHAN_WEAPON)
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Takeinventory("WeaponSpecialAction",0)
		TNT1 A 0 A_Takeinventory("TossMine",0)
		TNT1 A 0 A_Takeinventory("PlaceMine",0)
		TNT1 A 0 A_Takeinventory("Kicking",0)
		TNT1 A 0 A_TakeInventory("HasPlasmaWeapon",0)
		HDLS A 0 A_JumpIfInventory("AltLaserSkin",1,2)
		LZRS A 0
		"####" EFGH 1
		TNT1 A 0 A_Lower
		Wait
	Fire:
		TNT1 A 0 A_JumpIf(waterlevel > 1,"Ready")
		TNT1 A 0 A_GiveInventory("HasPlasmaWeapon",1)
		TNT1 A 0 A_ZoomFactor(0.99)
		TNT1 A 0 A_PlaySound("LSSTART",CHAN_VOICE)
		HDLF A 0 A_JumpIfInventory("AltLaserSkin",1,2)
		LZRF A 0
		"####" ABC 1 Bright
	Hold:
		"####" A 0 A_JumpIf(waterlevel > 1,"Ready")
		"####" A 0 A_JumpIfInventory("IsCrouching",1,3)
		"####" A 0 A_SpawnItemEx("PlayerMuzzle4",20,5,40)
		"####" A 0 A_Jump(256,2)
		"####" A 0 A_SpawnItemEx("PlayerMuzzle4",20,5,22)
		"####" A 0 A_AlertMonsters
		"####" A 0 A_JumpIfInventory("WeaponPower",3,"Hold4")
		"####" A 0 A_JumpIfInventory("WeaponPower",2,"Hold3")
		"####" A 0 A_JumpIfInventory("WeaponPower",1,"Hold2")
	Hold1:
		"####" A 0 A_PlaySound("LSLoop",CHAN_WEAPON,0.25,TRUE)
		"####" D 1 Bright Offset(0,33) A_FireCustomMissile("AlphaLZRGkerLaser",0,1)
		"####" D 1 Bright Offset(1,34)
		"####" D 1 Bright Offset(-1,35) A_FireCustomMissile("AlphaLZRGkerLaser",0,1)
		Goto HoldFinish
	Hold2:
		"####" A 0 A_PlaySound("LSLoop",CHAN_WEAPON,0.5,TRUE)
		"####" D 1 Bright Offset(0,33) A_FireCustomMissile("AlphaLZRGkerLaser2",0,1)
		"####" D 1 Bright Offset(1,34)
		"####" D 1 Bright Offset(-1,35) A_FireCustomMissile("AlphaLZRGkerLaser2",0,1)
		Goto HoldFinish
	Hold3:
		"####" A 0 A_PlaySound("LSLoop",CHAN_WEAPON,0.75,TRUE)
		"####" D 1 Bright Offset(0,33) A_FireCustomMissile("AlphaLZRGkerLaser3",0,1)
		"####" D 1 Bright Offset(1,34)
		"####" D 1 Bright Offset(-1,35) A_FireCustomMissile("AlphaLZRGkerLaser3",0,1)
		Goto HoldFinish
	Hold4:
		"####" A 0 A_PlaySound("LSLoop",CHAN_WEAPON,1,TRUE)
		"####" D 1 Bright Offset(0,33) A_FireCustomMissile("AlphaLZRGkerLaser4",0,1)
		"####" D 1 Bright Offset(1,34)
		"####" D 1 Bright Offset(-1,35) A_FireCustomMissile("AlphaLZRGkerLaser4",0,1)
	HoldFinish:
		"####" A 0 A_Refire
		"####" A 0 A_StopSound(CHAN_WEAPON)
		"####" A 0 A_PlaySound("LSStop",CHAN_VOICE)
		"####" A 0 A_ZoomFactor(1.0)
		"####" EFGHIJK 1 Offset(0,32) A_FireCustomMissile("GunFireSmoke",0,0)
		HDLG A 0 A_JumpIfInventory("AltLaserSkin",1,2)
		LZRG A 0
		"####" A 2
		Goto Ready
	AltFire:
		TNT1 A 0 A_JumpIf(waterlevel > 1,"Ready")
		TNT1 A 0 A_JumpIfInventory("CELL",10,2)
		TNT1 A 0 A_Print("Not Enough Ammo!",1)
		Goto Ready
		TNT1 A 0 A_JumpIfInventory("WeaponPower",3,2)
		TNT1 A 0 A_Print("Not Enough Demonic Artefacts!",1)
		Goto Ready
		TNT1 A 0 A_GiveInventory("HasPlasmaWeapon",1)
		TNT1 A 0 A_PlaySound("RCHARGE",CHAN_ITEM)
		HDLG A 0 A_JumpIfInventory("AltLaserSkin",1,2)
		LZRG A 0
		"####" A 5
		HDLF A 0 A_JumpIfInventory("AltLaserSkin",1,2)
		LZRF A 0
		"####" LMNO 2
		"####" P 1 Bright Offset(0,33)
		"####" Q 1 Bright Offset(1,34)
		"####" P 1 Bright Offset(0,35)
		"####" Q 1 Bright Offset(-1,34)
		"####" P 1 Bright Offset(0,33)
		"####" Q 1 Bright Offset(1,34)
		"####" P 1 Bright Offset(0,35)
		"####" Q 1 Bright Offset(-1,34)
		"####" P 1 Bright Offset(0,33)
		"####" Q 1 Bright Offset(1,34)
		"####" P 1 Bright Offset(0,35)
		"####" Q 1 Bright Offset(-1,34)
		"####" P 1 Bright Offset(0,33)
		"####" Q 1 Bright Offset(1,34)
		"####" P 1 Bright Offset(0,35)
		"####" Q 1 Bright Offset(-1,34)
		"####" P 1 Bright Offset(0,33)
		"####" Q 1 Bright Offset(1,34)
		"####" P 1 Bright Offset(0,35)
		"####" Q 1 Bright Offset(-1,34)
	AltHold:
		"####" A 0 A_JumpIf(waterlevel > 1,"Ready")
		"####" A 0 A_JumpIfInventory("CELL",10,2)
		"####" A 0 A_Print("Not Enough Ammo!",1)
		Goto Ready
		"####" A 0 A_JumpIfInventory("IsCrouching",1,3)
		"####" A 0 A_SpawnItemEx("PlayerMuzzle4",20,5,40)
		"####" A 0 A_Jump(256,2)
		"####" A 0 A_SpawnItemEx("PlayerMuzzle4",20,5,22)
		"####" A 0 A_TakeInventory("Cell",10,TIF_NOTAKEINFINITE)
		"####" A 0 A_FireCustomMissile("EnergyBlast",0,FALSE,0,-3)
		"####" A 0 A_AlertMonsters
		"####" A 0 A_FireCustomMissile("LaserTrail",0,FALSE)
		"####" A 0 A_SetPitch(pitch+random(-1,1),SPF_INTERPOLATE)
		"####" A 0 A_SetAngle(angle+random(-1,1),SPF_INTERPOLATE)
		"####" A 0 Offset(0,32) A_StopSound(CHAN_ITEM)
		"####" R 2 Bright A_FireCustomMissile("GunFireSmoke",0,FALSE)
		"####" S 2 Bright A_PlaySound("RBLAST",CHAN_WEAPON)
		"####" T 1 Bright
		"####" A 0 A_ZoomFactor(1.0)
		"####" UVWJK 1
		"####" F 0 A_ReFire
		HDLG A 0 A_JumpIfInventory("AltLaserSkin",1,2)
		LZRG A 0
		"####" AA 2 A_FireCustomMissile("GunFireSmoke",0,FALSE)
		Goto Ready
	}
}

Actor AlphaLZRGkerLaser : FastProjectile
{
Scale 0.4
radius 1
height 1
speed 150
damage 2
Alpha 1
Renderstyle Add
Decal "LaserDecal"
DamageType "Plasma"
SeeSound "weapons/AlphaLZRGker"
DeathSound ""
Obituary "%o was fried by %k's laser."
+DONTSPLASH
+PAINLESS
	States
	{
	Spawn:
		TNT1 A 0 A_ChangeVelocity(velx*2,vely*2,velz*2,CVF_REPLACE)
		TNT1 A 1 A_JumpIf(waterlevel > 1,"Death")
		Loop
	Death:
		TNT1 A 0 A_Stop
		TNT1 AAAAAAAA 0 A_CustomMissile("ExplosionParticleLaser",0,0,random(0,360),2,random(0,180))
		TNT1 AAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleHeavy",0,0,random(0,360),2,random(0,360))
		TNT1 AA 0 A_SpawnItemEx("RocketFlare")
		TNT1 A 0 A_SpawnItemEx("DetectFloorLaser")
		TNT1 A 0 A_SpawnItemEx("DetectCeilLaser")
		LPUF A 4 Bright Light("UNMAKERSHOT")
		LPUF BC 4 Bright
		LPUF C 1 Bright A_FadeOut(0.05)
		Wait
	}
}

Actor AlphaLZRGkerLaser2 : AlphaLZRGkerLaser
{Scale 0.6 Damage 3}

Actor AlphaLZRGkerLaser3 : AlphaLZRGkerLaser
{Scale 0.8 Damage 4}

Actor AlphaLZRGkerLaser4 : AlphaLZRGkerLaser
{Scale 1.0 Damage 5}

Actor EnergyBlast : PlasmaBall
{
Scale 0.8
Speed 85
Damage 25
Height 7
Radius 7
Alpha 0.9
RenderStyle Add
Decal "MedScorch"
DamageType "Plasma"
ReactionTime 60
+DONTSPLASH
+BLOODSPLATTER
+FORCERADIUSDMG
+NOGRAVITY
+BOUNCEONWALLS
+EXPLODEONWATER
+PAINLESS
Obituary "%o was barbequed by %k's Lasergun."
SeeSound ""
BounceFactor 0.95
ReactionTime 45
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_JumpIf(waterlevel > 1,"Death")
		TNT1 A 0 A_CountDown
		TNT1 A 0 A_SpawnItemEx("RocketFlare")
		TNT1 A 0 A_SpawnItemEx("LaserTrail")
		LEYS R 1 Bright Light("BIGREDLASER") A_SpawnItemEx("LaserTrail")
		TNT1 AAAAA 0 A_CustomMissile("ExplosionParticleLaser",0,0,random(0,360))
		TNT1 A 0 A_SpawnItemEx("Shoque")
		Loop
	Death:
		TNT1 A 0 A_Stop
		TNT1 A 0 A_PlaySound("DSFIRXPL",CHAN_ITEM)
		L2NR AB 3 Bright Light("BIGREDLASER") A_SpawnItemEx("RocketFlare")
		LENR AB 1 Bright Light("BIGREDLASER") A_SpawnItemEx("RocketFlare")
		TNT1 AAAAAAAAAA 0 A_CustomMissile("ExplosionParticleHeavy",2,0,random(0,360),2,random(0,180))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleLaser",0,0,random(0,360),2,random(0,360))
		LPUF A 1 Light("REDLASER") A_Explode(100,75,0)
		TNT1 A 0 A_SpawnItemEx("Shoque")
		TNT1 A 0 A_SpawnItemEx("DetectFloorCraterMed")
		TNT1 A 0 A_SpawnItemEx("DetectCeilCraterMed")
		TNT1 A 0 A_SpawnItemEx("LiquidExplosionEffectSpawner")
		TNT1 A 0 A_SetScale(1.2)
		LPUF ABC 7 Bright Light("REDLASER") A_CustomMissile("PlasmaSmoke",1,0,random(0,360),2,random(0,160))
		Stop
	}
}

Actor LaserTrail
{
Scale 2.75
Height 7
Radius 7
Speed 80
Damage 0
Alpha 1
RenderStyle Add
	states
	{
	Spawn:
		LAZR AB 1 Bright A_SpawnItemEx("RocketFlare")
		Stop
	Death:
		LPUF A 1 Bright
		Stop
	}
}