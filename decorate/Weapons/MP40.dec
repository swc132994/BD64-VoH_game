Actor MP40Spawner : CustomInventory 19124
{
//$category "Game/BD64_Weapons
Scale 0.8
Tag " MP40"
Inventory.PickupMessage "You got the MP40!"
Inventory.PickupSound "WPICK"
	States
	{
	Spawn:
		MP4P A 3
		Loop
	Pickup:
		TNT1 A 0 A_JumpIfInventory("IsTacticalClass",1,"Pickup2")
		TNT1 A 0 A_JumpIfInventory("IsModern",1,"Pickup2")
		TNT1 A 0 A_GiveInventory("64MP40")
		Stop
	Pickup2:
		TNT1 A 0 A_GiveInventory("64MP40Tactical")
		Stop
	SpawnClip:
		TNT1 A 1 A_SpawnItemEx("64Clip",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERSPECIAL|SXF_CLEARCALLERTID,0,tid)
		Stop
	}
}

Actor 64MP40 : DoomWeapon
{
Scale 0.45
Weapon.SlotNumber 2
Weapon.SlotPriority 2
Weapon.SelectionOrder 850
Weapon.AmmoType "Clip"
Weapon.AmmoUse 1
Weapon.AmmoGive 20
Inventory.PickupMessage "You got the MP40!"
Obituary "%o was gunned down by %k."
Tag "MP-40"
VisibleToPlayerClass "64DoomPlayer"
Inventory.ForbiddenTo "Tactical64DoomPlayer","Tactical64DoomPlayer_p","Modern"
+INVENTORY.RESTRICTABSOLUTELY
+WEAPON.CHEATNOTWEAPON
	States
	{
	Spawn:
		MP4P A -1
		Stop
	Ready:
		MP4G A 1 A_WeaponReady
		Wait
	Select:
		MP4G A 1 A_Raise
		Wait
	Deselect:
		MP4G A 1 A_Lower
		Wait
	Fire:
		MP4F A 1 Bright
		TNT1 A 0 A_FireCustomMissile("GunFireSmoke",0,0)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,22)
		TNT1 A 0 A_FireCustomMissile("Tracer",random(-3,3),0,-1,-12,0,random(-2,2))
		TNT1 A 0 A_FireBullets(random(0,7),random(0,7),-1,6,"HitPuff")
		TNT1 A 0 A_PlaySound("MP40",CHAN_WEAPON)
		MP4F B 1
		TNT1 A 0 A_Firecustommissile("BulletCaseSpawn",0,0,0,-10)
		MP4F C 1
		TNT1 A 0 A_Refire
		MP4G A 1
		Goto Ready
	}
}

Actor MP40Ammo : Ammo
{
Inventory.Amount 0
Inventory.MaxAmount 32
Ammo.BackpackAmount 0
Ammo.BackpackMaxAmount 32
Inventory.Icon "MP4PA0"
+IGNORESKILL
}

Actor 64MP40Tactical : Brutal64Weapon
{
Scale 0.45
Weapon.SlotNumber 2
Weapon.SlotPriority 2
Weapon.SelectionOrder 850
Weapon.AmmoType1 "Clip"
Weapon.AmmoUse1 1
Weapon.AmmoGive1 20
Weapon.AmmoType2 "MP40Ammo"
Weapon.AmmoUse2 1
Weapon.AmmoGive2 0
Inventory.PickupMessage "You got the MP40!"
Obituary "%o was gunned down by %k."
Tag "MP40"
+INVENTORY.RESTRICTABSOLUTELY
+WEAPON.CHEATNOTWEAPON
	States
	{
	MineThrowFlash:
		MP4S EFGH 1
		TNT1 A 43
		MP4S ABCD 1
		Stop
	KickingFlash:
		MP4G BCDE 1
		MP4G E 8
		MP4G EDCB 1
		Stop
	SlideKickingStart:
		MP4G BCDE 1
		MP4G E 35
	SlideKickingEnd:
		MP4G E 2
		MP4G EDCB 1
		Stop
	/*GrenadeThrowFlash:
		MP4A ABCDEF 1
		TNT1 A 0 A_PlaySound("GRNPIN",CHAN_WEAPON)
		MP4A GHG 2
		MP4A IJKLMNOPQRSTU 1
		TNT1 A 0 A_PlaySound("GRNTOSS",CHAN_WEAPON)
		MP4A WVXYZ 1
		MP4B ABC 1
		Stop*/
	Spawn:
		MP4P A -1
		Stop
	v1Ready:
	Ready:
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		MP4G A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		Loop
	Ready2:
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		MP4G R 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		Loop
	NoAmmo:
		MP4G A 10 A_PlaySound("DRYFIRE",CHAN_WEAPON)
		Goto Ready
	Select:
		TNT1 A 0 A_GiveInventory("MP40Checker",1)
		TNT1 A 0 A_PlaySound("RIFBOLT",CHAN_BODY)
		MP4S ABCD 1 A_WeaponReady(WRF_NOFIRE)
		TNT1 A 0 A_Raise
		Wait
	Fidget:
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"ADSFidget")
		MP4R ABC 1 A_WeaponReady
		MP4G FGH 1 A_WeaponReady
		TNT1 A 0 A_PlaySound("MP40/Charge",CHAN_WEAPON)
		MP4G IJJKLMNNNNO 1 A_WeaponReady
		TNT1 A 0 A_PlaySound("DRYFIRE",CHAN_WEAPON)
		MP4G JIHGF 1 A_WeaponReady
		MP4R CBA 1 A_WeaponReady
		Goto Ready
	ADSFidget:
		MP4G VWX 1 A_WeaponReady
		TNT1 A 0 A_PlaySound("MP40/Charge",CHAN_WEAPON)
		MP4G YZZ 1 A_WeaponReady
		MP4G "[\]" 1
		2P4G AB 1
		2P4G CCCCD 1 A_WeaponReady
		TNT1 A 0 A_PlaySound("DRYFIRE",CHAN_WEAPON)
		MP4G YXWV 1 A_WeaponReady
		Goto Ready2
	Deselect:
		TNT1 A 0 A_Takeinventory("WeaponSpecialAction",0)
		TNT1 A 0 A_Takeinventory("TossMine",0)
		TNT1 A 0 A_Takeinventory("PlaceMine",0)
		TNT1 A 0 A_Takeinventory("Kicking",0)
		TNT1 A 0 A_TakeInventory("MP40Checker",0)
		MP4S EFGH 1
		TNT1 A 0 A_Lower
		Wait
	Fire:
		TNT1 A 0 A_JumpIfInventory("NoReloading",1,"NoReload")
		TNT1 A 0 A_JumpIfInventory("MP40Ammo",1,1)
		Goto Reload
		TNT1 A 0 A_TakeInventory("MP40Ammo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_JumpIfInventory("MP40Ammo",9,"ResumeFire")
		TNT1 A 0 ACS_NamedExecute("LowAmmoSFX")
		Goto ResumeFire
	NoReload:
		TNT1 A 0 A_JumpIfInventory("Clip",1,1)
		Goto NoAmmo
		TNT1 A 0 A_TakeInventory("Clip",1,TIF_NOTAKEINFINITE)
	ResumeFire:
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"FireZoomed")
		TNT1 A 0 A_SetPitch(pitch-1.2,SPF_INTERPOLATE)
		TNT1 A 0 A_FireCustomMissile("GunFireSmoke",0,0)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,22)
		TNT1 A 0 A_FireCustomMissile("Tracer",random(-3,3),0,-1,-12,0,random(-2,2))
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("MP40",CHAN_WEAPON)
		MP4F A 1 Bright A_FireBullets(random(0,7),random(0,7),1,6,"HitPuff",0)
		MP4F BC 1 A_SetPitch(pitch+0.4,SPF_INTERPOLATE)
		TNT1 A 0 A_Firecustommissile("BulletCaseSpawn",0,0,0,-10)
		TNT1 A 0 A_SetAngle(angle + random(-1,1))
		TNT1 A 0 A_Refire
		MP4G A 1
		Goto Ready
	FireZoomed:
		TNT1 A 0 A_FireCustomMissile("Tracer",0,0,0,-6)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,22)
		TNT1 A 0 A_Firecustommissile("BulletCaseSpawn",0,0,0,-10)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("MP40",CHAN_WEAPON)
		MP4G S 1 Bright A_FireBullets(0.0,0.0,1,6,"HitPuff",FBF_NORANDOM)
		MP4G T 1 A_SetPitch(-1.0 + pitch)
		MP4G U 1 A_SetPitch(0.5 + pitch)
		TNT1 A 0 A_SetPitch(0.5 + pitch)
		TNT1 A 0 A_Refire
		MP4G R 1 A_WeaponReady
		Goto Ready2
	AltFire:
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"NoAim")
		TNT1 A 0 A_Giveinventory("Zoomed",1)
		TNT1 A 0 A_ZoomFactor(2.0)
		MP4G PQR 1
		Goto Ready2
	NoAim:
		MP4G RQP 1
		TNT1 A 0 A_Takeinventory("Zoomed",0)
		TNT1 A 0 A_ZoomFactor(1.0)
		Goto Ready
	/*CheckGrenade:
		TNT1 A 0 A_Takeinventory("TossMine",0)
		TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"ThrowGrenade")
		TNT1 A 0 A_Print("No Rocket Ammo",2)
		Goto Ready
	ThrowGrenade:
		MP4G BCDE 1
		TNT1 A 0 A_GunFlash("GrenadeThrowFlash")
		MP4G E 30
		TNT1 A 0 A_Takeinventory("RocketAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_FireCustomMissile("64StickGrenadePlayer",0,0)
		MP4G E 6
		MP4G EDCB 1
		TNT1 A 0 A_Refire
		Goto Ready*/
	Reload:
		MP4G R 0 A_JumpIfInventory("Zoomed",1,2)
		MP4G A 0
		MP4G "#" 1 A_WeaponReady(WRF_NOFIRE)
		TNT1 A 0 A_JumpIfInventory("MP40Ammo",0,"Fidget")
		TNT1 A 0 A_TakeInventory("Zoomed",0)
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_JumpIfInventory("Clip",1,1)
		Goto NoAmmo
		TNT1 A 0 A_JumpIfInventory("MP40Ammo",1,"ReloadNotEmpty")//30+1 effect
		MP4R ABCDEE 1
		TNT1 A 0 A_PlaySound("DRYFIRE",CHAN_5)
		MP4R FFFGHIJJJK 1
		TNT1 A 0 A_PlaySound("MP40/MagOut",CHAN_5)
		MP4R LMN 1
		TNT1 A 0 A_FireCustomMissile("EmptyClipSpawn",-5,0,8,-22)
		MP4R OPQRSTTTTTTTUVWXXYYY 1
	InsertBullets:
		TNT1 A 0 A_JumpIfInventory("MP40Ammo",0,4)
		TNT1 A 0 A_JumpIfInventory("Clip",1,1)
		Goto FullReload
		TNT1 A 0 A_Giveinventory("MP40Ammo",1)
		TNT1 A 0 A_Takeinventory("Clip",1,TIF_NOTAKEINFINITE)
		Goto InsertBullets
		TNT1 A 0 A_Refire
	FullReload:
		MP4R Z 1
		TNT1 A 0 A_PlaySound("MP40/MagIn",CHAN_5)
		MP4R "[\]" 1
		2P4R ABCCDE 1
		TNT1 A 0 A_PlaySound("MP40/Charge",CHAN_WEAPON)
		2P4R FFGHIJKLMNO 1
		2P4R SSRQP 1
		MP4R BA 1
		Goto Ready
	ReloadNotEmpty:
		MP4R AB 1
		2P4R PQRSS 1 A_JumpIfInventory("Kicking",1,"DoKick")
		SOUN D 0 A_PlaySound("DRYFIRE",CHAN_5)
		2P4R TTTUVWXYYYZ 1
		TNT1 A 0 A_PlaySound("MP40/MagOut",CHAN_5)
		2P4R "[\]" 1
		TNT1 A 0 A_FireCustomMissile("EmptyClipSpawn",-5,0,8,-22)
		3P4R ABCDEEEEEEEFGHIIJJJ 1 A_JumpIfInventory("Kicking",1,"DoKick")
	InsertBullets2:
		TNT1 A 0 A_JumpIfInventory("MP40Ammo",0,4)
		TNT1 A 0 A_JumpIfInventory("Clip",1,1)
		Goto ContinueReloading
		TNT1 A 0 A_Giveinventory("MP40Ammo",1)
		TNT1 A 0 A_Takeinventory("Clip",1,TIF_NOTAKEINFINITE)
		Goto InsertBullets2
		TNT1 A 0 A_Refire
	ContinueReloading:
		TNT1 A 0 A_PlaySound("MP40/MagIn",CHAN_5)
		3P4R KLMNNOOOO 1
		2P4R UTRQP 1
		MP4R BA 1
		Goto Ready
	}
}