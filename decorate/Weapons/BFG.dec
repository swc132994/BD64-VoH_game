Actor BFG9000Spawner : CustomInventory Replaces BFG9000
{
//$category "Game/BD64_Weapons
Scale 0.9
Tag "$FO_TAG_BFG9000"
Inventory.PickupMessage "$GOTBFG9000"
Inventory.PickupSound "WPICK"
	States
	{
	Spawn:
		BFUG A 2
		TNT1 A 0 ACS_NamedExecuteAlways("DM_SpawnACS")
		BFUG A 2
		TNT1 A 0 A_JumpIfInventory("ISDM",1,"DMSpawn")
		BFUG A -1
		Stop
	Pickup:
		TNT1 A 0 ACS_NamedExecuteWithResult("WeaponPickupChecker",11)
		Stop
	DMSpawn:
		TNT1 A 0 A_ChangeFlag("THRUACTORS",TRUE)
		TNT1 A 0 A_SpawnItemEx("TeleportationFog")
		TNT1 A 0 A_SpawnItemEx("64BFG9000")
		TNT1 A 0 A_SpawnItemEx("64BFG9000TACTICAL")
		TNT1 A 1050
		Loop
	}
}

Actor 64BFG9000 : DoomWeapon
{
Scale 0.9
Weapon.SlotNumber 7
Weapon.SelectionOrder 2800
Weapon.AmmoType "Cell"
Weapon.AmmoUse 40
Weapon.AmmoGive 40
Inventory.PickupMessage "$GOTBFG9000"
Tag "$FO_TAG_BFG9000"
VisibleToPlayerClass "64DoomPlayer"
Inventory.ForbiddenTo "Tactical64DoomPlayer","Tactical64DoomPlayer_p","Modern"
+INVENTORY.RESTRICTABSOLUTELY
+WEAPON.NOAUTOFIRE
+WEAPON.BFG
	States
	{
	Ready:
		BFGG A 1 A_WeaponReady
		Loop
	Deselect:
		TNT1 A 0 A_ZoomFactor(1.0)
		BFGG A 1 A_Lower
		Wait
	Select:
		BFGG A 1 A_Raise
		Wait
	Fire:
		TNT1 A 0 A_JumpIf(waterlevel > 1,"Ready")
		BFGG A 1 A_BFGSound
		BFGC ABCDABCDABCDABCDABCDABCD 1 Bright
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle5",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle5",20,5,22)
		TNT1 A 0 A_FireBFG
		TNT1 A 0 A_ZoomFactor(0.9)
		TNT1 A 0
		BFGF AB 1 Bright
		TNT1 A 0 A_ZoomFactor(1.0)
		BFGF C 1 Bright
		BFGF GGGHIJ 1
		BFGG A 20
		BFGG A 1 A_ReFire
		Goto Ready
	Spawn:
		BFUG A -1
		Stop
	}
}

Actor 64BFG9000Tactical : Brutal64Weapon
{
Scale 0.9
Weapon.SlotNumber 7
Weapon.SelectionOrder 2800
Weapon.AmmoType "Cell"
Weapon.AmmoUse 1
Weapon.AmmoGive 40
Inventory.PickupMessage "$GOTBFG9000"
Tag "$FO_TAG_BFG9000"
+WEAPON.ALT_USES_BOTH
+WEAPON.BFG
	States
	{
	MineThrowFlash:
		BFGZ ABCD 1
		TNT1 D 18
		BFGZ DCBA 1
		Stop
	KickingFlash:
		BFGZ ABCD 1
		BFGZ D 8
		BFGZ DCBA 1
		Stop
	SlideKickingStart:
		BFGZ ABCD 1
		BFGZ D 35
	SlideKickingEnd:
		BFGZ D 2
		BFGZ DCBA 1
		Stop
	v1Ready:
	Ready:
		BFGG A 1 A_WeaponReady(WRF_ALLOWZOOM)
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		Loop
	Deselect:
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Takeinventory("WeaponSpecialAction",0)
		TNT1 A 0 A_Takeinventory("TossMine",0)
		TNT1 A 0 A_Takeinventory("PlaceMine",0)
		TNT1 A 0 A_Takeinventory("Kicking",0)
		TNT1 A 0 A_StopSound(CHAN_5)
		BFGS FGHIJ 1
		TNT1 A 0 A_Lower
		Wait
	Select:
		TNT1 A 0 A_PlaySound("BFGREADY",CHAN_AUTO)
		BFGS ABCDE 1 Offset(0,32)
		TNT1 A 0 A_Raise
		Wait
	Fire:
		TNT1 A 0 A_JumpIf(waterlevel > 1,"Ready")
		TNT1 A 0 A_JumpIfInventory("CELL",40,2)
		TNT1 A 0 A_Print("Not Enough Ammo!",1)
		Goto Ready
		TNT1 A 0 A_AlertMonsters
		BFGG A 1 A_BFGSound
		BFGC ABCDABCDABCDABCDABCDABCD 1 Bright
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle5",20,5,40)
		TNT1 A 0 A_FireBFG
		TNT1 A 0 A_TakeInventory("CELL",39,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_ZoomFactor(0.9)
		TNT1 A 0 A_SetPitch(pitch-4, SPF_INTERPOLATE)
		BFGF AB 1 Bright
		TNT1 A 0 A_ZoomFactor(1.0)
		BFGF C 1 Bright
		BFGF GGGHIJ 1 A_SetPitch(pitch+0.5,SPF_INTERPOLATE)
		BFGG A 17
		BFGG A 1 A_ReFire
		Goto Ready
	Spawn:
		BFUG A -1
		Stop
	AltFire:
		TNT1 A 0 A_JumpIf(waterlevel > 1,"Ready")
		TNT1 A 0 A_JumpIfInventory("IsTacticalClass",1,2)
		TNT1 A 0 A_JumpIfInventory("IsModern",1,1)
		Goto Ready
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_Playsound("weapons/bfgf2",CHAN_WEAPON)
		BFGC ABCDAFCDEFCHABGDAFCDH 1 Bright
	AltHold:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("BBFGHOLD",CHAN_5,1,TRUE)
		TNT1 A 0 A_FireCustomMissile("BFGBetaBall",random(8,-8),1,random(5,-5),random(5,-5),0,random(5,-5))
		TNT1 A 0 A_FireCustomMissile("BFGBetaBall2",random(8,-8),1,random(5,-5),random(5,-5),0,random(5,-5))
		TNT1 A 0 A_SetPitch(pitch+random(-1,1),SPF_INTERPOLATE)
		TNT1 A 0 A_SetAngle(angle+random(-1,1),SPF_INTERPOLATE)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle5",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle5",20,5,22)
		BFGF A 1 Bright
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle4",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle4",20,5,22)
		BFGF D 1 Bright
		TNT1 A 0 A_FireCustomMissile("BFGBetaBall",random(8,-8),1,random(5,-5),random(5,-5),0,random(5,-5))
		TNT1 A 0 A_FireCustomMissile("BFGBetaBall2",random(8,-8),1,random(5,-5),random(5,-5),0,random(5,-5))
		TNT1 A 0 A_SetPitch(pitch+random(-1,1),SPF_INTERPOLATE)
		TNT1 A 0 A_SetAngle(angle+random(-1,1),SPF_INTERPOLATE)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle5",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle5",20,5,22)
		BFGF B 1 Bright
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle4",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle4",20,5,22)
		BFGF E 1 Bright
		TNT1 A 0 A_FireCustomMissile("BFGBetaBall",random(8,-8),1,random(5,-5),random(5,-5),0,random(5,-5))
		TNT1 A 0 A_FireCustomMissile("BFGBetaBall2",random(8,-8),1,random(5,-5),random(5,-5),0,random(5,-5))
		TNT1 A 0 A_SetPitch(pitch+random(-1,1),SPF_INTERPOLATE)
		TNT1 A 0 A_SetAngle(angle+random(-1,1),SPF_INTERPOLATE)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle5",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle5",20,5,22)
		BFGF C 1 Bright
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle4",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle4",20,5,22)
		BFGF F 1 Bright
		TNT1 A 0 A_FireCustomMissile("BFGBetaBall",random(8,-8),1,random(5,-5),random(5,-5),0,random(5,-5))
		TNT1 A 0 A_FireCustomMissile("BFGBetaBall2",random(8,-8),1,random(5,-5),random(5,-5),0,random(5,-5))
		TNT1 A 0 A_SetPitch(pitch+random(-1,1),SPF_INTERPOLATE)
		TNT1 A 0 A_SetAngle(angle+random(-1,1),SPF_INTERPOLATE)
		TNT1 A 0 A_ZoomFactor(0.9)
		BFGF JI 1
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_ReFire
		BFGF GGGHIJ 1
		TNT1 A 0 A_StopSound(CHAN_5)
		Goto Ready
	}
}

Actor 64BFGBall : BFGBall Replaces BFGBall
{
Scale 0.75
Alpha 1
Renderstyle Add
DamageType "BFG"
Decal "BFGLIGHTNING"
Obituary "$FO_OB_MPBFG_BOOM"
+EXTREMEDEATH
	states
	{
	Spawn:
		TNT1 A 0 NoDelay A_JumpIf(waterlevel > 1,"DeathSplash")
		BFS1 AABB 1 Bright A_SpawnItemEx("BFGFlare")
		Loop
	DeathSplash:
		TNT1 A 0 A_CustomMissile("GiantWaterRicochet")
		TNT1 A 0 A_CustomMissile("WaterParticleXSpawner",0,0,random(0,360),2,random(80,110))
	Death:
		TNT1 A 0 A_Stop
		TNT1 AAAAA 0 A_CustomMissile("ExplosionParticleGreenHeavy",0,0,random(0,360),2,random(10,70))
		TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleGreenHeavySmall",0,0,random(0,360),2,random(20,50))
		TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleGreenVeryFast",0,0,random(0,360),2,random(20,40))
		TNT1 AAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleGreen",0,0,random(0,360),2,random(0,360))
		TNT1 AAAA 0 A_CustomMissile("ExplosionSmokeFast22",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_SpawnItemEx("DetectFloorCrater")
		TNT1 A 0 A_SpawnItemEx("DetectCeilCrater")
		TNT1 A 0 A_SpawnItemEx("ExplosionAirDisplacementEffect")
		TNT1 A 0 Radius_Quake(4,8,0,12,0)
		TNT1 A 0 A_Explode(200,180,0)
		TNT1 A 0 A_SpawnItemEx("LiquidExplosionEffectSpawner")
		TNT1 A 0 A_SetScale(1.3,1.3)
		BFE1 A 8 BRIGHT
		BFE1 B 7 BRIGHT A_FadeOut(0.14)
		BFE1 C 3 BRIGHT A_FadeOut(0.14)
		TNT1 A 0 A_BFGSpray("BFGExtra",50)
		BFE1 D 3 BRIGHT A_FadeOut(0.14)
		BFE1 E 3 BRIGHT A_FadeOut(0.14)
		BFE1 F 3 BRIGHT A_FadeOut(0.14)
		Stop
	}
}

Actor 64BFGExtra : BFGExtra Replaces BFGExtra
{
Renderstyle Normal
DamageType "BFG"
+EXTREMEDEATH
	states
	{
	Spawn:
		BFE2 ABCDEF 3 Bright
		Stop
	}
}

Actor BFGBetaBall
{
Scale 0.6
Damage 9
Radius 6
Height 8
Speed 25
Gravity 0.5
RenderStyle Add
Alpha 0.75
DeathSound "weapons/plasmax"
DamageType "Extreme"
Obituary "%o was obliterated by %k's BFG."
SeeSound ""
Decal "MedScorch"
PROJECTILE
+BOUNCEONFLOORS
-NOGRAVITY
BounceFactor 0.95
ReactionTime 20
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_JumpIf(waterlevel > 1,"Death")
		GRNB ACB 2 Bright Light("BETABALL1_X3")
		TNT1 A 0 A_Countdown
		Loop
	Death:
		TNT1 A 0 A_Stop
		TNT1 A 0 A_NoGravity
		TNT1 A 0 A_PlaySound("DSFIRXPL",CHAN_ITEM)
		TNT1 AAAAA 0 A_CustomMissile("explosionParticleGreenHeavySmall",0,0,random(0,360),2,random(0,180))
		TNT1 AAAAAAAAAA 0 A_CustomMissile("ExplosionParticleGreen",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_Explode(100,70,0)
		TNT1 A 0 A_SpawnItemEx("DetectFloorCraterMed")
		TNT1 A 0 A_SpawnItemEx("DetectCeilCraterMed",0,0,4)
		BFE2 ABCD 3 Bright
		TNT1 A 0 A_SpawnItemEx("LiquidExplosionEffectSpawner")
		TNT1 A 1 A_CustomMissile("PlasmaSmoke",1,0,random(0,360),2,random(0,160))
		Stop
	}
}

Actor BFGBetaBall2 : BFGBetaBall
{
Damage 7
Speed 30
Gravity 0.4
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_JumpIf(waterlevel > 1,"Death")
		LAZR AB 2 Bright Light("BETABALL2_X3")
		TNT1 A 0 A_Countdown
		Loop
	Death:
		TNT1 A 0 A_Stop
		TNT1 A 0 A_NoGravity
		TNT1 A 0 A_PlaySound("DSFIRXPL",CHAN_ITEM)
		TNT1 AAAAA 0 A_CustomMissile("ExplosionParticleHeavy",0,0,random(0,360),2,random(0,180))
		TNT1 AAAAAAAAAA 0 A_CustomMissile("ExplosionParticleLaser",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_Explode(80,100,0)
		TNT1 A 0 A_SpawnItemEx("DetectFloorCraterMed")
		TNT1 A 0 A_SpawnItemEx("DetectCeilCraterMed")
		REDB ABCD 3 Bright
		TNT1 A 0 A_SpawnItemEx("LiquidExplosionEffectSpawner")
		TNT1 A 1 A_CustomMissile("PlasmaSmoke",1,0,random(0,360),2,random(0,160))
		Stop
	}
}