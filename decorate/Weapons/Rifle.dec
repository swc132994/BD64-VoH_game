Actor RifleSpawner : CustomInventory 19120
{
//$category "Game/BD64_Weapons"
Scale 0.82
Tag "$FO_TAG_RIFLE"
Inventory.PickupMessage "$BD64_GOTRIFLE"
Inventory.PickupSound "WPICK"
	States
	{
	Spawn:
		ARSD A 2
		ARSD A 0 ACS_NamedExecuteAlways("DM_SpawnACS")
		ARSD A 2
		ARSD A 0 A_JumpIfInventory("ISDM",1,"DMSpawn")
		ARSD A 1
		Loop
	Pickup:
		TNT1 A 0 A_JumpIfInventory("IsTacticalClass",1,"PickupTactical")
		TNT1 A 0 A_JumpIfInventory("IsModern",1,"PickupTactical")	
		TNT1 A 0 A_GiveInventory("64Rifle",1)
		Stop
	PickupTactical:
		TNT1 A 0 A_GiveInventory("64RifleTactical",1)
		Stop		
	SpawnChaingun:
		TNT1 A 1 A_SpawnItemEx("ChaingunSpawner",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERSPECIAL|SXF_CLEARCALLERTID,0,tid)
		Stop
	DMSpawn:
		TNT1 A 0 A_ChangeFlag("THRUACTORS",TRUE)
		TNT1 A 0 A_SpawnItemEx("TeleportationFog")
		TNT1 A 0 A_SpawnItemEx("64Rifle")
		TNT1 A 0 A_SpawnItemEx("64RifleTactical")
		TNT1 A 1050
		Loop
	}
}

Actor 64Rifle : DoomWeapon
{
Scale 0.82
Weapon.SlotNumber 4
Weapon.SlotPriority 100
Weapon.SelectionOrder 800
Weapon.AmmoType "64HighCaliberBox"
Weapon.AmmoUse 1
Weapon.AmmoGive 10
Inventory.PickupMessage "$BD64_GOTRIFLE"
Obituary "$FO_OB_MPRIFLE"
Tag "$FO_TAG_RIFLE"
VisibleToPlayerClass "64DoomPlayer"
Inventory.ForbiddenTo "Tactical64DoomPlayer","Tactical64DoomPlayer_p","Modern"
+INVENTORY.RESTRICTABSOLUTELY
	States
	{
	Spawn:
		ARSD A -1
		Stop
	Ready:
		ARSG A 1 A_WeaponReady(WRF_ALLOWZOOM)
		Wait
	Select:
		ARSG A 1 A_Raise
		Wait
	Deselect:
		ARSG A 1 A_Lower
		Wait
	Fire:
		ARSF A 1 Bright A_FireBullets(random(0,6),random(0,6),-1,8,"RifleBulletPuff")
		TNT1 A 0 A_FireCustomMissile("GunFireSmoke",0,0)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,22)
		TNT1 A 0 A_FireCustomMissile("Tracer",random(-3,3),0,-1,-12,0,random(-2,2))
		TNT1 A 0 A_PlaySound("RIFLE",CHAN_WEAPON)
		ARSF BC 1
		TNT1 A 0 A_Firecustommissile("50CaseSpawn",0,0,2,-16)
		ARSG D 1
		TNT1 A 0 A_Refire
		ARSG E 1
		ARSG F 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		Goto Ready
	}
}

Actor RifleAmmo : Ammo
{
Inventory.Amount 0
Inventory.MaxAmount 31
Ammo.BackpackAmount 0
Ammo.BackpackMaxAmount 31
Inventory.Icon "ARSDA0"
+IGNORESKILL
}

Actor 64RifleTactical : Brutal64Weapon
{
Scale 0.82
Weapon.SlotNumber 4
Weapon.SlotPriority 100
Weapon.SelectionOrder 800
Weapon.AmmoType1 "64HighCaliberBox"
Weapon.AmmoUse1 1
Weapon.AmmoGive1 20
Weapon.AmmoType2 "RifleAmmo"
Weapon.AmmoUse2 1
Weapon.AmmoGive2 0
Inventory.PickupMessage "$BD64_GOTRIFLE"
Obituary "$FO_OB_MPRIFLE"
Tag "$FO_TAG_RIFLE"
	States
	{
	MineThrowFlash:
		ARSS EFGH 1
		TNT1 A 18
		ARSS ABCD 1
		Stop
	KickingFlash:
		ARSK BCDE 1
		ARSK F 8
		ARSK EDCB 1
		Stop
	SlideKickingStart:
		ARSK BCDE 1
		ARSK F 35
	SlideKickingEnd:
		ARSK F 2
		ARSK EDCB 1
		Stop
	Spawn:
		ARSD A -1
		Stop
	v1Ready:
	Ready:
		ARSG A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		Loop
	NoAmmo:
		ARSG A 1
		ARSG A 10 A_PlaySound("DRYFIRE",CHAN_WEAPON)
		Goto Ready
	LetsGetStarted:
		TNT1 A 0 A_TakeInventory("StartWeapon")
		TNT1 A 9 A_WeaponReady(WRF_ALLOWZOOM)
		PUN6 ABCD 1	A_WeaponReady(WRF_ALLOWZOOM)
		PUNS DE 2 A_WeaponReady(WRF_ALLOWZOOM)
		PUNS FG 1 A_WeaponReady(WRF_ALLOWZOOM)
		TNT1 A 0 A_PlaySound("Knuckled",CHAN_VOICE)
		PUNS J 4 A_WeaponReady(WRF_ALLOWZOOM)
		PUNS IHG 1 A_WeaponReady(WRF_ALLOWZOOM)
		PUNS KL 2 A_WeaponReady(WRF_ALLOWZOOM)
		PUNS MN 1 A_WeaponReady(WRF_ALLOWZOOM)
		TNT1 A 0 A_PlaySound("Knuckled",CHAN_VOICE)
		PUNS Q 4 A_WeaponReady(WRF_ALLOWZOOM)
		PUNS PON 1 A_WeaponReady(WRF_ALLOWZOOM)
		PUN6 DCBA 1	A_WeaponReady(WRF_ALLOWZOOM)
	WarmUp:
		TNT1 A 0 A_TakeInventory("NeverSelectedRifle")
		ARSG S 1 Offset(-56,53)
		ARSG S 1 Offset(-48,39)
		ARSG S 1 Offset(-38,34)
		ARSG S 1 Offset(-24,32)
		ARSG ST 1 A_WeaponReady(WRF_ALLOWZOOM)
		ARSG T 6 A_WeaponReady(WRF_ALLOWZOOM)
		TNT1 A 0 A_PlaySound("Rifle/MagIn",CHAN_AUTO)
		ARS0 CDEFG 1 A_WeaponReady(WRF_ALLOWZOOM|WRF_NOFIRE)
		ARS0 G 8 A_WeaponReady(WRF_ALLOWZOOM|WRF_NOFIRE)
		ARS0 HIJKLMN 1 A_WeaponReady(WRF_ALLOWZOOM|WRF_NOFIRE)
		TNT1 A 0 A_PlaySound("Rifle/BoltIn",CHAN_WEAPON)
		ARS0 OPQRST 1 A_WeaponReady(WRF_ALLOWZOOM|WRF_NOFIRE)
		TNT1 A 0 A_PlaySound("Rifle/BoltOut",CHAN_WEAPON)
		ARS0 SRQPONMKJIH 1 A_WeaponReady(WRF_ALLOWZOOM|WRF_NOFIRE)
		ARSG TSRQPO 1 A_WeaponReady(WRF_ALLOWZOOM|WRF_NOFIRE)
		ARSG NMLKJIH 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM|WRF_NOFIRE)
		Goto Ready
	Select:
		TNT1 A 0 A_JumpIfInventory("StartWeapon",1,"LetsGetStarted")
		TNT1 A 0 A_JumpIfInventory("NeverSelectedRifle",1,"WarmUp")
		TNT1 A 0 A_PlaySound("RIFBOLT",CHAN_AUTO)
		ARSS ABCD 1 Offset(0,32)
		TNT1 A 0 A_Raise
		Wait
	Fidget:
		ARSG HIJKLMNOPQRST 1 A_WeaponReady(WRF_ALLOWZOOM|WRF_NOFIRE|WRF_NOBOB)
		ARS0 HIJ 1 A_WeaponReady(WRF_ALLOWZOOM|WRF_NOFIRE|WRF_NOBOB)
		TNT1 A 0 A_GiveInventory("IsFidgeting",1)
	CountinueFidget:	
		ARS0 KLMN 1 A_WeaponReady(WRF_ALLOWZOOM|WRF_NOFIRE|WRF_NOBOB)
		TNT1 A 0 A_PlaySound("Rifle/BoltIn",CHAN_WEAPON)
		ARS0 OPPPPP 1 A_WeaponReady(WRF_ALLOWZOOM|WRF_NOFIRE|WRF_NOBOB)
		TNT1 A 0 A_GiveInventory("HoldFidget",1)
	HoldingFidget:	
		ARS0 P 2 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM|WRF_NOFIRE)
		TNT1 A 0 A_TakeInventory("HoldFidget",1)
		TNT1 A 0 A_PlaySound("Rifle/BoltOut",CHAN_WEAPON)
		ARS0 ONMKJIH 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM|WRF_NOFIRE|WRF_NOBOB)
		TNT1 A 0 A_TakeInventory("IsFidgeting",1)
		ARSG TSRQPO 1 A_WeaponReady(WRF_ALLOWZOOM|WRF_NOFIRE|WRF_NOBOB)
		ARSG NMLKJIH 1 A_WeaponReady(WRF_ALLOWZOOM|WRF_NOFIRE|WRF_NOBOB)
		Goto Ready
	ADSFidget:
		AR2G HIJ 1
		AR2G K 3
		AR2G L 1 A_PlaySound("Rifle/BoltIn",CHAN_WEAPON)
		AR2G MN 2
		AR2G O 4
		TNT1 A 0 A_PlaySound("Rifle/BoltOut",CHAN_WEAPON)
		AR2G NML 2
		AR2G KJIH 1
		Goto Ready2
	Deselect:
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Takeinventory("WeaponSpecialAction",0)
		TNT1 A 0 A_Takeinventory("RifleBolt",1)
		TNT1 A 0 A_TakeInventory("HoldFidget",0)
		TNT1 A 0 A_TakeInventory("IsFidgeting",0)
		TNT1 A 0 A_Takeinventory("TossMine",0)
		TNT1 A 0 A_Takeinventory("PlaceMine",0)
		TNT1 A 0 A_Takeinventory("Kicking",0)
		TNT1 A 0 A_Takeinventory("Zoomed",0)
		ARSS EFGH 1
		TNT1 A 0 A_Lower
		Wait
	Fire:
		TNT1 A 0 A_JumpIfInventory("NoReloading",1,"NoReload")
		TNT1 A 0 A_JumpIfInventory("RifleAmmo",1,1)
		Goto Reload
		TNT1 A 0 A_Takeinventory("RifleAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_JumpIfInventory("RifleAmmo",9,"ResumeFire")
		TNT1 A 0 ACS_NamedExecute("LowAmmoSFX")
		Goto ResumeFire
	NoReload:
		TNT1 A 0 A_JumpIfInventory("64HighCaliberBox",1,1)
		Goto NoAmmo
		TNT1 A 0 A_Takeinventory("64HighCaliberBox",1,TIF_NOTAKEINFINITE)
	ResumeFire:
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"FireZoomed")
		TNT1 A 0 A_SetPitch(pitch-1.2)
		TNT1 A 0 A_FireCustomMissile("GunFireSmoke",0,0)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,22)
		TNT1 A 0 A_FireCustomMissile("Tracer",random(-3,3),0,-1,-12,0,random(-2,2))
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("RIFLE",CHAN_WEAPON)
		ARSF A 1 Bright A_FireBullets(random(0,6),random(0,6),1,8,"RifleBulletPuff",FBF_NORANDOM)
		ARSF BC 1 A_SetPitch(pitch+0.4,SPF_INTERPOLATE)
		TNT1 A 0 A_Firecustommissile("50CaseSpawn",0,0,2,-16)
		ARSG D 1 A_SetAngle(angle + random(-1,1))
		TNT1 A 0 A_Refire
		ARSG E 1
		ARSG F 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		Goto Ready
	Reload:
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"ADSReload")
		TNT1 A 0 A_JumpIfInventory("HoldFidget",1,"HoldingFidget")
		TNT1 A 0 A_JumpIfInventory("IsFidgeting",1,"CountinueFidget")
		TNT1 A 0 A_JumpIfInventory("RifleAmmo",0,"Fidget")
		TNT1 A 0 A_JumpIfInventory("64HighCaliberBox",1,1)
		Goto NoAmmo
		ARSG HIJKLMNOPQRST 1
		TNT1 A 0 A_PlaySound("Rifle/MagOut",CHAN_AUTO)
		TNT1 A 0 A_FireCustomMissile("EmptyClipSpawn",-5,0,8,-30)
		ARSG VWXYZ 1
		ARS0 A 1
		ARS0 B 11
		TNT1 A 0 A_PlaySound("Rifle/MagIn",CHAN_AUTO)
		TNT1 A 0 A_JumpIfInventory("RifleAmmo",1,"InsertBullets2")//30+1 effect
		Goto InsertBullets
	FinishReload:
		ARS0 CDEFG 1
		TNT1 A 0 A_JumpIfInventory("RifleBolt",1,"ReloadEmpty")
		ARSG TSRQPONMLKJIH 1
		Goto Ready
	ReloadEmpty:
		TNT1 A 0 A_TakeInventory("RifleBolt",1)
		ARS0 HIJKLMN 1
		TNT1 A 0 A_PlaySound("Rifle/BoltIn",CHAN_WEAPON)
		ARS0 OPQRST 1
		TNT1 A 0 A_PlaySound("Rifle/BoltOut",CHAN_WEAPON)
		ARS0 SRQPONMKJIH 1
		ARSG TSRQPO 1
		ARSG NMLJKIH 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		Goto Ready
	InsertBullets:
		TNT1 A 0 A_JumpIfInventory("RifleAmmo",30,"FinishReloadChecking")
		TNT1 A 0 A_JumpIfInventory("64HighCaliberBox",1,1)
		Goto FinishReloadChecking
		TNT1 A 0 A_Giveinventory("RifleAmmo",1)
		TNT1 A 0 A_GiveInventory("RifleBolt",1)
		TNT1 A 0 A_Takeinventory("64HighCaliberBox",1,TIF_NOTAKEINFINITE)
		Goto InsertBullets
		TNT1 A 0 A_Refire
		Goto FinishReloadChecking
	InsertBullets2:
		TNT1 A 0 A_JumpIfInventory("RifleAmmo",0,"FinishReloadChecking")
		TNT1 A 0 A_JumpIfInventory("64HighCaliberBox",1,1)
		Goto FinishReloadChecking
		TNT1 A 0 A_Giveinventory("RifleAmmo",1)
		TNT1 A 0 A_Takeinventory("64HighCaliberBox",1,TIF_NOTAKEINFINITE)
		Goto InsertBullets2
		TNT1 A 0 A_Refire
		Goto FinishReloadChecking
	FinishReloadChecking:
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"FinishADSReload")
		Goto FinishReload
	ADSReload:
		AR2G A 1 A_WeaponReady(WRF_ALLOWZOOM)
		TNT1 A 0 A_JumpIfInventory("RifleAmmo",0,"ADSFidget")
		TNT1 A 0 A_JumpIfInventory("64HighCaliberBox",1,1)
		Goto Ready2
		TNT1 A 0 A_PlaySound("Rifle/MagOut",CHAN_AUTO)
		AR2G AB 1
		TNT1 A 0 A_FireCustomMissile("EmptyClipSpawn",-5,0,8,-14)
		AR2G CDEF 1
		AR2G G 1 Offset(0,33)
		AR2G G 1 Offset(0,34)
		AR2G G 1 Offset(0,35)
		AR2G G 1 Offset(0,36)
		AR2G GG 4
		AR2G G 1 Offset(0,37) A_PlaySound("Rifle/MagIn",CHAN_AUTO)
		TNT1 A 0 A_JumpIfInventory("RifleAmmo",1,"InsertBullets2")//30+1 effect
		Goto InsertBullets
	FinishADSReload:
		AR2G G 1 Offset(0,38)
		AR2G G 1 Offset(0,36)
		AR2G G 3
		AR2G G 1 Offset(0,35)
		AR2G G 1 Offset(0,34)
		AR2G G 1 Offset(0,33)
		AR2G F 1 Offset(0,32)
		AR2G EDC 1
		TNT1 A 0 A_JumpIfInventory("RifleBolt",1,"ADSReloadEmpty")
		AR2G BA 1
		Goto Ready2
	ADSReloadEmpty:
		TNT1 A 0 A_TakeInventory("RifleBolt",1)
		AR2G HIJ 1
		AR2G K 3
		AR2G L 1 A_PlaySound("Rifle/BoltIn",CHAN_WEAPON)
		AR2G MNOO 2
		AR2G O 1 A_PlaySound("Rifle/BoltOut",CHAN_WEAPON)
		AR2G ONML 2
		AR2G KJIH 1
		Goto Ready2
	AltFire:
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"NoAim")
		TNT1 A 0 A_Giveinventory("Zoomed",1)
		TNT1 A 0 A_ZoomFactor(2.0)
		ARSZ ABC 1
		Goto Ready2
	NoAim:
		ARSZ CBA 1
		TNT1 A 0 A_Takeinventory("Zoomed",0)
		TNT1 A 0 A_ZoomFactor(1.0)
		Goto Ready
	Ready2:
		AR2G A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		Loop
	FireZoomed:
		TNT1 A 0 A_FireCustomMissile("Tracer",0,0,0,-6)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,22)
		TNT1 A 0 A_Firecustommissile("50CaseSpawn",0,0,4,-5)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("RIFLE",CHAN_WEAPON)
		AR2F A 1 Bright A_FireBullets(0.0,0.0,1,8,"RifleBulletPuff",0)
		AR2F B 1 A_SetPitch(-1.0 + pitch)
		AR2F C 1 A_SetPitch(0.5 + pitch)
		AR2G A 1 A_SetPitch(0.5 + pitch)
		TNT1 A 0 A_Refire
		AR2G A 5 A_WeaponReady(WRF_ALLOWZOOM)
		Goto Ready2
	}
}
